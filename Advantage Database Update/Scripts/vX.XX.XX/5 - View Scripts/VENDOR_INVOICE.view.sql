CREATE VIEW [dbo].[VENDOR_INVOICE]
WITH SCHEMABINDING 
AS

SELECT
	[AccountPayableID] = A.AP_ID,
	[SequenceNumber] = A.AP_SEQ,
	[VendorCode] = A.VN_FRL_EMP_CODE,
	[VendorName] = V.VN_NAME,
	[VendorCategory] = V.VN_CATEGORY,
	[OfficeCode] = A.OFFICE_CODE, 
	[OfficeName] = O.OFFICE_NAME,
	[MarketCode] = V.MARKET_CODE, 
	[MarketDescription] = V.MARKET_DESC,
	[InvoiceNumber] = A.AP_INV_VCHR,
	[InvoiceDate] = A.AP_INV_DATE,
	[EntryDate] = A.CREATE_DATE,
	[InvoiceDescription] = A.AP_DESC,
	[InvoiceAmount] = A.AP_INV_AMT + COALESCE(A.AP_SHIPPING, 0) + COALESCE(A.AP_SALES_TAX, 0),
	[PaymentHold] = A.PAYMENT_HOLD,
	[Is1099Invoice] = A.FLAG_1099,
	[VendorOfficeCode] = V.OFFICE_CODE,
    [VendorPayToName] = VPT.VN_NAME,
    [QuickBooks] = CAST(CASE WHEN A.QUICKBOOKS_BILL_ID IS NOT NULL THEN 1 ELSE 0 END as BIT)
FROM [dbo].AP_HEADER A
	INNER JOIN 
		(SELECT VN_CODE, VN_NAME, VN_CATEGORY, M.MARKET_CODE, M.MARKET_DESC, OFFICE_CODE, VN_PAY_TO_CODE
		FROM [dbo].VENDOR 
		LEFT OUTER JOIN [dbo].MARKET M ON VENDOR.MARKET_CODE=M.MARKET_CODE) AS V ON A.VN_FRL_EMP_CODE = V.VN_CODE 
	LEFT OUTER JOIN [dbo].OFFICE O ON A.OFFICE_CODE=O.OFFICE_CODE
    INNER JOIN dbo.VENDOR VPT ON VPT.VN_CODE = V.VN_PAY_TO_CODE
WHERE (A.ARCHIVE_FLAG IS NULL OR A.ARCHIVE_FLAG = 0)
AND A.MODIFY_FLAG IS NULL
AND A.DELETE_FLAG IS NULL

GO
