IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'[dbo].[advsp_agile_sprint_check_jobs]') AND OBJECTPROPERTY(id, N'IsProcedure') = 1)
    DROP PROCEDURE [dbo].[advsp_agile_sprint_check_jobs]
GO

CREATE PROCEDURE [dbo].[advsp_agile_sprint_check_jobs]
@SPRINT_HDR_ID INT
AS
/*=========== QUERY ===========*/
BEGIN

	BEGIN
		
		--	VARIABLES
		BEGIN
			DECLARE
				@BOARD_ID INT,
				@INCL_ALL_JOBS BIT
		END
		--	INIT
		BEGIN
			SELECT @BOARD_ID = BOARD_ID FROM SPRINT_HDR WHERE ID = @SPRINT_HDR_ID;
			SELECT @INCL_ALL_JOBS = ISNULL(INCL_ALL_JOBS, 0) FROM BOARD WHERE ID = @BOARD_ID;
		END		

		--	CLOSED JOBS		
		BEGIN
			-- BY JOB
			BEGIN
				DELETE SE
				FROM 
					SPRINT_EMPLOYEE SE
					INNER JOIN SPRINT_DTL SD ON SD.ID = SE.SPRINT_DTL_ID AND SD.ALERT_ID = SE.ALERT_ID
					INNER JOIN ALERT A ON SD.ALERT_ID = A.ALERT_ID 
					INNER JOIN JOB_COMPONENT JC ON A.JOB_NUMBER = JC.JOB_NUMBER AND A.JOB_COMPONENT_NBR = JC.JOB_COMPONENT_NBR
				WHERE 
					SD.SPRINT_HDR_ID = @SPRINT_HDR_ID
					AND (JC.JOB_PROCESS_CONTRL IN (6, 12))

				DELETE SD
				FROM 
					SPRINT_DTL SD 
					INNER JOIN ALERT A ON SD.ALERT_ID = A.ALERT_ID 
					INNER JOIN JOB_COMPONENT JC ON A.JOB_NUMBER = JC.JOB_NUMBER AND A.JOB_COMPONENT_NBR = JC.JOB_COMPONENT_NBR
				WHERE 
					SD.SPRINT_HDR_ID = @SPRINT_HDR_ID
					AND (JC.JOB_PROCESS_CONTRL IN (6, 12))

				DELETE BJ
				FROM
					BOARD_JOB BJ
					INNER JOIN JOB_COMPONENT JC ON BJ.JOB_NUMBER = JC.JOB_NUMBER AND BJ.JOB_COMPONENT_NBR = JC.JOB_COMPONENT_NBR
				WHERE 
					BJ.BOARD_ID = @BOARD_ID
					AND (JC.JOB_PROCESS_CONTRL IN (6, 12));
			END
			-- BY SCHEDULE
			BEGIN
				DELETE SE
				FROM 
					SPRINT_EMPLOYEE SE
					INNER JOIN SPRINT_DTL SD ON SD.ID = SE.SPRINT_DTL_ID AND SD.ALERT_ID = SE.ALERT_ID
					INNER JOIN ALERT A ON SD.ALERT_ID = A.ALERT_ID 
					INNER JOIN JOB_TRAFFIC JT ON A.JOB_NUMBER = JT.JOB_NUMBER AND A.JOB_COMPONENT_NBR = JT.JOB_COMPONENT_NBR
				WHERE 
					SD.SPRINT_HDR_ID = @SPRINT_HDR_ID
					AND (NOT JT.COMPLETED_DATE IS NULL)
					AND (A.ALERT_CAT_ID = 71 AND A.TASK_SEQ_NBR IS NOT NULL)
					;
				DELETE SD
				FROM 
					SPRINT_DTL SD 
					INNER JOIN ALERT A ON SD.ALERT_ID = A.ALERT_ID 
					INNER JOIN JOB_TRAFFIC JT ON A.JOB_NUMBER = JT.JOB_NUMBER AND A.JOB_COMPONENT_NBR = JT.JOB_COMPONENT_NBR
				WHERE 
					SD.SPRINT_HDR_ID = @SPRINT_HDR_ID
					AND (NOT JT.COMPLETED_DATE IS NULL)
					AND (A.ALERT_CAT_ID = 71 AND A.TASK_SEQ_NBR IS NOT NULL)
					;
			END
		END
		--	JOBS REMOVED FROM BOARD
		IF @INCL_ALL_JOBS = 0
		BEGIN

			DELETE SE
			FROM 
				SPRINT_HDR SH 
				INNER JOIN SPRINT_DTL SD ON SH.ID = SD.SPRINT_HDR_ID
				INNER JOIN ALERT A ON SD.ALERT_ID = A.ALERT_ID
				INNER JOIN SPRINT_EMPLOYEE SE ON SE.SPRINT_DTL_ID = SD.ID AND SE.ALERT_ID = A.ALERT_ID
				LEFT OUTER JOIN BOARD_JOB BJ ON BJ.BOARD_ID = SH.BOARD_ID AND BJ.JOB_NUMBER = A.JOB_NUMBER AND BJ.JOB_COMPONENT_NBR = A.JOB_COMPONENT_NBR
			WHERE 
				SH.BOARD_ID = @BOARD_ID
				AND SH.ID = @SPRINT_HDR_ID	
				AND BJ.ID IS NULL;

			DELETE SD
			FROM 
				SPRINT_HDR SH 
				INNER JOIN SPRINT_DTL SD ON SH.ID = SD.SPRINT_HDR_ID
				INNER JOIN ALERT A ON SD.ALERT_ID = A.ALERT_ID
				LEFT OUTER JOIN BOARD_JOB BJ ON BJ.BOARD_ID = SH.BOARD_ID AND BJ.JOB_NUMBER = A.JOB_NUMBER AND BJ.JOB_COMPONENT_NBR = A.JOB_COMPONENT_NBR
			WHERE 
				SH.BOARD_ID = @BOARD_ID
				AND SH.ID = @SPRINT_HDR_ID	
				AND BJ.ID IS NULL;

		END

	END

END
/*=========== QUERY ===========*/