IF EXISTS ( SELECT * FROM dbo.sysobjects WHERE id = OBJECT_ID(N'[dbo].[advsp_alert_assignment_template_add_employee_to_state]') AND OBJECTPROPERTY(id, N'IsProcedure') = 1)
BEGIN
    DROP PROCEDURE [dbo].[advsp_alert_assignment_template_add_employee_to_state];
END
GO
CREATE PROCEDURE [dbo].[advsp_alert_assignment_template_add_employee_to_state] 
@ALERT_ID INT,
@ALRT_NOTIFY_HDR_ID INT,
@ALERT_STATE_ID INT,
@EMP_CODE VARCHAR(6)
AS
/*=========== QUERY ===========*/
BEGIN
	--	VARS
	BEGIN
		DECLARE
			@CURRENT_ALERT_STATE_ID INT,
			@HOURS_ALLOWED DECIMAL (7, 2)
		;
	END
	--	INIT
	BEGIN
		IF @ALRT_NOTIFY_HDR_ID IS NULL
		BEGIN
			SELECT
				@ALRT_NOTIFY_HDR_ID = A.ALRT_NOTIFY_HDR_ID
			FROM
				ALERT A WITH(NOLOCK)
			WHERE
				A.ALERT_ID = @ALERT_ID
			;
		END
		SELECT
			@CURRENT_ALERT_STATE_ID = A.ALERT_STATE_ID,
			@HOURS_ALLOWED = A.HRS_ALLOWED
		FROM
			ALERT A WITH(NOLOCK)
		WHERE
			A.ALERT_ID = @ALERT_ID
		;
	END
	IF @ALRT_NOTIFY_HDR_ID IS NOT NULL
	BEGIN
        --  MAKE SURE ASSIGNEE IS IN TEMPLATE
        BEGIN
            EXEC [dbo].[advsp_alert_assignment_template_add_employee_to_template] @ALERT_ID, @ALRT_NOTIFY_HDR_ID, @ALERT_STATE_ID, @EMP_CODE;
        END
		--	CHECK IF NEED TO ADD RECIPIENT
		IF @ALERT_STATE_ID = @CURRENT_ALERT_STATE_ID
		BEGIN
			IF	NOT EXISTS (SELECT 1 FROM ALERT_RCPT WITH(NOLOCK) WHERE ALERT_ID = @ALERT_ID AND EMP_CODE = @EMP_CODE AND CURRENT_NOTIFY = 1 AND ALRT_NOTIFY_HDR_ID = @ALRT_NOTIFY_HDR_ID AND ALERT_STATE_ID = @ALERT_STATE_ID)
				AND NOT EXISTS (SELECT 1 FROM ALERT_RCPT_DISMISSED WITH(NOLOCK) WHERE ALERT_ID = @ALERT_ID AND EMP_CODE = @EMP_CODE AND CURRENT_NOTIFY = 1 AND ALRT_NOTIFY_HDR_ID = @ALRT_NOTIFY_HDR_ID AND ALERT_STATE_ID = @ALERT_STATE_ID)
			BEGIN
				DECLARE
					@MAX_RCPT_ID INT
				;
				SELECT @MAX_RCPT_ID = MAX(ALERT_RCPT_ID) FROM ALERT_RCPT R WITH(NOLOCK) WHERE R.ALERT_ID = @ALERT_ID;
				SELECT @MAX_RCPT_ID = ISNULL(@MAX_RCPT_ID, 0) + 1;
				INSERT INTO ALERT_RCPT WITH(ROWLOCK) (	ALERT_ID, 
														ALERT_RCPT_ID, 
														EMP_CODE, 
														EMAIL_ADDRESS, 
														NEW_ALERT, 
														CURRENT_RCPT, 
														CURRENT_NOTIFY, 
														ALRT_NOTIFY_HDR_ID, 
														ALERT_STATE_ID, 
														HOURS_ALLOWED)
				SELECT
					@ALERT_ID,
					@MAX_RCPT_ID,
					@EMP_CODE,
					E.EMP_EMAIL,
					1,
					NULL,
					1,
					@ALRT_NOTIFY_HDR_ID,
					@ALERT_STATE_ID,
					@HOURS_ALLOWED
				FROM
					EMPLOYEE_CLOAK E WITH(NOLOCK)
				WHERE
					E.EMP_CODE = @EMP_CODE
				;
			END
		END
	END
END
/*=========== QUERY ===========*/