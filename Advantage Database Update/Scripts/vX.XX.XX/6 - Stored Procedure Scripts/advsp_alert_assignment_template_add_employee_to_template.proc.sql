IF EXISTS ( SELECT * FROM dbo.sysobjects WHERE id = OBJECT_ID(N'[dbo].[advsp_alert_assignment_template_add_employee_to_template]') AND OBJECTPROPERTY(id, N'IsProcedure') = 1)
BEGIN
    DROP PROCEDURE [dbo].[advsp_alert_assignment_template_add_employee_to_template];
END
GO
CREATE PROCEDURE [dbo].[advsp_alert_assignment_template_add_employee_to_template] 
@ALERT_ID INT = NULL,
@ALRT_NOTIFY_HDR_ID INT = NULL,
@ALERT_STATE_ID INT = NULL,
@EMP_CODE VARCHAR(6) = NULL
AS
/*=========== QUERY ===========*/
BEGIN
	--	VARS
	BEGIN
		DECLARE
			@CURR_ALRT_NOTIFY_HDR_ID INT,
			@CURR_ALERT_STATE_ID INT,
			@IS_ROUTED BIT
		;
	END
	--	INIT
	BEGIN
		SELECT
			@CURR_ALRT_NOTIFY_HDR_ID = A.ALRT_NOTIFY_HDR_ID,
			@CURR_ALERT_STATE_ID = A.ALERT_STATE_ID,
			@IS_ROUTED = CASE	
							WHEN A.ALRT_NOTIFY_HDR_ID IS NULL AND A.ALERT_STATE_ID IS NULL THEN 0
							ELSE 1
						 END
		FROM
			ALERT A WITH(NOLOCK)
		WHERE
			A.ALERT_ID = @ALERT_ID
		;
		IF @ALRT_NOTIFY_HDR_ID IS NULL OR @ALERT_STATE_ID IS NULL
		BEGIN
			SELECT
				@ALRT_NOTIFY_HDR_ID = @CURR_ALRT_NOTIFY_HDR_ID,
				@ALERT_STATE_ID = @CURR_ALERT_STATE_ID
		END
	END
	IF @IS_ROUTED = 1
	BEGIN
		--	MAKE SURE TEMPLATE EXISTS
		BEGIN
			EXEC [dbo].[advsp_alert_assignment_template_create_custom] @ALERT_ID;
		END
		IF NOT EXISTS (SELECT 1 
					   FROM 
							ALERT_NOTIFY_EMPS_ASSIGNMENT ANEA WITH(NOLOCK) 
					   WHERE
							ANEA.ALERT_ID = @ALERT_ID
							AND ANEA.ALRT_NOTIFY_HDR_ID = @ALRT_NOTIFY_HDR_ID
							AND ANEA.ALERT_STATE_ID = @ALERT_STATE_ID
							AND ANEA.EMP_CODE = @EMP_CODE)
		BEGIN
			INSERT INTO ALERT_NOTIFY_EMPS_ASSIGNMENT (ALERT_ID, ALRT_NOTIFY_HDR_ID, ALERT_STATE_ID, EMP_CODE, IS_DEFAULT, IS_SELECTED)
			VALUES (@ALERT_ID, @ALRT_NOTIFY_HDR_ID, @ALERT_STATE_ID, @EMP_CODE, 0, 1);
		END
        ELSE
        BEGIN
            UPDATE ALERT_NOTIFY_EMPS_ASSIGNMENT WITH(ROWLOCK) SET IS_SELECTED = 1
			WHERE
				ALERT_ID = @ALERT_ID
				AND ALRT_NOTIFY_HDR_ID = @ALRT_NOTIFY_HDR_ID
				AND ALERT_STATE_ID = @ALERT_STATE_ID
				AND EMP_CODE = @EMP_CODE
			;
        END
	END
END
/*=========== QUERY ===========*/
