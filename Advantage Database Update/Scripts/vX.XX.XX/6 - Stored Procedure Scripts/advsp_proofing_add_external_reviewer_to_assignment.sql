IF EXISTS ( SELECT * FROM dbo.sysobjects WHERE id = OBJECT_ID(N'[dbo].[advsp_proofing_add_external_reviewer_to_assignment]') AND OBJECTPROPERTY(id, N'IsProcedure') = 1)
BEGIN
    DROP PROCEDURE [dbo].[advsp_proofing_add_external_reviewer_to_assignment];
END
GO
CREATE PROCEDURE [dbo].[advsp_proofing_add_external_reviewer_to_assignment] 
@ALERT_ID INT,
@PROOFING_EXTERNAL_REVIEWER_ID INT
AS
/*=========== QUERY ===========*/
BEGIN

	IF EXISTS (SELECT 1 FROM ALERT A WITH(NOLOCK) WHERE A.ALERT_ID = @ALERT_ID)
	   AND EXISTS (SELECT 1 FROM PROOFING_X_REVIEWER X WITH(NOLOCK) WHERE X.ID = @PROOFING_EXTERNAL_REVIEWER_ID)	
	BEGIN
		IF NOT EXISTS (
			SELECT
				X.PROOFING_EXTERNAL_REVIEWER_ID
			FROM
				ALERT_RCPT_X_REVIEWER X
			WHERE
				X.ALERT_ID = @ALERT_ID
				AND X.PROOFING_EXTERNAL_REVIEWER_ID = @PROOFING_EXTERNAL_REVIEWER_ID
			UNION
			SELECT
				XD.PROOFING_EXTERNAL_REVIEWER_ID
			FROM
				ALERT_RCPT_X_REVIEWER_DISMISSED XD
			WHERE
				XD.ALERT_ID = @ALERT_ID
				AND XD.PROOFING_EXTERNAL_REVIEWER_ID = @PROOFING_EXTERNAL_REVIEWER_ID
		)
		BEGIN				
			INSERT INTO ALERT_RCPT_X_REVIEWER WITH(ROWLOCK) (ALERT_ID, 
															 PROOFING_EXTERNAL_REVIEWER_ID, 
													         ALRT_NOTIFY_HDR_ID, 
															 ALERT_STATE_ID, 
															 PROOFING_STATUS_ID)
			SELECT
				A.ALERT_ID,
				@PROOFING_EXTERNAL_REVIEWER_ID,
				A.ALRT_NOTIFY_HDR_ID,
				A.ALERT_STATE_ID,
				NULL
			FROM
				ALERT A WITH(NOLOCK)
			WHERE
				A.ALERT_ID = @ALERT_ID
			;
		END
	END
END
/*=========== QUERY ===========*/
