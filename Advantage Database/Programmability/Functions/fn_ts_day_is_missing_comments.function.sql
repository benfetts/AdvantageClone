--DROP FUNCTION [dbo].[fn_ts_day_is_missing_comments]
CREATE FUNCTION [dbo].[fn_ts_day_is_missing_comments] (@ET_ID AS INT, @USER_CODE VARCHAR(100))
RETURNS BIT
AS
BEGIN

	DECLARE 
		@IS_MISSING_COMMENTS BIT, 
		
		@EMP_CODE VARCHAR(6),
		@DATE SMALLDATETIME,
		@AGENCY_COMMENT_REQUIRED SMALLINT,
		@CLIENT_COMMENT_REQUIRED SMALLINT,
		@TS_REQ_CMT_APPR BIT
		
	SET @IS_MISSING_COMMENTS = 0;

	SELECT 
		@EMP_CODE = EMP_TIME.EMP_CODE,
		@DATE = EMP_TIME.EMP_DATE 
	FROM 
		EMP_TIME WITH(NOLOCK)
	WHERE 
		EMP_TIME.ET_ID = @ET_ID;
	
	SELECT @AGENCY_COMMENT_REQUIRED = ISNULL(AGENCY.TIME_COMMENTS_REQ,0) 
	FROM AGENCY WITH(NOLOCK);
	SET @AGENCY_COMMENT_REQUIRED = ISNULL(@AGENCY_COMMENT_REQUIRED,0);

	SELECT @TS_REQ_CMT_APPR = CONVERT(BIT, ISNULL(AGY_SETTINGS.AGY_SETTINGS_VALUE, AGY_SETTINGS.AGY_SETTINGS_DEF)) 
	FROM AGY_SETTINGS WITH(NOLOCK) WHERE AGY_SETTINGS.AGY_SETTINGS_CODE = 'TS_REQ_CMT_APPR';

	IF EXISTS (
	
		SELECT 
			TS.ET_ID, TS.ET_DTL_ID, TS.CL_CODE, TS.EMP_HOURS, TS.COMMENTS
			, CL.REQ_TIME_COMMENT AS CLIENT_REQ_TIME_COMMENT
			, @AGENCY_COMMENT_REQUIRED AS AGENCY_REQ_TIME_COMMENT
		FROM 
			[dbo].[fn_my_ts_get_timesheet](@EMP_CODE, @DATE, @DATE, 'CLIENT', @USER_CODE) AS TS
			LEFT OUTER JOIN CLIENT CL WITH(NOLOCK) ON TS.CL_CODE = CL.CL_CODE
		WHERE
			(CL.REQ_TIME_COMMENT = 1 OR @AGENCY_COMMENT_REQUIRED = 1 OR @TS_REQ_CMT_APPR = 1)
			AND TS.EMP_HOURS > 0
			AND (TS.COMMENTS IS NULL OR CONVERT(VARCHAR(MAX), TS.COMMENTS) = '')
	)
	BEGIN
	
		SET @IS_MISSING_COMMENTS = 1;
		
	END	
			
RETURN @IS_MISSING_COMMENTS

END