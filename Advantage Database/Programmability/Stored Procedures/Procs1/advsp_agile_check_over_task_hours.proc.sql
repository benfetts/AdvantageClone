IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'[dbo].[advsp_agile_check_over_task_hours]') AND OBJECTPROPERTY(id, N'IsProcedure') = 1)
    DROP PROCEDURE [dbo].[advsp_agile_check_over_task_hours]
GO
CREATE PROCEDURE [dbo].[advsp_agile_check_over_task_hours] 
@SPRINT_EMPLOYEE_ID INT,
@HOURS DECIMAL (7,2)
AS
/*=========== QUERY ===========*/
BEGIN
	DECLARE
		@JOB_NUMBER INT,
		@JOB_COMPONENT_NBR SMALLINT,
		@TASK_SEQ_NBR SMALLINT,
		@ALERT_LEVEL VARCHAR(50),
		@TASK_HOURS_ALLOWED DECIMAL (7,2),
		@EMP_HOURS DECIMAL (7,2),
		@RETURN_HOURS DECIMAL (7,2),
		@BEFORE_CHANGE_HOURS DECIMAL (7,2),
		@ALERT_ID INT;
	SELECT @HOURS = ISNULL(@HOURS, 0.00);
	SELECT 
		@ALERT_ID = ALERT_ID,
		@BEFORE_CHANGE_HOURS = ISNULL([HOURS], 0.00)
	FROM 
		SPRINT_EMPLOYEE SE 
	WHERE 
		SE.ID = @SPRINT_EMPLOYEE_ID;
	SELECT
		@JOB_NUMBER = A.JOB_NUMBER,
		@JOB_COMPONENT_NBR = A.JOB_COMPONENT_NBR,
		@TASK_SEQ_NBR = A.TASK_SEQ_NBR,
		@ALERT_LEVEL = A.ALERT_LEVEL
	FROM
		ALERT A WITH(NOLOCK)
	WHERE
		A.IS_WORK_ITEM = 1
		AND A.ALERT_ID = @ALERT_ID;
	IF @ALERT_LEVEL = 'BRD'
	BEGIN
		SELECT 
			@EMP_HOURS = ISNULL(SUM([HOURS]), 0.00)
		FROM 
			SPRINT_EMPLOYEE SE WITH(NOLOCK)
		WHERE 
			ALERT_ID = @ALERT_ID;
		SELECT
			@TASK_HOURS_ALLOWED = CASE WHEN ISNULL(J.HOURS_ASSIGNED, 0) <> 0 THEN ISNULL(J.HOURS_ASSIGNED, 0)
										ELSE ISNULL(J.JOB_HRS, 0.00)
									END
		FROM
			JOB_TRAFFIC_DET J WITH(NOLOCK)
		WHERE
			J.JOB_NUMBER = @JOB_NUMBER
			AND J.JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR
			AND J.SEQ_NBR = @TASK_SEQ_NBR;
		SELECT  @RETURN_HOURS =  @TASK_HOURS_ALLOWED - (@EMP_HOURS - @BEFORE_CHANGE_HOURS + @HOURS)
		IF @RETURN_HOURS < 0
		BEGIN
			SELECT @RETURN_HOURS = ABS(@RETURN_HOURS);
		END
		ELSE
		BEGIN
			SELECT @RETURN_HOURS = 0;
		END
	END
	SELECT ISNULL(@RETURN_HOURS, 0.00);
END
/*=========== QUERY ===========*/
