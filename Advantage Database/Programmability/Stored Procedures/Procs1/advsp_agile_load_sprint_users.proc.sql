IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'[dbo].[advsp_agile_load_sprint_users]') AND OBJECTPROPERTY(id, N'IsProcedure') = 1)
    DROP PROCEDURE [dbo].[advsp_agile_load_sprint_users]
GO

CREATE PROCEDURE [dbo].[advsp_agile_load_sprint_users] 
@SPRINT_ID INT,
@USER_CODE VARCHAR(100),
@INCLUDE_SELF BIT,
@IS_ONLINE BIT
AS
BEGIN

	DECLARE @USER_CODES TABLE (USER_CODE VARCHAR(100));

	INSERT INTO @USER_CODES
	SELECT
		SU.USER_CODE
	FROM
		SPRINT_DTL SD
		INNER JOIN ALERT A ON SD.ALERT_ID = A.ALERT_ID
		INNER JOIN ALERT_RCPT AR ON AR.ALERT_ID = A.ALERT_ID
		INNER JOIN SEC_USER SU ON SU.EMP_CODE = AR.EMP_CODE
	WHERE
		SD.SPRINT_HDR_ID = @SPRINT_ID
		AND A.IS_WORK_ITEM = 1
		AND (A.ALERT_LEVEL IS NULL OR A.ALERT_LEVEL <> 'BRD')
		AND (A.ASSIGN_COMPLETED IS NULL OR A.ASSIGN_COMPLETED = 0)
	UNION
	SELECT
		SU.USER_CODE
	FROM
		SPRINT_DTL SD
		INNER JOIN ALERT A ON SD.ALERT_ID = A.ALERT_ID
		INNER JOIN JOB_TRAFFIC_DET_EMPS JE ON A.JOB_NUMBER = JE.JOB_NUMBER AND A.JOB_COMPONENT_NBR = JE.JOB_COMPONENT_NBR AND A.TASK_SEQ_NBR = JE.SEQ_NBR
		INNER JOIN SEC_USER SU ON SU.EMP_CODE = JE.EMP_CODE
	WHERE
		SD.SPRINT_HDR_ID = @SPRINT_ID
		AND A.IS_WORK_ITEM = 1
		AND (A.ALERT_LEVEL = 'BRD')
		AND (A.ASSIGN_COMPLETED IS NULL OR A.ASSIGN_COMPLETED = 0)
	UNION
	SELECT
		SH.CREATE_USER
	FROM
		SPRINT_HDR SH
	WHERE
		SH.ID = @SPRINT_ID
	UNION	
	SELECT
		@USER_CODE
	WHERE
		1 = CASE WHEN @INCLUDE_SELF = 1 AND NOT @USER_CODE IS NULL THEN 1 ELSE 0 END

	IF @INCLUDE_SELF IS NULL OR @INCLUDE_SELF = 0
	BEGIN
		DELETE FROM @USER_CODES WHERE USER_CODE = @USER_CODE;
	END

	IF @IS_ONLINE = 1
	BEGIN
		SELECT 
			UC.USER_CODE
		FROM 
			@USER_CODES UC
			INNER JOIN SEC_USER SU ON UC.USER_CODE = SU.USER_CODE
		WHERE
			NOT SU.WEB_ID IS NULL;
	END
	ELSE
	BEGIN
		SELECT 
			UC.USER_CODE
		FROM 
			@USER_CODES UC;
	END

END