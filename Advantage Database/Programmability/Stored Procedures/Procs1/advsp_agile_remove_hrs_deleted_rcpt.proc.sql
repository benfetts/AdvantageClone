IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'[dbo].[advsp_agile_remove_hrs_deleted_rcpt]') AND OBJECTPROPERTY(id, N'IsProcedure') = 1)
    DROP PROCEDURE [dbo].[advsp_agile_remove_hrs_deleted_rcpt]
GO

CREATE PROCEDURE [dbo].[advsp_agile_remove_hrs_deleted_rcpt]
@ALERT_ID INT
AS
/*=========== QUERY ===========*/
BEGIN

    DECLARE @REAL_RECIPIENTS TABLE (ALERT_ID INT, EMP_CODE VARCHAR(6))
    DECLARE @HRS_RECIPIENTS TABLE (ALERT_ID INT, EMP_CODE VARCHAR(6))
    DECLARE 
	   @ALERT_LEVEL VARCHAR(100),
	   @IS_BOARD_TASK BIT,
	   @JOB_NUMBER INT,
	   @JOB_COMPONENT_NBR SMALLINT,
	   @TASK_SEQ_NBR SMALLINT

    SELECT 
	   @ALERT_LEVEL = ALERT_LEVEL, 
	   @JOB_NUMBER = ISNULL(JOB_NUMBER, 0), 
	   @JOB_COMPONENT_NBR = ISNULL(JOB_COMPONENT_NBR, 0),
	   @TASK_SEQ_NBR = ISNULL(TASK_SEQ_NBR, 0) 
    FROM ALERT 
    WHERE 
	   ALERT_ID = @ALERT_ID;

    IF @ALERT_LEVEL = 'BRD'
    BEGIN
	   SET @IS_BOARD_TASK = 1;
    END
    ELSE
    BEGIN
	   SET @IS_BOARD_TASK = 0;
    END

    IF @IS_BOARD_TASK = 0
    BEGIN
	   INSERT INTO @REAL_RECIPIENTS
	   SELECT DISTINCT
		  AR.ALERT_ID, AR.EMP_CODE
	   FROM
		  ALERT_RCPT AR
	   WHERE
		  AR.ALERT_ID = @ALERT_ID AND AR.CURRENT_NOTIFY = 1
	   UNION
	   SELECT DISTINCT
		  AR.ALERT_ID, AR.EMP_CODE
	   FROM
		  ALERT_RCPT_DISMISSED AR
	   WHERE
		  AR.ALERT_ID = @ALERT_ID AND AR.CURRENT_NOTIFY = 1
    END
    IF @IS_BOARD_TASK = 1
    BEGIN
	   INSERT INTO @REAL_RECIPIENTS
	   SELECT DISTINCT
		  @ALERT_ID, JE.EMP_CODE
	   FROM
		  JOB_TRAFFIC_DET_EMPS JE
	   WHERE
		  JE.JOB_NUMBER = @JOB_NUMBER
		  AND JE.JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR
		  AND JE.SEQ_NBR = @TASK_SEQ_NBR;
    END

    INSERT INTO @HRS_RECIPIENTS
    SELECT DISTINCT SE.ALERT_ID, SE.EMP_CODE
    FROM
    SPRINT_EMPLOYEE SE
    WHERE
	   SE.ALERT_ID = @ALERT_ID

    DELETE SPRINT_EMPLOYEE
    FROM
    SPRINT_EMPLOYEE SE INNER JOIN
    (
	   SELECT HR.ALERT_ID, HR.EMP_CODE 
	   FROM
	   @REAL_RECIPIENTS RR
	   RIGHT OUTER JOIN @HRS_RECIPIENTS HR ON RR.ALERT_ID = HR.ALERT_ID AND RR.EMP_CODE = HR.EMP_CODE
	   WHERE
	   RR.EMP_CODE IS NULL
    ) AS DEL ON SE.EMP_CODE = DEL.EMP_CODE AND SE.ALERT_ID = DEL.ALERT_ID


END
/*=========== QUERY ===========*/
