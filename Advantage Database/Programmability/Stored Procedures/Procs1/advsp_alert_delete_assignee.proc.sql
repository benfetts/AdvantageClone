IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'[dbo].[advsp_alert_delete_assignee]') AND OBJECTPROPERTY(id, N'IsProcedure') = 1)
DROP PROCEDURE [dbo].[advsp_alert_delete_assignee]
GO
CREATE PROCEDURE [dbo].[advsp_alert_delete_assignee] 
@ALERT_ID INT,
@EMP_CODE VARCHAR(6)
AS
BEGIN
	DECLARE
		@ALRT_NOTIFY_HDR_ID INT,
		@ALERT_STATE_ID INT,
		@IS_WORK_ITEM BIT,
		@IS_ROUTED BIT,
		@CUSTODY_END_DATE SMALLDATETIME,
		@CUSTODY_ID INT,
		@ALERT_LEVEL VARCHAR(50),
		@ALERT_CATEGORY_ID INT,
		@IS_TASK BIT,
		@JOB_NUMBER INT,
		@JOB_COMPONENT_NBR SMALLINT,
		@TASK_SEQ_NBR SMALLINT,
		@CAN_DELETE BIT
	SELECT @CUSTODY_END_DATE = GETDATE();
	SET @CAN_DELETE = 1;
	SELECT
		@ALRT_NOTIFY_HDR_ID = ISNULL(A.ALRT_NOTIFY_HDR_ID, 0),
		@ALERT_STATE_ID = ISNULL(A.ALERT_STATE_ID, 0),
		@IS_WORK_ITEM = CAST(ISNULL(A.IS_WORK_ITEM, 0) AS BIT),
		@ALERT_LEVEL = A.ALERT_LEVEL,
		@ALERT_CATEGORY_ID = A.ALERT_CAT_ID,
		@JOB_NUMBER = A.JOB_NUMBER,
		@JOB_COMPONENT_NBR = A.JOB_COMPONENT_NBR,
		@TASK_SEQ_NBR = A.TASK_SEQ_NBR
	FROM	
		ALERT A WITH(NOLOCK)
	WHERE
		ALERT_ID = @ALERT_ID;
	IF @ALRT_NOTIFY_HDR_ID = 0 AND @ALERT_STATE_ID = 0
	BEGIN
		SET @IS_ROUTED = 0;
	END
	ELSE
	BEGIN
		SET @IS_ROUTED = 1;
	END
	IF @ALERT_LEVEL = 'BRD' OR @ALERT_CATEGORY_ID = 71
	BEGIN
		SET @IS_TASK = 1;
	END
	ELSE
	BEGIN
		SET @IS_TASK = 0;
	END
	IF @IS_TASK = 0
	BEGIN
		IF EXISTS (SELECT ALERT_ID FROM ALERT_RCPT_DISMISSED WHERE ALERT_ID = @ALERT_ID AND EMP_CODE = @EMP_CODE AND CURRENT_NOTIFY = 1 AND ALRT_NOTIFY_HDR_ID = @ALRT_NOTIFY_HDR_ID AND ALERT_STATE_ID = @ALERT_STATE_ID)
		BEGIN
			SET @CAN_DELETE = 0;
		END
	END
	IF @CAN_DELETE = 1
	BEGIN

		IF @IS_TASK = 0
		BEGIN
			DELETE FROM SPRINT_EMPLOYEE WHERE ALERT_ID = @ALERT_ID AND EMP_CODE = @EMP_CODE;
			DELETE FROM ALERT_RCPT WHERE ALERT_ID = @ALERT_ID AND EMP_CODE = @EMP_CODE AND CURRENT_NOTIFY = 1;
			--DELETE FROM ALERT_RCPT_DISMISSED WHERE ALERT_ID = @ALERT_ID AND EMP_CODE = @EMP_CODE AND CURRENT_NOTIFY = 1;
		END
		ELSE
		BEGIN
			SET @IS_ROUTED = 0;
			DELETE FROM JOB_TRAFFIC_DET_EMPS WHERE JOB_NUMBER = @JOB_NUMBER AND JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR AND SEQ_NBR = @TASK_SEQ_NBR AND EMP_CODE = @EMP_CODE;
		END
		IF @IS_ROUTED = 1
		BEGIN
			UPDATE ALERT_COMMENT WITH(ROWLOCK)
			SET CUSTODY_END = @CUSTODY_END_DATE
			WHERE
				ALERT_ID = @ALERT_ID
				AND NOT ALRT_NOTIFY_HDR_ID IS NULL 
				AND NOT ALERT_STATE_ID IS NULL
				AND ALRT_NOTIFY_HDR_ID = @ALRT_NOTIFY_HDR_ID
				AND ALERT_STATE_ID = @ALERT_STATE_ID
				AND NOT ASSIGNED_EMP_CODE IS NULL
				AND ASSIGNED_EMP_CODE = @EMP_CODE
				AND CUSTODY_END IS NULL;
		END
		ELSE
		BEGIN
			UPDATE ALERT_COMMENT WITH(ROWLOCK)
			SET CUSTODY_END = @CUSTODY_END_DATE
			WHERE
				ALERT_ID = @ALERT_ID
				AND ALRT_NOTIFY_HDR_ID IS NULL 
				AND ALERT_STATE_ID IS NULL
				AND NOT ASSIGNED_EMP_CODE IS NULL
				AND ASSIGNED_EMP_CODE = @EMP_CODE
				AND CUSTODY_END IS NULL;
		END

	END

END