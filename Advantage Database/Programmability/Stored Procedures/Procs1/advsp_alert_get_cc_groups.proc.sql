IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'[dbo].[advsp_alert_get_cc_groups]') AND OBJECTPROPERTY(id, N'IsProcedure') = 1)
    DROP PROCEDURE [dbo].[advsp_alert_get_cc_groups]
GO
CREATE PROCEDURE [dbo].[advsp_alert_get_cc_groups] 
@TYPE SMALLINT, -- 0 = EMAIL GROUP, 1 = TASK EMPLOYEES, 2 = CLIENT CONTACTS
@ALERT_ID INT = NULL,
@CL_CODE VARCHAR(6) = NULL,
@JOB_NUMBER INT = NULL,
@JOB_COMPONENT_NBR SMALLINT = NULL,
@TASK_SEQ_NBR SMALLINT = NULL
AS
BEGIN	
	DECLARE
		@DIV_CODE VARCHAR(6),
		@PRD_CODE VARCHAR(6),
		@EMAIL_GR_CODE VARCHAR(50);
	IF NOT @ALERT_ID IS NULL AND @ALERT_ID > 0
	BEGIN
		SELECT
			@CL_CODE = CL_CODE,
			@DIV_CODE = DIV_CODE,
			@PRD_CODE = PRD_CODE,
			@JOB_NUMBER = JOB_NUMBER,
			@JOB_COMPONENT_NBR = JOB_COMPONENT_NBR,
			@TASK_SEQ_NBR = TASK_SEQ_NBR
		FROM
			ALERT WITH(NOLOCK)
		WHERE
			ALERT_ID = @ALERT_ID;
	END
	IF @TYPE IS NULL OR @TYPE = 0 --  EMAIL GROUP
	BEGIN
		IF @JOB_NUMBER > 0 AND @JOB_COMPONENT_NBR > 0
		BEGIN
			SELECT
				@EMAIL_GR_CODE = EMAIL_GR_CODE
			FROM
				JOB_COMPONENT WITH(NOLOCK)
			WHERE
				JOB_NUMBER = @JOB_NUMBER
				AND JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR;
		END
		IF @EMAIL_GR_CODE IS NULL
		BEGIN
			IF @JOB_NUMBER > 0 
			BEGIN
				SELECT 
					@EMAIL_GR_CODE = P.EMAIL_GR_CODE,
					@CL_CODE = J.CL_CODE,
					@DIV_CODE = J.DIV_CODE,
					@PRD_CODE = J.PRD_CODE
				FROM
					JOB_LOG J WITH(NOLOCK)
					INNER JOIN PRODUCT P WITH(NOLOCK) ON J.CL_CODE = P.CL_CODE AND J.DIV_CODE = P.DIV_CODE AND J.PRD_CODE = P.PRD_CODE
				WHERE
					NOT P.EMAIL_GR_CODE IS NULL
					AND J.JOB_NUMBER = @JOB_NUMBER
			END
		END
		IF @EMAIL_GR_CODE IS NULL
		BEGIN
			IF NOT @CL_CODE IS NULL AND NOT @DIV_CODE IS NULL AND NOT @PRD_CODE IS NULL
			BEGIN
				SELECT
					@EMAIL_GR_CODE = P.EMAIL_GR_CODE
				FROM
					PRODUCT P WITH(NOLOCK)
				WHERE
					P.CL_CODE = @CL_CODE AND P.DIV_CODE = @DIV_CODE AND P.PRD_CODE = @PRD_CODE;
			END
		END
		IF NOT @EMAIL_GR_CODE IS NULL
		BEGIN		
			SELECT
				[Code] = E.EMP_CODE,
				[Name] = E.EMP_FNAME + ISNULL(' ' + E.EMP_MI + '. ', ' ') + E.EMP_LNAME
			FROM
				EMAIL_GROUP EG WITH(NOLOCK)
				INNER JOIN EMPLOYEE E WITH(NOLOCK) ON EG.EMP_CODE = E.EMP_CODE
			WHERE
				EG.EMAIL_GR_CODE = @EMAIL_GR_CODE
				AND (EG.INACTIVE_FLAG IS NULL OR EG.INACTIVE_FLAG = 0)
				AND E.EMP_TERM_DATE IS NULL
			ORDER BY
				E.EMP_FNAME, E.EMP_MI, E.EMP_LNAME;
		END
	END
	IF @TYPE = 1 --  TASK EMPLOYEES
	BEGIN
		IF @JOB_NUMBER > 0 AND @JOB_COMPONENT_NBR > 0 AND @TASK_SEQ_NBR >= 0
		BEGIN
			SELECT
				[Code] = E.EMP_CODE,
				[Name] = E.EMP_FNAME + ISNULL(' ' + E.EMP_MI + '. ', ' ') + E.EMP_LNAME
			FROM
				JOB_TRAFFIC_DET_EMPS JTDE WITH(NOLOCK)
				INNER JOIN EMPLOYEE E WITH(NOLOCK) ON JTDE.EMP_CODE = E.EMP_CODE
			WHERE
				JTDE.JOB_NUMBER = @JOB_NUMBER
				AND JTDE.JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR
				AND JTDE.SEQ_NBR = @TASK_SEQ_NBR
			ORDER BY
				E.EMP_FNAME, E.EMP_MI, E.EMP_LNAME;
		END
	END
	IF @TYPE = 2 --  CONTACTS
	BEGIN
		IF NOT @JOB_NUMBER IS NULL
		BEGIN
			SELECT
				@CL_CODE = CL_CODE,
				@DIV_CODE = DIV_CODE,
				@PRD_CODE = PRD_CODE
			FROM
				JOB_LOG J WITH(NOLOCK)
			WHERE
				J.JOB_NUMBER = @JOB_NUMBER;
		END
		IF RTRIM(LTRIM(@CL_CODE)) = ''
		BEGIN
			SET @CL_CODE = NULL;
		END
		IF RTRIM(LTRIM(@DIV_CODE)) = ''
		BEGIN
			SET @DIV_CODE = NULL;
		END
		IF RTRIM(LTRIM(@PRD_CODE)) = ''
		BEGIN
			SET @PRD_CODE = NULL;
		END
		IF NOT @CL_CODE IS NULL
		BEGIN
			SELECT 
				[Code] = 'CC|' + CAST(CCH.CDP_CONTACT_ID AS VARCHAR(MAX)),
				[Name] =  CCH.CONT_FML
			FROM
				CDP_CONTACT_HDR CCH WITH(NOLOCK)
			WHERE
				CCH.CL_CODE = @CL_CODE
				AND ((CCH.INACTIVE_FLAG = 0) OR (CCH.INACTIVE_FLAG IS NULL))
			ORDER BY 
				CCH.CONT_FML;
		END
	END
END