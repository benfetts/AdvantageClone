CREATE PROCEDURE [dbo].[advsp_ap_gl_purchase_orders] @vn_code varchar(6)
AS
BEGIN
	SET NOCOUNT ON;
	
	SELECT
		[Number] = O.PO_NUMBER,
		[Description] = O.PO_DESCRIPTION,
		[Date] = O.PO_DATE,
		[DueDate] = O.PO_DUE_DATE,
		[Status] = POSTATUS.PSTATUS,
		[POTotal] = SUM(D.PO_EXT_AMOUNT),
		[POBalance] = SUM(D.PO_EXT_AMOUNT) - COALESCE(SUM(A.AP_GL_AMT), 0),
		[WorkComplete] = CASE COALESCE(O.PO_WORK_COMPLETE , 0)
							WHEN 1 THEN 'Yes'
							ELSE 'No' END
	FROM
		[dbo].PURCHASE_ORDER O
		INNER JOIN [dbo].PURCHASE_ORDER_DET D ON O.PO_NUMBER = D.PO_NUMBER 
		INNER JOIN (SELECT PO_NUMBER,
						CASE
							WHEN PO_APPR_RULE_CODE IS NULL THEN 'None'
							WHEN PO_APPR_RULE_CODE IS NOT NULL AND PO_APPROVAL_FLAG = 1 THEN 'Pending'
							WHEN PO_APPR_RULE_CODE IS NOT NULL AND PO_APPROVAL_FLAG = 2 THEN 'Approved'
							WHEN PO_APPR_RULE_CODE IS NOT NULL AND PO_APPROVAL_FLAG = 3 THEN 'Denied'
							WHEN PO_APPR_RULE_CODE IS NOT NULL AND COALESCE(PO_APPROVAL_FLAG,0) = 0 AND COALESCE(PO_PRINTED,0) = 0 THEN 'Incomplete'
							WHEN PO_APPR_RULE_CODE IS NOT NULL AND COALESCE(PO_APPROVAL_FLAG,0) = 0 AND PO_PRINTED = 1 THEN 'None'
						END AS PSTATUS
					FROM dbo.PURCHASE_ORDER) POSTATUS ON POSTATUS.PO_NUMBER = O.PO_NUMBER 
		LEFT OUTER JOIN (
			SELECT SUM(AP_GL_AMT) AP_GL_AMT, PO_NUMBER, PO_LINE_NUMBER
			FROM AP_GL_DIST
			WHERE (MODIFY_DELETE IS NULL OR MODIFY_DELETE = 0)
			AND PO_NUMBER IS NOT NULL
			GROUP BY PO_NUMBER, PO_LINE_NUMBER) A ON A.PO_NUMBER = D.PO_NUMBER AND A.PO_LINE_NUMBER = D.LINE_NUMBER 
	WHERE
				VN_CODE = @vn_code
		AND		(VOID_FLAG IS NULL OR VOID_FLAG = 0)
		AND		(O.PO_COMPLETE IS NULL OR O.PO_COMPLETE = 0)
		AND		(D.PO_COMPLETE IS NULL OR D.PO_COMPLETE = 0)
		AND		D.JOB_NUMBER IS NULL
	GROUP BY
		O.PO_NUMBER, O.PO_DESCRIPTION, O.PO_DATE, O.PO_DUE_DATE, POSTATUS.PSTATUS, O.PO_WORK_COMPLETE
	ORDER BY
		O.PO_NUMBER

END

GO