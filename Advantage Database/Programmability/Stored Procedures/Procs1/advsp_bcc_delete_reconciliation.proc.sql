CREATE PROCEDURE [dbo].[advsp_bcc_delete_reconciliation] @JobNumber int, @JobComponentNumber smallint

AS

BEGIN
	-- 08/05/2016 MJC - fixed arithmetic overflow issue - @FINAL_AB_ID was declared as smallint
	DECLARE @FINAL_AB_ID int

	SELECT TOP 1 @FINAL_AB_ID = FINAL_AB_ID
	FROM dbo.ADVANCE_BILLING
	WHERE JOB_NUMBER = @JobNumber
	AND JOB_COMPONENT_NBR = @JobComponentNumber
	AND AR_INV_NBR IS NULL
	
	UPDATE jc
				SET JOB_PROCESS_CONTRL = ( CASE WHEN ( jpl.NEW_PROCESS_CNTRL IS NULL ) THEN 1
												WHEN ( jpl.NEW_PROCESS_CNTRL = 0 ) THEN 1
												ELSE jpl.NEW_PROCESS_CNTRL END )
	FROM dbo.JOB_COMPONENT jc
		INNER JOIN dbo.ADVANCE_BILLING ab ON ( jc.JOB_NUMBER = ab.JOB_NUMBER ) AND ( jc.JOB_COMPONENT_NBR = ab.JOB_COMPONENT_NBR )
		LEFT OUTER JOIN dbo.JOB_PROCESS_LOG jpl 
					ON ( jc.JOB_NUMBER = jpl.JOB_NUMBER )
					AND ( jc.JOB_COMPONENT_NBR = jpl.JOB_COMPONENT_NBR )
					AND ( jpl.JOB_PROCESS_LOG_ID = ( SELECT MAX( jpl2.JOB_PROCESS_LOG_ID )
													   FROM dbo.JOB_PROCESS_LOG jpl2
													  WHERE ( jpl2.JOB_NUMBER = jpl.JOB_NUMBER )
														AND ( jpl2.JOB_COMPONENT_NBR = jpl.JOB_COMPONENT_NBR )
														AND ( jpl2.BCC_FLAG IS NULL OR jpl2.BCC_FLAG = 0 )))					 
	WHERE jc.JOB_NUMBER = @JobNumber
	AND jc.JOB_COMPONENT_NBR = @JobComponentNumber
	AND ( ab.AB_FLAG IN ( 4, 5 ))
	AND EXISTS (SELECT *
				FROM dbo.JOB_PROCESS_LOG jpl3
				WHERE ( jpl3.JOB_NUMBER = jc.JOB_NUMBER )
				AND ( jpl3.JOB_COMPONENT_NBR = jc.JOB_COMPONENT_NBR )
				AND ( jpl3.BCC_FLAG = 1 ))

	DELETE FROM dbo.INCOME_REC
	WHERE JOB_NUMBER = @JobNumber
	AND JOB_COMPONENT_NBR = @JobComponentNumber
	AND AR_INV_NBR IS NULL
	AND COALESCE(AB_FLAG, 0) <> 2

	DELETE FROM dbo.ADVANCE_BILLING
	WHERE JOB_NUMBER = @JobNumber
	AND JOB_COMPONENT_NBR = @JobComponentNumber
	AND AR_INV_NBR IS NULL
	AND COALESCE(AB_FLAG, 0) <> 2

	UPDATE dbo.ADVANCE_BILLING SET FINAL_AB_FLAG = NULL, FINAL_DATE = NULL, FINAL_AB_ID = NULL, FINAL_SEQ_NBR = NULL, FINAL_USER = NULL
	WHERE JOB_NUMBER = @JobNumber
	AND JOB_COMPONENT_NBR = @JobComponentNumber
	AND FINAL_AB_ID = @FINAL_AB_ID

	DELETE FROM dbo.JOB_PROCESS_LOG
	WHERE JOB_NUMBER = @JobNumber
	AND JOB_COMPONENT_NBR = @JobComponentNumber
	AND BCC_FLAG = 1

	UPDATE dbo.JOB_COMPONENT	SET AB_FLAG = 2
	WHERE JOB_NUMBER = @JobNumber 
	AND JOB_COMPONENT_NBR = @JobComponentNumber 

	UPDATE dbo.AP_PRODUCTION	SET AB_FLAG = 2
	WHERE JOB_NUMBER = @JobNumber 
	AND JOB_COMPONENT_NBR = @JobComponentNumber 

	UPDATE dbo.EMP_TIME_DTL		SET AB_FLAG = 2
	WHERE JOB_NUMBER = @JobNumber 
	AND JOB_COMPONENT_NBR = @JobComponentNumber

	UPDATE dbo.INCOME_ONLY		SET AB_FLAG = 2
	WHERE JOB_NUMBER = @JobNumber 
	AND JOB_COMPONENT_NBR = @JobComponentNumber 

END