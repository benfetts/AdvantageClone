IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'[dbo].[advsp_employee_timesheet_staging]') AND OBJECTPROPERTY(id, N'IsProcedure') = 1)
DROP PROCEDURE [dbo].[advsp_employee_timesheet_staging]
GO

CREATE PROCEDURE [dbo].[advsp_employee_timesheet_staging]	
@EMP_CODE VARCHAR(6)
AS 
BEGIN

	SELECT DISTINCT
		[ID] = ETS.ET_ID_STAGING,
		[StartDate] = ETS.[START_DATE],
		[EndDate] = ETS.END_DATE,
		[StartTime] = ETS.START_TIME,
		[EndTime] = ETS.END_TIME,
		[Hours] = CAST(ISNULL(ETS.[HOURS], 0) AS DECIMAL(9, 3)),
		[EmployeeCode] = ETS.EMP_CODE,
		[OutlookID] = ISNULL(ETS.OUTLOOK_ID, 0),
		[GoogleID] = ISNULL(ETS.GOOGLE_ID, 0),
		[CalendarID] = ISNULL(ETS.CALENDAR_ID, 0),
		[Comments] = CAST(ETS.COMMENTS AS VARCHAR(MAX)),
		[ClientCode] = J.CL_CODE,
		[ClientName] = C.CL_NAME,
		[DivisionCode] = J.DIV_CODE,
		[DivisionName] = D.DIV_NAME,
		[ProductCode] = J.PRD_CODE,
		[ProductName] = P.PRD_DESCRIPTION,
		[JobNumber] = ETS.JOB_NUMBER,
		[JobDescription] = J.JOB_DESC,
		[JobComponentDescription] = JC.JOB_COMP_DESC,
		[JobComponentNumber] = ETS.JOB_COMPONENT_NBR,
		[FunctionCode] = ETS.FNC_CODE,
		[FunctionName] = COALESCE(F.FNC_DESCRIPTION, TC.[DESCRIPTION]),
		[DepartmentCode] = ETS.DP_TM_CODE,
		[DepartmentName] = DT.DP_TM_DESC,
		[ProductCategoryCode] = ETS.PROD_CAT_CODE,
		[TimeType] = CASE
						WHEN (ETS.JOB_NUMBER IS NULL AND ETS.JOB_COMPONENT_NBR IS NULL) THEN 'N'
						ELSE 'D'
					 END
	FROM 
		EMP_TIME_STAGING ETS WITH(NOLOCK)
		LEFT OUTER JOIN FUNCTIONS F WITH(NOLOCK) ON F.FNC_CODE = ETS.FNC_CODE	
		LEFT OUTER JOIN TIME_CATEGORY TC WITH(NOLOCK) ON TC.CATEGORY = ETS.FNC_CODE	
		LEFT OUTER JOIN DEPT_TEAM DT WITH(NOLOCK) ON DT.DP_TM_CODE = ETS.DP_TM_CODE	 
		LEFT OUTER JOIN JOB_LOG J WITH(NOLOCK) ON J.JOB_NUMBER = ETS.JOB_NUMBER
		LEFT OUTER JOIN CLIENT C WITH(NOLOCK) ON C.CL_CODE = J.CL_CODE
		LEFT OUTER JOIN DIVISION D WITH(NOLOCK) ON D.CL_CODE = J.CL_CODE AND D.DIV_CODE = J.DIV_CODE
		LEFT OUTER JOIN PRODUCT P WITH(NOLOCK) ON P.CL_CODE = J.CL_CODE AND P.DIV_CODE = J.DIV_CODE AND P.PRD_CODE = J.PRD_CODE
		LEFT OUTER JOIN JOB_COMPONENT JC WITH(NOLOCK) ON JC.JOB_NUMBER = ETS.JOB_NUMBER AND JC.JOB_COMPONENT_NBR = ETS.JOB_COMPONENT_NBR
	WHERE
		ETS.EMP_CODE = @EMP_CODE;	

END
