CREATE PROCEDURE [dbo].[advsp_gl_detail_report]
	@USER_CODE varchar(100),
	@FROM_PPPERIOD varchar(6),
	@TO_PPPERIOD varchar(6),
	@USE_BASE_GLACODES bit,
	@FROM_GLACODE varchar(30),
	@TO_GLACODE varchar(30),
	@INCLUDE_INACTIVE_ACCOUNTS bit
AS
BEGIN

/*
	01/20/15: TP - Show Year End Post Periods

*/

	SET NOCOUNT ON

	DECLARE @EMP_CODE varchar(6),
			@EMP_OFFICE_LIMITED BIT

	SELECT @EMP_CODE = EMP_CODE FROM dbo.SEC_USER WHERE USER_CODE = @USER_CODE


	IF EXISTS( SELECT * FROM dbo.EMP_OFFICE WHERE EMP_CODE = @EMP_CODE)
		SET @EMP_OFFICE_LIMITED = 1
	ELSE
		SET @EMP_OFFICE_LIMITED = 0

	IF @USE_BASE_GLACODES = 1 BEGIN
		
		SELECT 
			[TransactionID] = GLT.GLEHXACT,   
			[AccountCode] = GLA.GLACODE,   
			[AccountDescription] = GLA.GLADESC,   
			[Amount] = GLTD.GLETAMT,   
			[Remark] = GLTD.GLETREM,   
			[PostPeriodCode] = PP.PPPERIOD,   
			[PostedToSummary] = GLT.GLEHPOSTSUM,   
			[ClientCode] = GLTD.CL_CODE, 
			[ClientName] = C.CL_NAME,
			[DivisionCode] = GLTD.DIV_CODE, 
			[DivisionName] = D.DIV_NAME, 
			[ProductCode] = GLTD.PRD_CODE, 
			[ProductDescription] = P.PRD_DESCRIPTION,   
			[Source] = GLTD.GLETSOURCE  
		FROM 
			[dbo].[GLENTHDR] AS GLT INNER JOIN 
			[dbo].[GLENTTRL] AS GLTD ON GLTD.GLETXACT = GLT.GLEHXACT INNER JOIN  
			[dbo].[GLACCOUNT] AS GLA ON GLA.GLACODE = GLTD.GLETCODE INNER JOIN
			[dbo].[POSTPERIOD] AS PP ON PP.PPPERIOD = GLT.GLEHPP LEFT OUTER JOIN
			(SELECT 
					GLO.GLOXGLOFFICE
				FROM 
					[dbo].[GLOXREF] AS GLO INNER JOIN
					(SELECT
							O.OFFICE_CODE
						FROM 
							[dbo].[EMPLOYEE] AS E CROSS JOIN 
							[dbo].[OFFICE] AS O LEFT OUTER JOIN 
							[dbo].[EMP_OFFICE] AS EO ON EO.EMP_CODE = E.EMP_CODE
						WHERE
							E.EMP_CODE = @EMP_CODE AND
							(EO.OFFICE_CODE = O.OFFICE_CODE OR
							 (O.OFFICE_CODE IS NOT NULL AND 
							  EO.OFFICE_CODE IS NULL))) AS OL ON OL.OFFICE_CODE = GLO.GLOXOFFICE) AS GLO ON GLO.GLOXGLOFFICE = GLA.GLAOFFICE LEFT OUTER JOIN 
			[dbo].[PRODUCT] AS P ON P.CL_CODE = GLTD.CL_CODE AND 
									P.DIV_CODE = GLTD.DIV_CODE AND 
									P.PRD_CODE = GLTD.PRD_CODE LEFT OUTER JOIN
			[dbo].[DIVISION] AS D ON D.CL_CODE = P.CL_CODE AND
									 D.DIV_CODE = P.DIV_CODE LEFT OUTER JOIN
			[dbo].[CLIENT] AS C ON C.CL_CODE = P.CL_CODE 
		WHERE 
			1 = CASE WHEN @INCLUDE_INACTIVE_ACCOUNTS = 1 THEN 1 
				     ELSE CASE WHEN GLA.GLAACTIVE = 'A' THEN 1 ELSE 0 END END AND 
			-- PP.PPGLMONTH <> 99 AND  
			PP.PPPERIOD BETWEEN @FROM_PPPERIOD AND @TO_PPPERIOD AND  
			GLA.GLABASE BETWEEN @FROM_GLACODE AND @TO_GLACODE AND
			1 = CASE WHEN @EMP_OFFICE_LIMITED = 0 THEN 1 WHEN GLO.GLOXGLOFFICE IS NOT NULL THEN 1 ELSE 0 END
			
	END ELSE BEGIN
		
		SELECT 
			[TransactionID] = GLT.GLEHXACT,   
			[AccountCode] = GLA.GLACODE,   
			[AccountDescription] = GLA.GLADESC,   
			[Amount] = GLTD.GLETAMT,   
			[Remark] = GLTD.GLETREM,   
			[PostPeriodCode] = PP.PPPERIOD,   
			[PostedToSummary] = GLT.GLEHPOSTSUM,   
			[ClientCode] = GLTD.CL_CODE, 
			[ClientName] = C.CL_NAME,
			[DivisionCode] = GLTD.DIV_CODE, 
			[DivisionName] = D.DIV_NAME, 
			[ProductCode] = GLTD.PRD_CODE, 
			[ProductDescription] = P.PRD_DESCRIPTION,   
			[Source] = GLTD.GLETSOURCE  
		FROM 
			[dbo].[GLENTHDR] AS GLT INNER JOIN 
			[dbo].[GLENTTRL] AS GLTD ON GLTD.GLETXACT = GLT.GLEHXACT INNER JOIN  
			[dbo].[GLACCOUNT] AS GLA ON GLA.GLACODE = GLTD.GLETCODE INNER JOIN
			[dbo].[POSTPERIOD] AS PP ON PP.PPPERIOD = GLT.GLEHPP LEFT OUTER JOIN 
			[dbo].[PRODUCT] AS P ON P.CL_CODE = GLTD.CL_CODE AND 
									P.DIV_CODE = GLTD.DIV_CODE AND 
									P.PRD_CODE = GLTD.PRD_CODE LEFT OUTER JOIN
			[dbo].[DIVISION] AS D ON D.CL_CODE = P.CL_CODE AND
									 D.DIV_CODE = P.DIV_CODE LEFT OUTER JOIN
			[dbo].[CLIENT] AS C ON C.CL_CODE = P.CL_CODE 
		WHERE 
			1 = CASE WHEN @INCLUDE_INACTIVE_ACCOUNTS = 1 THEN 1 
				     ELSE CASE WHEN GLA.GLAACTIVE = 'A' THEN 1 ELSE 0 END END AND 
			-- PP.PPGLMONTH <> 99 AND  
			PP.PPPERIOD BETWEEN @FROM_PPPERIOD AND @TO_PPPERIOD AND  
			GLA.GLACODE BETWEEN @FROM_GLACODE AND @TO_GLACODE
			
	END
	
END