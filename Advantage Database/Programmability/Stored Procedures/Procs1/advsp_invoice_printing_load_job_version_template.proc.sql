CREATE PROCEDURE [dbo].[advsp_invoice_printing_load_job_version_template]
	@JobNumber AS int,
	@ComponentNumber AS smallint
AS
BEGIN

	SET NOCOUNT ON

	DECLARE @SequenceNumber AS int

	SELECT 
		@SequenceNumber = MAX(JV.JOB_VER_SEQ_NBR) 
	FROM 
		[dbo].[JOB_VER_HDR] AS JV
	WHERE   
		JV.JOB_NUMBER = @JobNumber AND
		JV.JOB_COMPONENT_NBR = @ComponentNumber
	GROUP BY
		JV.JOB_NUMBER,
		JV.JOB_COMPONENT_NBR

	SELECT  
		[Label] = CAST(JVTD.JV_TMPLT_LABEL + ':' AS varchar(MAX)),
		[Value] = CAST(CASE WHEN JVT.ADVAN_DTYPE_ID = 7 THEN CASE WHEN JVD.JOB_VER_VALUE = 1 THEN 'Yes' ELSE 'No' END ELSE JVD.JOB_VER_VALUE END AS varchar(MAX))
	FROM 
		[dbo].[JOB_VER_HDR] AS JV INNER JOIN
		[dbo].[JOB_VER_DTL] AS JVD ON JVD.JOB_VER_HDR_ID = JV.JOB_VER_HDR_ID INNER JOIN
		[dbo].[JOB_VER_TMPLT_DTL] AS JVTD ON JVTD.JV_TMPLT_DTL_ID = JVD.JV_TMPLT_DTL_ID INNER JOIN
		[dbo].[JOB_VER_DTYPE] AS JVT ON JVT.JV_DTYPE_ID = JVTD.JV_DTYPE_ID
	WHERE   
		ISNULL(JVTD.INACTIVE_FLAG, 0) = 0 AND
		JV.JOB_NUMBER = @JobNumber AND
		JV.JOB_COMPONENT_NBR = @ComponentNumber AND
		JV.JV_TMPLT_CODE LIKE 'inv%' AND
		JVD.JOB_VER_VALUE IS NOT NULL AND
		JV.JOB_VER_SEQ_NBR = @SequenceNumber
	ORDER BY 
		JVTD.JV_TMPLT_ORDER

END

GO