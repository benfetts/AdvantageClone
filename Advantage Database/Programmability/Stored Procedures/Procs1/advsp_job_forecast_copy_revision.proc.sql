CREATE PROC [dbo].[advsp_job_forecast_copy_revision]
	@COPY_FROM_REVISION_ID INT,
	@COPY_TO_FORECAST_ID INT,
	@USER_CODE VARCHAR(100)
AS
BEGIN

	DECLARE @REVISION_ID INT
	DECLARE @NEXT_REV_NBR INT
	DECLARE @POST_PERIOD_START AS SMALLDATETIME
	DECLARE @POST_PERIOD_END AS SMALLDATETIME
	DECLARE @IS_REVISING BIT

	SELECT 
		@NEXT_REV_NBR =  ISNULL(MAX(REV_NBR) + 1, 0)
	FROM
		dbo.JF_REV 
	WHERE
		JF_ID = @COPY_TO_FORECAST_ID
		
	IF (SELECT JF_ID FROM dbo.JF_REV WHERE JF_REV_ID = @COPY_FROM_REVISION_ID) = @COPY_TO_FORECAST_ID
		SET @IS_REVISING = 1
	ELSE
		SET @IS_REVISING = 0

	IF @NEXT_REV_NBR >= 0 
	BEGIN

		INSERT INTO 
			dbo.JF_REV (JF_ID, REV_NBR, JF_REV_COMMENT, USER_CREATED, CREATED_DATE, APPROVED)
		SELECT
			@COPY_TO_FORECAST_ID,
			@NEXT_REV_NBR,
			CASE WHEN @IS_REVISING = 1 THEN JF_REV_COMMENT END,
			@USER_CODE,
			GETDATE(),
			0
		FROM
			dbo.JF_REV
		WHERE 
			JF_REV_ID = @COPY_FROM_REVISION_ID

		SELECT @REVISION_ID = @@IDENTITY

		IF @REVISION_ID > 0 
		BEGIN

			INSERT INTO 
				dbo.JF_JOB (JF_REV_ID, JF_ID, JOB_NUMBER, JOB_COMPONENT_NBR, JF_JOB_COMMENT, INCOME_AMT, COLOR, USER_CREATED, CREATED_DATE)
			SELECT
				@REVISION_ID, @COPY_TO_FORECAST_ID, JOB_NUMBER, JOB_COMPONENT_NBR, JF_JOB_COMMENT, INCOME_AMT, COLOR, @USER_CODE, GETDATE()
			FROM
				dbo.JF_JOB
			WHERE
				JF_REV_ID = @COPY_FROM_REVISION_ID
			
			SELECT
				@POST_PERIOD_START = POSTPERIOD.PPSRTDATE
			FROM
				dbo.POSTPERIOD JOIN
				dbo.JF_HDR ON POSTPERIOD.PPPERIOD = JF_HDR.PPPERIOD_START
			WHERE
				JF_ID = @COPY_TO_FORECAST_ID

			SELECT
				@POST_PERIOD_END = POSTPERIOD.PPENDDATE
			FROM
				dbo.POSTPERIOD JOIN
				dbo.JF_HDR ON POSTPERIOD.PPPERIOD = JF_HDR.PPPERIOD_END
			WHERE
				JF_ID = @COPY_TO_FORECAST_ID

			INSERT INTO
				dbo.JF_JOB_DTL (JF_JOB_ID, JF_REV_ID, JF_ID, PPPERIOD, BILLING_AMT, REVENUE_AMT, USER_CREATED, CREATED_DATE)
			SELECT
				COPIED_JOBS.JF_JOB_ID, @REVISION_ID, @COPY_TO_FORECAST_ID, JF_JOB_DTL.PPPERIOD, BILLING_AMT, REVENUE_AMT, @USER_CODE, GETDATE()
			FROM
				dbo.JF_JOB_DTL JOIN
				dbo.JF_JOB ON JF_JOB_DTL.JF_JOB_ID = JF_JOB.JF_JOB_ID JOIN
				(SELECT
					*
				 FROM
					dbo.JF_JOB
				 WHERE
					JF_REV_ID = @REVISION_ID) COPIED_JOBS ON JF_JOB.JOB_NUMBER = COPIED_JOBS.JOB_NUMBER AND
															 JF_JOB.JOB_COMPONENT_NBR = COPIED_JOBS.JOB_COMPONENT_NBR JOIN
					(SELECT
						*
					 FROM
						dbo.POSTPERIOD 
					 WHERE
						POSTPERIOD.PPSRTDATE >= @POST_PERIOD_START AND
						POSTPERIOD.PPENDDATE <= @POST_PERIOD_END AND
						POSTPERIOD.PPGLMONTH IS NOT NULL AND
						POSTPERIOD.PPGLMONTH <> 99) POSTPERIOD ON JF_JOB_DTL.PPPERIOD = POSTPERIOD.PPPERIOD
			WHERE
				JF_JOB_DTL.JF_REV_ID = @COPY_FROM_REVISION_ID

		END

	END

END