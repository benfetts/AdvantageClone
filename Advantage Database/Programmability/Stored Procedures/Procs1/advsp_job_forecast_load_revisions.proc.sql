CREATE PROCEDURE [dbo].[advsp_job_forecast_load_revisions]
	@EMP_CODE VARCHAR(6),
	@JOB_NUMBER INT, 
	@JOB_COMPONENT_NBR SMALLINT,
	@DATE_TO_SEARCH SMALLDATETIME,
	@CURRENT_ONLY BIT,
	@USER_CODE VARCHAR(100)
AS 
BEGIN

	DECLARE @USER_EMP_CODE VARCHAR(6)
	DECLARE @LIMITED_BY_OFFICE BIT

	SELECT
		@USER_EMP_CODE = EMP_CODE
	FROM
		dbo.SEC_USER
	WHERE
	 	UPPER([USER_CODE]) = UPPER(@USER_CODE)

	SELECT
		@LIMITED_BY_OFFICE = CASE WHEN COUNT(*) > 0 THEN 1 ELSE 0 END
	FROM
		dbo.EMP_OFFICE 
	WHERE 
		EMP_CODE = @USER_EMP_CODE

	SELECT
		[JobForecastID] = JF_HDR.JF_ID,
		[JobForecastRevisionID] = JF_REV.JF_REV_ID,
		[RevisionNumber] = JF_REV.REV_NBR,
		[Description] = JF_HDR.JF_DESCRIPTION,
		[Comment] = JF_REV.JF_REV_COMMENT,
		[Budget] = JF_HDR.BUDGET,
		[AssignedToEmployeeCode] = JF_HDR.USER_ASSIGNED,
		[AssignedToEmployeeName] = dbo.advfn_get_emp_name(JF_HDR.USER_ASSIGNED, 'FML'),
		[IsApproved] = JF_REV.APPROVED,
		[CreatedDate] = JF_REV.CREATED_DATE,
		[ModifiedDate] = JF_REV.MODIFIED_DATE,
		[PostPeriodStartDate] = PPSTART.PPSRTDATE,
		[PostPeriodEndDate] = PPEND.PPENDDATE
	FROM
		dbo.JF_HDR INNER JOIN
		 dbo.JF_REV ON JF_HDR.JF_ID = JF_REV.JF_ID LEFT OUTER JOIN
		(SELECT
			MAX_REV.JF_ID,
			REV_NBR = ISNULL(APPR_REV.REV_NBR, MAX_REV.REV_NBR)
		 FROM
			(SELECT
				JF_ID,
				REV_NBR = MAX(REV_NBR)
			 FROM
				dbo.JF_REV
			 GROUP BY 
				JF_ID) MAX_REV LEFT OUTER JOIN
			(SELECT
				JF_ID,
				REV_NBR
			 FROM
				dbo.JF_REV
			 WHERE	
				APPROVED = 1) APPR_REV ON MAX_REV.JF_ID = APPR_REV.JF_ID) CURR_REV ON JF_REV.JF_ID = CURR_REV.JF_ID AND
																				      JF_REV.REV_NBR = CURR_REV.REV_NBR LEFT OUTER JOIN			
		dbo.POSTPERIOD AS PPSTART ON JF_HDR.PPPERIOD_START = PPSTART.PPPERIOD INNER JOIN
		dbo.POSTPERIOD AS PPEND ON JF_HDR.PPPERIOD_END = PPEND.PPPERIOD LEFT OUTER JOIN
		dbo.EMP_OFFICE ON JF_HDR.OFFICE_CODE = EMP_OFFICE.OFFICE_CODE AND
						  EMP_OFFICE.EMP_CODE = @USER_EMP_CODE LEFT OUTER JOIN
		(SELECT
			JF_REV_ID
		 FROM
			dbo.JF_JOB
		 WHERE
			JOB_NUMBER = @JOB_NUMBER AND
			JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR) JF_JOB ON JF_REV.JF_REV_ID = JF_JOB.JF_REV_ID
	WHERE
		1 = CASE WHEN ISNULL(@EMP_CODE, '') = '' THEN 1 WHEN JF_HDR.USER_ASSIGNED = @EMP_CODE THEN 1 ELSE 0 END AND
		1 = CASE WHEN @DATE_TO_SEARCH IS NULL THEN 1 WHEN PPEND.PPENDDATE >= @DATE_TO_SEARCH THEN 1 ELSE 0 END AND
		1 = CASE WHEN @LIMITED_BY_OFFICE = 0 THEN 1 WHEN EMP_OFFICE.OFFICE_CODE IS NOT NULL THEN 1 ELSE 0 END AND
		1 = CASE WHEN ISNULL(@JOB_NUMBER, 0) <= 0 AND ISNULL(@JOB_COMPONENT_NBR, 0) <= 0 THEN 1 WHEN JF_JOB.JF_REV_ID IS NOT NULL THEN 1 ELSE 0 END AND
		1 = CASE WHEN @CURRENT_ONLY = 0 THEN 1 WHEN ISNULL(CURR_REV.REV_NBR, -1) >= 0 THEN 1 END 
	
END
