--DROP PROCEDURE [dbo].[advsp_job_status_load] 
CREATE PROCEDURE [dbo].[advsp_job_status_load] 
	@JOB_NUMBER INT,
	@JOB_COMPONENT_NBR INT,
	@USER_CODE VARCHAR(100)
AS
BEGIN
/*================ QUERY ================*/
DECLARE
	@JOB_DESCRIPTION VARCHAR(50),
	@JOB_COMPONENT_DESCRIPTION VARCHAR(50),
	@JOB_PROCESS_CONTROL SMALLINT,
	@JOB_PROCESS_CONTROL_TEXT VARCHAR(50),
	@JOB_IS_BILLABLE BIT,
	@JOB_NO_BILL_FLAG SMALLINT,
	@CLIENT_DISPLAY VARCHAR(100),
	@DIVISION_DISPLAY VARCHAR(100),
	@PRODUCT_DISPLAY VARCHAR(100),
	@OFFICE_DISPLAY VARCHAR(100),
	@SCHEDULE_STATUS_CODE VARCHAR(10), --
	@SCHEDULE_STATUS_DESC VARCHAR(200), --
	@SCHEDULE_COMMENTS VARCHAR(MAX), --

	@ESTIMATE_APPROVAL_STATUS_CLIENT VARCHAR(20), --
	@ESTIMATE_APPROVAL_STATUS_CLIENT_DATE SMALLDATETIME, --
	@ESTIMATE_APPROVAL_CLIENT_COMMENT VARCHAR(MAX),
	@ESTIMATE_APPROVAL_STATUS_INTERNAL VARCHAR(20), --
	@ESTIMATE_APPROVAL_STATUS_INTERNAL_DATE SMALLDATETIME, --
	@ESTIMATE_APPROVAL_INTERNAL_COMMENT VARCHAR(MAX),
	@APPROVED_ESTIMATE_REQUIRED AS BIT,
	@APPROVED_ESTIMATE_REQUIRED_SMALLINT AS SMALLINT,

	@QUOTED_HOURS DECIMAL(24,3),
	@ACTUAL_HOURS DECIMAL (24, 3),
	@REMAINING_HOURS DECIMAL (24,3),
	@QUOTED_DOLLARS DECIMAL (24,3),
	@ACTUAL_DOLLARS DECIMAL (24,3),
	@REMAINING_DOLLARS DECIMAL (24,3),

	@PERCENT_COMPLETE DECIMAL (9,3), --BASED ON QUOTE

	@PROJECTED_HOURS DECIMAL (24,3),
	@TASK_ACTUAL_HOURS DECIMAL (24,3), --?

	@LAST_ALERT_ID INT, --
	@LAST_ALERT_SUBJECT VARCHAR(254), --
	@LAST_ALERT_DATE SMALLDATETIME, --
	@OPEN_ALERT_COUNT DECIMAL (9,3), --
	@TOTAL_ALERT_COUNT DECIMAL (9,3), --
	
	@LAST_ALERT_ASSIGNMENT_ID INT, --
	@LAST_ALERT_ASSIGNMENT_SUBJECT VARCHAR(254), --
	@LAST_ALERT_ASSIGNMENT_DATE SMALLDATETIME, --
	@OPEN_ALERT_ASSIGNMENT_COUNT DECIMAL (9,3), --
	@TOTAL_ALERT_ASSIGNMENT_COUNT DECIMAL (9,3), --

	@OPEN_TASKS_COUNT DECIMAL (9,3), --
	@TOTAL_TASKS_COUNT DECIMAL (9,3), --
	@TASK_MIN_START SMALLDATETIME, --
	@TASK_MAX_DUE SMALLDATETIME, --
	@TOTAL_TASK_HOURS DECIMAL (14,3),
	@ACTUAL_EMP_TASK_HOURS DECIMAL (14,3),

	@PERCENT_COMPLETE_SCHEDULE_HOURS DECIMAL (9,3),
	@PERCENT_COMPLETE_ALERTS DECIMAL (9,3),
	@PERCENT_COMPLETE_TASKS DECIMAL (9,3),

	@AE_EMP_CODE VARCHAR(6), --
	@AE_EMP_FML VARCHAR(200),

	@SCHEDULE_MANAGER_CODE VARCHAR(6), --
	@SCHEDULE_MANAGER_FML VARCHAR(200),

	@SCHEDULE_ASSIGNMENT_1_DISPLAY VARCHAR(20),
	@SCHEDULE_ASSIGNMENT_1 VARCHAR(6), --
	@SCHEDULE_ASSIGNMENT_1_FML VARCHAR(200), --

	@SCHEDULE_ASSIGNMENT_2_DISPLAY VARCHAR(20),
	@SCHEDULE_ASSIGNMENT_2 VARCHAR(6), --
	@SCHEDULE_ASSIGNMENT_2_FML VARCHAR(200), --

	@SCHEDULE_ASSIGNMENT_3_DISPLAY VARCHAR(20),
	@SCHEDULE_ASSIGNMENT_3 VARCHAR(6), --
	@SCHEDULE_ASSIGNMENT_3_FML VARCHAR(200), --

	@SCHEDULE_ASSIGNMENT_4_DISPLAY VARCHAR(20),
	@SCHEDULE_ASSIGNMENT_4 VARCHAR(6), --
	@SCHEDULE_ASSIGNMENT_4_FML VARCHAR(200), --

	@SCHEDULE_ASSIGNMENT_5_DISPLAY VARCHAR(20),
	@SCHEDULE_ASSIGNMENT_5 VARCHAR(6), --
	@SCHEDULE_ASSIGNMENT_5_FML VARCHAR(200), --

	@SCHEDULE_START_DATE SMALLDATETIME, --
	@SCHEDULE_DUE_DATE SMALLDATETIME, --
	@SCHEDULE_END_DATE SMALLDATETIME,
	@SCHEDULE_COMPLETED_DATE SMALLDATETIME, --
	@SCHEDULE_GUT_PERCENT_COMPLETE DECIMAL(9,3),
	@DURATION DECIMAL (9,3),
	@TASK_HOURS_UNASSIGNED  DECIMAL (9,3),

	@BATCH_TEXT VARCHAR(1000),
	@BA_BATCH_ID INT,
	@BA_BATCH_DESC VARCHAR(50),
	@BA_BATCH_CREATE_USER VARCHAR(100),
	@BA_BATCH_BATCH_DATE SMALLDATETIME,
	@BILLING_USER VARCHAR(100),
	@AB_FLAG SMALLINT,
	@AB_TEXT VARCHAR(10)

--	SCHEDULE HEADER
BEGIN
	SELECT 
		@SCHEDULE_STATUS_CODE = JT.TRF_CODE,
		@SCHEDULE_STATUS_DESC = CAST(T.TRF_DESCRIPTION AS VARCHAR(MAX)),
		@SCHEDULE_COMMENTS = JT.TRF_COMMENTS,
		@SCHEDULE_ASSIGNMENT_1 = JT.ASSIGN_1,
		@SCHEDULE_ASSIGNMENT_2 = JT.ASSIGN_2,
		@SCHEDULE_ASSIGNMENT_3 = JT.ASSIGN_3,
		@SCHEDULE_ASSIGNMENT_4 = JT.ASSIGN_4,
		@SCHEDULE_ASSIGNMENT_5 = JT.ASSIGN_5,
		@SCHEDULE_MANAGER_CODE = JT.MGR_EMP_CODE,
		@SCHEDULE_COMPLETED_DATE = JT.COMPLETED_DATE,
		@SCHEDULE_GUT_PERCENT_COMPLETE = JT.PERCENT_COMPLETE
	FROM
		JOB_TRAFFIC AS JT WITH(NOLOCK)
		LEFT OUTER JOIN TRAFFIC AS T WITH(NOLOCK) ON JT.TRF_CODE = T.TRF_CODE
	WHERE
		JT.JOB_NUMBER = @JOB_NUMBER
		AND JT.JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR;

	SELECT 
		@SCHEDULE_ASSIGNMENT_1_DISPLAY = TR_TITLE1,
		@SCHEDULE_ASSIGNMENT_2_DISPLAY = TR_TITLE2,
		@SCHEDULE_ASSIGNMENT_3_DISPLAY = TR_TITLE3,
		@SCHEDULE_ASSIGNMENT_4_DISPLAY = TR_TITLE4,
		@SCHEDULE_ASSIGNMENT_5_DISPLAY = TR_TITLE5 
	FROM 
		AGENCY WITH(NOLOCK);

	IF NOT @SCHEDULE_ASSIGNMENT_1 IS NULL
	BEGIN
		SELECT @SCHEDULE_ASSIGNMENT_1_FML = E.EMP_FNAME + ' ' + E.EMP_LNAME FROM EMPLOYEE E WHERE E.EMP_CODE = @SCHEDULE_ASSIGNMENT_1;
		IF @SCHEDULE_ASSIGNMENT_1 = @SCHEDULE_MANAGER_CODE
		BEGIN
			SET @SCHEDULE_MANAGER_FML = @SCHEDULE_ASSIGNMENT_1_FML;
		END
	END
	IF NOT @SCHEDULE_ASSIGNMENT_2 IS NULL
	BEGIN
		SELECT @SCHEDULE_ASSIGNMENT_2_FML = E.EMP_FNAME + ' ' + E.EMP_LNAME FROM EMPLOYEE E WHERE E.EMP_CODE = @SCHEDULE_ASSIGNMENT_2;
		IF @SCHEDULE_ASSIGNMENT_2 = @SCHEDULE_MANAGER_CODE
		BEGIN
			SET @SCHEDULE_MANAGER_FML = @SCHEDULE_ASSIGNMENT_2_FML;
		END
	END
	IF NOT @SCHEDULE_ASSIGNMENT_3 IS NULL
	BEGIN
		SELECT @SCHEDULE_ASSIGNMENT_3_FML = E.EMP_FNAME + ' ' + E.EMP_LNAME FROM EMPLOYEE E WHERE E.EMP_CODE = @SCHEDULE_ASSIGNMENT_3;
		IF @SCHEDULE_ASSIGNMENT_3 = @SCHEDULE_MANAGER_CODE
		BEGIN
			SET @SCHEDULE_MANAGER_FML = @SCHEDULE_ASSIGNMENT_3_FML;
		END
	END
	IF NOT @SCHEDULE_ASSIGNMENT_4 IS NULL
	BEGIN
		SELECT @SCHEDULE_ASSIGNMENT_4_FML = E.EMP_FNAME + ' ' + E.EMP_LNAME FROM EMPLOYEE E WHERE E.EMP_CODE = @SCHEDULE_ASSIGNMENT_4;
		IF @SCHEDULE_ASSIGNMENT_4 = @SCHEDULE_MANAGER_CODE
		BEGIN
			SET @SCHEDULE_MANAGER_FML = @SCHEDULE_ASSIGNMENT_4_FML;
		END
	END
	IF NOT @SCHEDULE_ASSIGNMENT_5 IS NULL
	BEGIN
		SELECT @SCHEDULE_ASSIGNMENT_5_FML = E.EMP_FNAME + ' ' + E.EMP_LNAME FROM EMPLOYEE E WHERE E.EMP_CODE = @SCHEDULE_ASSIGNMENT_5;
		IF @SCHEDULE_ASSIGNMENT_5 = @SCHEDULE_MANAGER_CODE
		BEGIN
			SET @SCHEDULE_MANAGER_FML = @SCHEDULE_ASSIGNMENT_5_FML;
		END
	END

END

--  JOB
BEGIN
	SELECT 
		@CLIENT_DISPLAY = CL.CL_CODE + ' - ' + CL.CL_NAME,
		@DIVISION_DISPLAY = DI.DIV_CODE + ' - ' + DI.DIV_NAME,
		@PRODUCT_DISPLAY = PR.PRD_CODE + ' - ' + PR.PRD_DESCRIPTION,
		@OFFICE_DISPLAY = ISNULL([OF].OFFICE_CODE,'') + ' - ' + ISNULL([OF].OFFICE_NAME, ''),
		@JOB_DESCRIPTION = JL.JOB_DESC,
		@APPROVED_ESTIMATE_REQUIRED_SMALLINT = JL.JOB_ESTIMATE_REQ
	FROM
		JOB_LOG AS JL WITH(NOLOCK)
		INNER JOIN CLIENT AS CL WITH(NOLOCK) ON JL.CL_CODE = CL.CL_CODE
		INNER JOIN DIVISION AS DI WITH(NOLOCK) ON JL.CL_CODE = DI.CL_CODE AND JL.DIV_CODE = DI.DIV_CODE
		INNER JOIN PRODUCT AS PR WITH(NOLOCK) ON JL.CL_CODE = PR.CL_CODE AND JL.DIV_CODE = PR.DIV_CODE AND JL.PRD_CODE = PR.PRD_CODE
		LEFT OUTER JOIN OFFICE AS [OF] WITH(NOLOCK) ON JL.OFFICE_CODE = [OF].OFFICE_CODE
	WHERE
		JL.JOB_NUMBER = @JOB_NUMBER;

	IF @APPROVED_ESTIMATE_REQUIRED_SMALLINT = 1
	BEGIN
		SET @APPROVED_ESTIMATE_REQUIRED = 1;
	END
	ELSE
	BEGIN
		SET @APPROVED_ESTIMATE_REQUIRED = 0;
	END
END

--	JOB COMPONENT
BEGIN
	SELECT 
		@SCHEDULE_START_DATE = JC.START_DATE,
		@SCHEDULE_DUE_DATE = JC.JOB_FIRST_USE_DATE,
		@AE_EMP_CODE = JC.EMP_CODE,
		@JOB_COMPONENT_DESCRIPTION = JC.JOB_COMP_DESC,
		@JOB_PROCESS_CONTROL = JC.JOB_PROCESS_CONTRL,
		@JOB_NO_BILL_FLAG = JC.NOBILL_FLAG
	FROM 
		JOB_COMPONENT AS JC WITH(NOLOCK)
	WHERE
		JC.JOB_NUMBER = @JOB_NUMBER AND JC.JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR;

	IF @JOB_NO_BILL_FLAG = 1
	BEGIN
		SET @JOB_IS_BILLABLE = 0;
	END
	ELSE
	BEGIN
		SET @JOB_IS_BILLABLE = 1;
	END

	IF NOT @AE_EMP_CODE IS NULL
	BEGIN
		SELECT @AE_EMP_FML = E.EMP_FNAME + ' ' + E.EMP_LNAME FROM EMPLOYEE E WHERE E.EMP_CODE = @AE_EMP_CODE;
	END

	SELECT @JOB_PROCESS_CONTROL_TEXT = JPC.JOB_PROCESS_DESC FROM JOB_PROC_CONTROLS AS JPC WITH(NOLOCK) WHERE JPC.JOB_PROCESS_CONTRL = @JOB_PROCESS_CONTROL;

END


--  ALERT
BEGIN
	DECLARE @ALERTS TABLE (ALERT_ID INT,
						   [SUBJECT] VARCHAR(254),
						   GENERATED SMALLDATETIME,
						   IS_ASSIGNMENT BIT
						   );

	INSERT INTO @ALERTS (ALERT_ID, [SUBJECT], GENERATED, IS_ASSIGNMENT)
	SELECT ALERT_ID, [SUBJECT], GENERATED, 0
	FROM ALERT AS A WITH(NOLOCK) 
	WHERE A.JOB_NUMBER = @JOB_NUMBER AND A.JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR;

	UPDATE @ALERTS SET GENERATED = A3.GENERATED_DATE
	FROM @ALERTS AS A2
	INNER JOIN
	(
		SELECT A.ALERT_ID, MAX(AC.GENERATED_DATE) AS GENERATED_DATE
		FROM @ALERTS AS A 
		INNER JOIN ALERT_COMMENT AS AC WITH(NOLOCK) ON A.ALERT_ID = AC.ALERT_ID
		WHERE AC.GENERATED_DATE > A.GENERATED
		GROUP BY A.ALERT_ID
	) AS A3 ON A2.ALERT_ID = A3.ALERT_ID

	UPDATE @ALERTS SET IS_ASSIGNMENT = 1
	FROM @ALERTS AS ALRT
	WHERE ALRT.ALERT_ID IN
	(
		SELECT A.ALERT_ID
		FROM ALERT_RCPT AS AR WITH(NOLOCK) INNER JOIN @ALERTS AS A ON AR.ALERT_ID = A.ALERT_ID
		WHERE AR.CURRENT_NOTIFY = 1
		UNION
		SELECT A.ALERT_ID
		FROM ALERT_RCPT_DISMISSED AS AR WITH(NOLOCK) INNER JOIN @ALERTS AS A ON AR.ALERT_ID = A.ALERT_ID
		WHERE AR.CURRENT_NOTIFY = 1
	);

	--  LATEST ALERT
	SELECT
		TOP 1 
			@LAST_ALERT_ID = A.ALERT_ID,
			@LAST_ALERT_SUBJECT = A.[SUBJECT],
			@LAST_ALERT_DATE = A.GENERATED
	FROM
		@ALERTS AS A
	WHERE
		A.IS_ASSIGNMENT = 0
	ORDER BY
		A.GENERATED DESC;

	--  LATEST ASSIGNMENT
	SELECT
		TOP 1 
			@LAST_ALERT_ASSIGNMENT_ID = A.ALERT_ID,
			@LAST_ALERT_ASSIGNMENT_SUBJECT = A.[SUBJECT],
			@LAST_ALERT_ASSIGNMENT_DATE = A.GENERATED
	FROM
		@ALERTS AS A
	WHERE
		A.IS_ASSIGNMENT = 1
	ORDER BY
		A.GENERATED DESC;

	SELECT @TOTAL_ALERT_COUNT = COUNT(ALERT_ID) FROM @ALERTS AS A WHERE IS_ASSIGNMENT = 0;
	SELECT @TOTAL_ALERT_ASSIGNMENT_COUNT = COUNT(ALERT_ID) FROM @ALERTS AS A WHERE IS_ASSIGNMENT = 1;

	SELECT
		@OPEN_ALERT_COUNT = COUNT(DISTINCT A.ALERT_ID)
	FROM 
		@ALERTS AS A 
		INNER JOIN ALERT_RCPT AS AR WITH(NOLOCK) ON A.ALERT_ID = AR.ALERT_ID
	WHERE
		A.IS_ASSIGNMENT = 0;

	SELECT
		@OPEN_ALERT_ASSIGNMENT_COUNT = COUNT(DISTINCT A.ALERT_ID)
	FROM 
		@ALERTS AS A 
		INNER JOIN ALERT_RCPT AS AR WITH(NOLOCK) ON A.ALERT_ID = AR.ALERT_ID
	WHERE
		A.IS_ASSIGNMENT = 1
		AND AR.CURRENT_NOTIFY = 1;

	IF @TOTAL_ALERT_COUNT > 0
	BEGIN
		SET @PERCENT_COMPLETE_ALERTS = ((@TOTAL_ALERT_COUNT - @OPEN_ALERT_COUNT) / @TOTAL_ALERT_COUNT) * 100;
	END

END

-- TASKS
BEGIN

	SELECT 
		@TOTAL_TASKS_COUNT = COUNT(1)
	FROM
		JOB_TRAFFIC_DET AS JTD WITH(NOLOCK)
	WHERE
		JTD.JOB_NUMBER = @JOB_NUMBER
		AND JTD.JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR;

	SELECT 
		@OPEN_TASKS_COUNT = COUNT(1),
		@TASK_MIN_START = MIN(JTD.TASK_START_DATE),
		@TASK_MAX_DUE = MAX(JTD.JOB_REVISED_DATE),
		@TOTAL_TASK_HOURS = SUM(JTD.JOB_HRS)
	FROM
		JOB_TRAFFIC_DET AS JTD WITH(NOLOCK)
	WHERE
		JTD.JOB_COMPLETED_DATE IS NULL
		AND JTD.JOB_NUMBER = @JOB_NUMBER
		AND JTD.JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR;
		
	IF @TOTAL_TASKS_COUNT > 0
	BEGIN
		SET @PERCENT_COMPLETE_TASKS = ((@TOTAL_TASKS_COUNT - @OPEN_TASKS_COUNT) / @TOTAL_TASKS_COUNT) * 100;
	END

	SELECT @PROJECTED_HOURS = SUM(HOURS_ALLOWED) FROM JOB_TRAFFIC_DET_EMPS WITH(NOLOCK)  WHERE JOB_NUMBER = @JOB_NUMBER AND JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR;

	SELECT
		@ACTUAL_EMP_TASK_HOURS = SUM(ETD.EMP_HOURS)
	FROM
		EMP_TIME_DTL AS ETD
	WHERE
		ETD.JOB_NUMBER = @JOB_NUMBER
		AND ETD.JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR;

	SELECT 
		@TASK_HOURS_UNASSIGNED = SUM(JTD.JOB_HRS)
	FROM 
		JOB_TRAFFIC_DET AS JTD WITH(NOLOCK) 
		LEFT OUTER JOIN JOB_TRAFFIC_DET_EMPS AS JTDE WITH(NOLOCK)
		ON JTD.JOB_NUMBER = JTDE.JOB_NUMBER AND JTD.JOB_COMPONENT_NBR = JTDE.JOB_COMPONENT_NBR AND JTD.SEQ_NBR = JTDE.SEQ_NBR
	WHERE
		JTD.JOB_NUMBER = @JOB_NUMBER
		AND JTD.JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR
		AND JTDE.SEQ_NBR IS NULL

		
END

-- ESTIMATE
BEGIN

	SELECT        
		@ESTIMATE_APPROVAL_STATUS_CLIENT_DATE = EA.EST_APPR_DATE, 
		@ESTIMATE_APPROVAL_STATUS_CLIENT = EA.EST_APPR_BY,
		@ESTIMATE_APPROVAL_CLIENT_COMMENT = EA.APPR_NOTES
	FROM            
		ESTIMATE_APPROVAL AS EA INNER JOIN
		JOB_COMPONENT AS JC ON EA.JOB_NUMBER = JC.JOB_NUMBER AND EA.JOB_COMPONENT_NBR = JC.JOB_COMPONENT_NBR
	WHERE
		JC.JOB_NUMBER = @JOB_NUMBER
		AND JC.JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR;

	SELECT        
		@ESTIMATE_APPROVAL_STATUS_INTERNAL_DATE = EIA.EST_APPR_DATE, 
		@ESTIMATE_APPROVAL_STATUS_INTERNAL = EIA.EST_APPR_BY,
		@ESTIMATE_APPROVAL_INTERNAL_COMMENT = EIA.APPR_NOTES
	FROM            
		ESTIMATE_INT_APPR AS EIA INNER JOIN
		JOB_COMPONENT AS JC ON EIA.JOB_NUMBER = JC.JOB_NUMBER AND EIA.JOB_COMPONENT_NBR = JC.JOB_COMPONENT_NBR
	WHERE
		JC.JOB_NUMBER = @JOB_NUMBER
		AND JC.JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR;

END

-- QvA
BEGIN

	DECLARE @QVA TABLE (JOB_NUMBER INT, JOB_COMPONENT_NBR SMALLINT, QUOTED_AMT DECIMAL(14,3), ACTUAL_AMT DECIMAL (14,3), QUOTED_HRS DECIMAL (14,3), ACTUAL_HRS DECIMAL (14,3))

	INSERT INTO @QVA
	EXEC [usp_wv_dto_qva_JobComp] 'E', @USER_CODE, @JOB_NUMBER, @JOB_COMPONENT_NBR;

	SELECT 
		@QUOTED_HOURS = ISNULL(QVA.QUOTED_HRS, 0.00),
		@ACTUAL_HOURS = ISNULL(QVA.ACTUAL_HRS, 0.00),
		@QUOTED_DOLLARS = ISNULL(QVA.QUOTED_AMT, 0.00),
		@ACTUAL_DOLLARS = ISNULL(QVA.ACTUAL_AMT, 0.00)
	FROM
		@QVA AS QVA;

	SET @REMAINING_HOURS = @QUOTED_HOURS - @ACTUAL_HOURS;
	SET @REMAINING_DOLLARS = @QUOTED_DOLLARS - @ACTUAL_DOLLARS;

	IF @QUOTED_HOURS > 0
	BEGIN
		SET @PERCENT_COMPLETE = (@ACTUAL_HOURS / @QUOTED_HOURS) * 100;
	END

END

-- Billing Approval Batch
BEGIN

	SELECT     
		@BATCH_TEXT = ISNULL(
		RIGHT(REPLICATE('0', 6) 
		+ CONVERT(VARCHAR(20), BILL_APPR_BATCH.BA_BATCH_ID), 6) + ' - ' + ISNULL(BILL_APPR_BATCH.BA_BATCH_DESC, '') 
		,''), 
		@BA_BATCH_ID = ISNULL(BILL_APPR_BATCH.BA_BATCH_ID,0), 
		@BA_BATCH_DESC = ISNULL(BILL_APPR_BATCH.BA_BATCH_DESC,''), 
		@BA_BATCH_CREATE_USER = ISNULL(BILL_APPR_BATCH.CREATE_USER,''), 
		@BA_BATCH_BATCH_DATE = BILL_APPR_BATCH.BATCH_DATE, 
		@BILLING_USER = ISNULL(JOB_COMPONENT.BILLING_USER,''), 
		@AB_FLAG = JOB_COMPONENT.AB_FLAG, 
		@AB_TEXT = CASE 
			WHEN JOB_COMPONENT.AB_FLAG = 0 OR JOB_COMPONENT.AB_FLAG IS NULL THEN 'No' 
			ELSE 'Yes' 
		END
	FROM         
		JOB_COMPONENT WITH (NOLOCK) LEFT OUTER JOIN
		BILL_APPR_BATCH WITH (NOLOCK) ON JOB_COMPONENT.BA_BATCH_ID = BILL_APPR_BATCH.BA_BATCH_ID
	WHERE     
		(JOB_COMPONENT.JOB_NUMBER = @JOB_NUMBER) 
		AND (JOB_COMPONENT.JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR);

END

-- RETURN
BEGIN 
	SELECT 
		@JOB_NUMBER AS JobNumber,
		@JOB_DESCRIPTION AS JobDescription,
		@JOB_COMPONENT_NBR AS JobComponentNumber,
		@JOB_COMPONENT_DESCRIPTION AS JobComponentDescription,

		@OFFICE_DISPLAY AS Office,
		@CLIENT_DISPLAY AS Client,
		@DIVISION_DISPLAY AS Division,
		@PRODUCT_DISPLAY AS Product,

		@SCHEDULE_STATUS_CODE AS ScheduleStatusCode,
		@SCHEDULE_STATUS_DESC AS ScheduleStatusDescription,
		@SCHEDULE_COMMENTS AS ScheduleComments,

		@ESTIMATE_APPROVAL_STATUS_CLIENT AS EstimateApprovalStatusClient,
		@ESTIMATE_APPROVAL_STATUS_CLIENT_DATE AS EstimateApprovalStatusClientDate,
		@ESTIMATE_APPROVAL_STATUS_INTERNAL AS EstimateApprovalStatusInternal,
		@ESTIMATE_APPROVAL_STATUS_INTERNAL_DATE AS EstimateApprovalStatusInternalDate,
		@ESTIMATE_APPROVAL_CLIENT_COMMENT AS EstimateApprovalStatusClientComment,
		@ESTIMATE_APPROVAL_INTERNAL_COMMENT AS EstimateApprovalStatusInternalComment,

		ISNULL(@QUOTED_HOURS, 0.00) AS QuotedHours,
		ISNULL(@ACTUAL_HOURS, 0.00) AS ActualHours,
		ISNULL(@REMAINING_HOURS, 0.00) AS RemainingHours,
		ISNULL(@QUOTED_DOLLARS, 0.00) AS QuotedDollars,
		ISNULL(@ACTUAL_DOLLARS, 0.00) AS ActualDollars,
		ISNULL(@REMAINING_DOLLARS, 0.00) AS RemainingDollars,
		ISNULL(@PERCENT_COMPLETE, 0.00) AS PercentComplete,

		ISNULL(@PROJECTED_HOURS, 0.00) AS ProjectedHours,
		ISNULL(@TOTAL_TASK_HOURS, 0.00) AS TotalTaskHours,
		ISNULL(@TASK_ACTUAL_HOURS, 0.00) AS TaskActualHours,
		ISNULL(@ACTUAL_EMP_TASK_HOURS, 0.00) AS ActualEmployeeTaskHours,

		ISNULL(@LAST_ALERT_ID, 0) AS LastAlertID,
		@LAST_ALERT_SUBJECT AS LastAlertSubject,
		@LAST_ALERT_DATE AS LastAlertDate,
		ISNULL(@OPEN_ALERT_COUNT, 0) AS OpenAlertCount,
		ISNULL(@TOTAL_ALERT_COUNT, 0) AS TotalAlertCount,

		ISNULL(@LAST_ALERT_ASSIGNMENT_ID, 0) AS LastAssignmentID,
		@LAST_ALERT_ASSIGNMENT_SUBJECT AS LastAssignmentSubject,
		@LAST_ALERT_ASSIGNMENT_DATE AS LastAssignmentDate,
		ISNULL(@OPEN_ALERT_ASSIGNMENT_COUNT, 0) AS OpenAssignmentCount,
		ISNULL(@TOTAL_ALERT_ASSIGNMENT_COUNT, 0) AS TotalAssignmentCount,

		ISNULL(@OPEN_TASKS_COUNT, 0) AS OpenTasksCount,
		ISNULL(@TOTAL_TASKS_COUNT, 0) AS TotalTasksCount,
		@TASK_MIN_START AS TaskMinStartDate,
		@TASK_MAX_DUE AS TaskMaxDueDate,

		ISNULL(@PERCENT_COMPLETE_SCHEDULE_HOURS, 0.00) AS PercentCompleteScheduleHours,
		ISNULL(@PERCENT_COMPLETE_ALERTS, 0.00) AS PercentCompleteAlerts,
		ISNULL(@PERCENT_COMPLETE_TASKS, 0.00) AS PercentCompleteTasks,

		@AE_EMP_CODE AS AccountExecutiveEmployeeCode,
		@AE_EMP_FML AS AccountExecutiveFullName,

		@SCHEDULE_MANAGER_CODE AS ScheduleManagerEmployeeCode,
		@SCHEDULE_MANAGER_FML AS ScheduleManagerFullName,

		@SCHEDULE_ASSIGNMENT_1_DISPLAY AS Assignment1Label,
		@SCHEDULE_ASSIGNMENT_1 AS Assignment1EmployeeCode,
		@SCHEDULE_ASSIGNMENT_1_FML AS Assignment1FullName,

		@SCHEDULE_ASSIGNMENT_2_DISPLAY AS Assignment2Label,
		@SCHEDULE_ASSIGNMENT_2 AS Assignment2EmployeeCode,
		@SCHEDULE_ASSIGNMENT_2_FML AS Assignment2FullName,

		@SCHEDULE_ASSIGNMENT_3_DISPLAY AS Assignment3Label,
		@SCHEDULE_ASSIGNMENT_3 AS Assignment3EmployeeCode,
		@SCHEDULE_ASSIGNMENT_3_FML AS Assignment3FullName,

		@SCHEDULE_ASSIGNMENT_4_DISPLAY AS Assignment4Label,
		@SCHEDULE_ASSIGNMENT_4 AS Assignment4EmployeeCode,
		@SCHEDULE_ASSIGNMENT_4_FML AS Assignment4FullName,

		@SCHEDULE_ASSIGNMENT_5_DISPLAY AS Assignment5Label,
		@SCHEDULE_ASSIGNMENT_5 AS Assignment5EmployeeCode,
		@SCHEDULE_ASSIGNMENT_5_FML AS Assignment5FullName,

		@SCHEDULE_START_DATE AS ScheduleStartDate,
		@SCHEDULE_DUE_DATE AS ScheduleDueDate,
		@SCHEDULE_END_DATE AS ScheduleEndDate,
		@SCHEDULE_COMPLETED_DATE AS ScheduleCompletedDate,
		@TASK_HOURS_UNASSIGNED AS TaskHoursUnassigned,
		ISNULL(@SCHEDULE_GUT_PERCENT_COMPLETE, 0.00) AS ScheduleGutPercentComplete,
		ISNULL(@DURATION, 0.00) AS Duration,
		ISNULL(@JOB_PROCESS_CONTROL, 0) AS JobProcessControlID,
		@JOB_PROCESS_CONTROL_TEXT AS JobProcessControl,
		ISNULL(@JOB_IS_BILLABLE, 0) AS JobIsBillable,
		ISNULL(@APPROVED_ESTIMATE_REQUIRED, 0) AS ApprovedEstimateRequired,

		@BATCH_TEXT AS BatchText,
		@BA_BATCH_ID AS BatchID,
		@BA_BATCH_DESC AS BatchDescription,
		@BA_BATCH_CREATE_USER AS BatchCreatedByUser,
		@BA_BATCH_BATCH_DATE AS BatchDate,
		@BILLING_USER AS BillingUser,
		@AB_FLAG AS ABFlag,
		@AB_TEXT AS ABFlagText


END

/*================ QUERY ================*/
END