CREATE PROCEDURE [dbo].[advsp_job_time_report]
	@StartDate SMALLDATETIME,
	@EndDate SMALLDATETIME
AS
BEGIN

	SELECT
		[ID] = NEWID(),
		[JobNumber] = JC.JOB_NUMBER,
		[JobDescription] = JL.JOB_DESC,
		[JobComponentNumber] = JC.JOB_COMPONENT_NBR,
		[JobComponentDescription] = JC.JOB_COMP_DESC,
		[JobComponent] = RIGHT(REPLICATE('0', 6) + CONVERT(VARCHAR(20), JC.JOB_NUMBER), 6) + '-' + RIGHT(REPLICATE('0', 2) + CONVERT(VARCHAR(20), JC.JOB_COMPONENT_NBR), 2),
		[Hours] = SUM(ETD.EMP_HOURS),
		[HoursAmount] = SUM(ETD.TOTAL_BILL),
		[HoursWithMarkupOrMarkdown] = SUM(ETD.TOTAL_BILL) + SUM(ETD.EXT_MARKUP_AMT), 
		[TotalAmountWithResaleTax] = SUM(ETD.LINE_TOTAL)
	FROM
		dbo.JOB_COMPONENT JC
	INNER JOIN
		dbo.JOB_LOG JL ON JC.JOB_NUMBER = JL.JOB_NUMBER
	INNER JOIN
		dbo.EMP_TIME_DTL ETD ON JC.JOB_NUMBER = ETD.JOB_NUMBER AND
								JC.JOB_COMPONENT_NBR = ETD.JOB_COMPONENT_NBR
	INNER JOIN
		dbo.EMP_TIME ET ON ETD.ET_ID = ET.ET_ID
	WHERE
		ET.EMP_DATE BETWEEN @StartDate AND @EndDate
	GROUP BY
		JC.JOB_NUMBER,
		JL.JOB_DESC,
		JC.JOB_COMPONENT_NBR,
		JC.JOB_COMP_DESC
	HAVING
		SUM(ETD.EMP_HOURS) > 0

END