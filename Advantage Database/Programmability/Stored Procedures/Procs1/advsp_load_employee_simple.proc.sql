--DROP PROCEDURE [dbo].[advsp_load_employee_simple] 
CREATE PROCEDURE [dbo].[advsp_load_employee_simple] 
@USER_CODE VARCHAR(100),
@EMP_CODE VARCHAR(6),
@DP_TM_CODE VARCHAR(6),
@EMAIL_GR_CODE VARCHAR(50),
@JOB_NUMBER INT,
@JOB_COMPONENT_NBR SMALLINT,
@SEQ_NBR SMALLINT,
@IS_AE AS BIT,
@IS_ROLE BIT,
@IS_EMAIL_GROUP BIT,
@ONLY_ACTIVE BIT
AS
BEGIN
/*================ QUERY ================*/
	DECLARE		
		@RESTRICTED_USER AS INT,
		@SQL NVARCHAR(MAX),
		@PARAMS NVARCHAR(500),
		@CL_CODE VARCHAR(6),
		@DIV_CODE VARCHAR(6),
		@PRD_CODE VARCHAR(6),
		@FNC_CODE VARCHAR(10),
		@OFFICE_RESTRICTIONS AS INT,
		@SEC_USER_EMP_CODE AS VARCHAR(6)	

	IF NOT @JOB_NUMBER IS NULL
	BEGIN
		SELECT @CL_CODE = CL_CODE, @DIV_CODE = DIV_CODE, @PRD_CODE = PRD_CODE FROM JOB_LOG WITH(NOLOCK) WHERE JOB_NUMBER = @JOB_NUMBER;
	END
	IF NOT @JOB_NUMBER IS NULL AND NOT @JOB_COMPONENT_NBR IS NULL
	BEGIN
		SELECT @EMAIL_GR_CODE = EMAIL_GR_CODE
		FROM JOB_COMPONENT WITH(NOLOCK) 
		WHERE JOB_NUMBER = @JOB_NUMBER AND JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR;
	END
	IF NOT @JOB_NUMBER IS NULL AND NOT @JOB_COMPONENT_NBR IS NULL AND NOT @SEQ_NBR IS NULL
	BEGIN
		SELECT @FNC_CODE = FNC_CODE
		FROM JOB_TRAFFIC_DET WITH(NOLOCK) 
		WHERE JOB_NUMBER = @JOB_NUMBER AND JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR AND SEQ_NBR = @SEQ_NBR;
	END
	SELECT @RESTRICTED_USER = COUNT(1) FROM SEC_EMP WITH(NOLOCK) WHERE UPPER([USER_ID]) = UPPER(@USER_CODE);
	SET @SEC_USER_EMP_CODE = (SELECT TOP 1 EMP_CODE FROM SEC_USER WITH(NOLOCK)  WHERE UPPER(USER_CODE) = UPPER(@USER_CODE));
	SELECT @OFFICE_RESTRICTIONS = COUNT(1) FROM EMP_OFFICE WITH(NOLOCK)  WHERE EMP_CODE = @SEC_USER_EMP_CODE;
	SET @IS_AE = ISNULL(@IS_AE, 0);
	SET @IS_ROLE = ISNULL(@IS_ROLE, 0);
	SET @IS_EMAIL_GROUP = ISNULL(@IS_EMAIL_GROUP, 0);
	SET @ONLY_ACTIVE = ISNULL(@ONLY_ACTIVE, 0);
	SET @RESTRICTED_USER = ISNULL(@RESTRICTED_USER, 0);
	SET @OFFICE_RESTRICTIONS = ISNULL(@OFFICE_RESTRICTIONS, 0);
	SET @SQL = '
		SELECT 
			A.*,
			EP.EMP_NICKNAME AS NickName,
			EP.EMP_IMAGE AS Picture 
		FROM (
		SELECT
			E.EMP_CODE AS Code,
			E.EMP_FNAME AS FirstName,
			E.EMP_MI AS MiddleInitial,
			E.EMP_LNAME AS LastName
		FROM
			EMPLOYEE E WITH(NOLOCK)
			LEFT OUTER JOIN EMPLOYEE_PICTURE EP WITH(NOLOCK) ON E.EMP_CODE = EP.EMP_CODE
	'
	IF @RESTRICTED_USER > 0 AND @IS_AE = 0 AND (NOT @USER_CODE IS NULL)
	BEGIN
		SET @SQL = @SQL + '
			INNER JOIN SEC_EMP SE WITH(NOLOCK) ON E.EMP_CODE = SE.EMP_CODE AND UPPER(SE.USER_ID) = UPPER(@USER_CODE)
		'
	END
	IF @OFFICE_RESTRICTIONS > 0 AND @IS_AE = 0 AND (NOT @SEC_USER_EMP_CODE IS NULL)
	BEGIN
		SET @SQL = @SQL + '
			INNER JOIN EMP_OFFICE ON E.OFFICE_CODE = EMP_OFFICE.OFFICE_CODE AND EMP_OFFICE.EMP_CODE = ''' + @SEC_USER_EMP_CODE + '''
		'
	END
	IF @IS_AE = 1
	BEGIN
		SET @SQL = @SQL + '
			INNER JOIN ACCOUNT_EXECUTIVE AE WITH(NOLOCK) ON E.EMP_CODE = AE.EMP_CODE
		'
	END
	SET @SQL = @SQL + '	
		WHERE 1 = 1
		'
	IF @ONLY_ACTIVE = 1
	BEGIN
		SET @SQL = @SQL + '	
			AND (E.EMP_TERM_DATE IS NULL OR E.EMP_TERM_DATE > GETDATE())
		'
	END
	IF NOT @EMP_CODE IS NULL
	BEGIN
		SET @SQL = @SQL + '	
			AND E.EMP_CODE = @EMP_CODE
		'
	END
	IF @IS_AE = 1
	BEGIN
		SET @SQL = @SQL + '
			AND AE.CL_CODE = @CL_CODE
			AND AE.DIV_CODE = @DIV_CODE
			AND AE.PRD_CODE = @PRD_CODE
		'
	END
	IF @IS_ROLE = 1 AND NOT @FNC_CODE IS NULL
	BEGIN
		SET @SQL = @SQL + '
			AND E.EMP_CODE IN (
								SELECT DISTINCT 
									EMP_TRAFFIC_ROLE.EMP_CODE
								FROM         
									TASK_TRAFFIC_ROLE WITH(NOLOCK) INNER JOIN
									TRAFFIC_ROLE WITH(NOLOCK) ON TASK_TRAFFIC_ROLE.ROLE_CODE = TRAFFIC_ROLE.ROLE_CODE INNER JOIN
									EMP_TRAFFIC_ROLE WITH(NOLOCK) ON TRAFFIC_ROLE.ROLE_CODE = EMP_TRAFFIC_ROLE.ROLE_CODE INNER JOIN
									EMPLOYEE WITH(NOLOCK) ON EMP_TRAFFIC_ROLE.EMP_CODE = EMPLOYEE.EMP_CODE
								WHERE     
									(TASK_TRAFFIC_ROLE.ROLE_CODE IN (
																	SELECT DISTINCT    
																		TRAFFIC_ROLE.ROLE_CODE
																	FROM         
																		TASK_TRAFFIC_ROLE WITH(NOLOCK) INNER JOIN
																		TRAFFIC_ROLE WITH(NOLOCK) ON TASK_TRAFFIC_ROLE.ROLE_CODE = TRAFFIC_ROLE.ROLE_CODE
																	WHERE     
																		(TASK_TRAFFIC_ROLE.TRF_CODE = @FNC_CODE)
																		AND ((TRAFFIC_ROLE.INACTIVE_FLAG IS NULL) OR (TRAFFIC_ROLE.INACTIVE_FLAG = 0))
																	)) 
			)
		'
	END
	IF NOT @EMAIL_GR_CODE IS NULL AND @IS_EMAIL_GROUP = 1
	BEGIN
		SET @SQL = @SQL + '
			AND E.EMP_CODE IN (
								SELECT DISTINCT	
									EMP_CODE
								FROM
									EMAIL_GROUP WITH(NOLOCK)
								WHERE
									EMAIL_GR_CODE = @EMAIL_GR_CODE
			)
		'
	END
	IF NOT @DP_TM_CODE IS NULL
	BEGIN
		SET @SQL = @SQL + '
			AND E.EMP_CODE IN (
								SELECT DISTINCT	
									EMP_CODE
								FROM
									EMP_DP_TM WITH(NOLOCK)
								WHERE
									DP_TM_CODE = @DP_TM_CODE
			)
		'
	END
	-- GET ANYONE ON THE TASK THAT IS NOT PART OF A FILTER INTO THE LIST
	IF NOT @JOB_NUMBER IS NULL AND NOT @JOB_COMPONENT_NBR IS NULL AND NOT @SEQ_NBR IS NULL
	BEGIN
		SET @SQL = @SQL + '
			UNION
			SELECT
				E.EMP_CODE AS Code,
				E.EMP_FNAME AS FirstName,
				E.EMP_MI AS MiddleInitial,
				E.EMP_LNAME AS LastName
			FROM
				EMPLOYEE E WITH(NOLOCK)
				INNER JOIN JOB_TRAFFIC_DET_EMPS JTDE ON E.EMP_CODE = JTDE.EMP_CODE
			WHERE
				JTDE.JOB_NUMBER = @JOB_NUMBER
				AND JTDE.JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR
				AND JTDE.SEQ_NBR = @SEQ_NBR
		'
	END
	BEGIN
		SET @SQL = @SQL + '	
		) AS A
		LEFT OUTER JOIN EMPLOYEE_PICTURE EP WITH(NOLOCK) ON A.Code = EP.EMP_CODE
		ORDER BY
			A.FirstName,
			A.MiddleInitial,
			A.LastName
	';
	END

	SET @PARAMS = '@USER_CODE VARCHAR(100), @EMP_CODE VARCHAR(6), @CL_CODE VARCHAR(6), @DIV_CODE VARCHAR(6), @PRD_CODE VARCHAR(6), @FNC_CODE VARCHAR(10), @EMAIL_GR_CODE VARCHAR(6), @DP_TM_CODE VARCHAR(6), @JOB_NUMBER INT, @JOB_COMPONENT_NBR SMALLINT, @SEQ_NBR SMALLINT';

	EXECUTE sp_executesql @SQL, @PARAMS, @USER_CODE, @EMP_CODE, @CL_CODE, @DIV_CODE, @PRD_CODE, @FNC_CODE, @EMAIL_GR_CODE, @DP_TM_CODE, @JOB_NUMBER, @JOB_COMPONENT_NBR, @SEQ_NBR;

	--PRINT @SQL

/*================ QUERY ================*/
END