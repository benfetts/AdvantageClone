CREATE PROCEDURE [dbo].[advsp_media_broadcast_worksheet_other_market_schedule_weekly_detail_report]
	@MEDIA_BROADCAST_WORKSHEET_ID int, @MEDIA_BROADCAST_WORKSHEET_MARKET_ID_VENDOR_CODES varchar(max), @DATE_START date, @DATE_END date, @SHOW_SECONDARY_DEMO bit
AS
BEGIN
	DECLARE	@StartDate smalldatetime,
			@Days smallint,
			@EndDate smalldatetime

	DECLARE @Weeks TABLE (
		WeekOf smalldatetime
	)
	
	SET @StartDate = @DATE_START
	SET @Days = DATEDIFF(d, @DATE_START, @DATE_END)
	SET @EndDate = @DATE_END

	SELECT @StartDate = CASE DATEPART(dw, @StartDate)
						WHEN 1 THEN DATEADD(d,-6,@StartDate)
						WHEN 2 THEN @StartDate
						WHEN 3 THEN DATEADD(d,-1,@StartDate)
						WHEN 4 THEN DATEADD(d,-2,@StartDate)
						WHEN 5 THEN DATEADD(d,-3,@StartDate)
						WHEN 6 THEN DATEADD(d,-4,@StartDate)
						WHEN 7 THEN DATEADD(d,-5,@StartDate)
						END
						
	SELECT @EndDate  = CASE DATEPART(dw, @EndDate)
						WHEN 1 THEN DATEADD(d,-6,@EndDate)
						WHEN 2 THEN @EndDate
						WHEN 3 THEN DATEADD(d,-1,@EndDate)
						WHEN 4 THEN DATEADD(d,-2,@EndDate)
						WHEN 5 THEN DATEADD(d,-3,@EndDate)
						WHEN 6 THEN DATEADD(d,-4,@EndDate)
						WHEN 7 THEN DATEADD(d,-5,@EndDate)
						END

	WHILE @Days > 0
	BEGIN
		INSERT INTO @Weeks (WeekOf) VALUES (@StartDate)
		SET @StartDate = DATEADD(day,7,@StartDate)
		SET @Days = @Days - 7
	END

	IF NOT EXISTS(SELECT 1 FROM @Weeks WHERE WeekOf = @EndDate)
		INSERT INTO @Weeks (WeekOf) VALUES (@EndDate)

	SELECT
		ClientName = C.CL_NAME,
		DivisionName = D.DIV_NAME,
		ProductDescription = P.PRD_DESCRIPTION,
		MediaBroadcastWorksheetName = MBW.[NAME],
		MarketName = M.MARKET_DESC + CASE WHEN MBWM.IS_CABLE = 1 THEN ' - CA' ELSE '' END,
		VendorCode = MBWMD.VN_CODE,
		VendorName = V.VN_NAME,
		FlightDates = CONVERT(CHAR(10),MBW.[START_DATE], 101) + ' - ' + CONVERT(CHAR(10),MBW.[END_DATE], 101),
		StationName = CASE 
						WHEN MBWM.IS_CABLE = 0 THEN 
							CASE WHEN V.IS_CABLE_SYSTEM = 1 THEN V.VN_CODE
							ELSE V.VN_NAME
							END
						WHEN MBWM.IS_CABLE = 1 THEN V.VN_CODE
						ELSE '' 
						END,
		wo.WeekOf,
		[Days] = MBWMD.[DAYS],
		[Time] = dbo.advfn_format_airtime(MBWMD.START_TIME, MBWMD.END_TIME),
		Program = CASE WHEN NULLIF(MBWMD.CABLE_NETWORK_STATION_CODE,'') IS NOT NULL THEN MBWMD.CABLE_NETWORK_STATION_CODE + '/' + MBWMD.PROGRAM
					ELSE MBWMD.PROGRAM END,
		Daypart = DP.[DAY_PART_CODE],
		[Len] = MBWMD.[LENGTH],
		Monday = CASE WHEN MBWMD.MONDAY = 1 THEN 'x' ELSE '' END,
		Tuesday = CASE WHEN MBWMD.TUESDAY = 1 THEN 'x' ELSE '' END,
		Wednesday = CASE WHEN MBWMD.WEDNESDAY = 1 THEN 'x' ELSE '' END,
		Thursday = CASE WHEN MBWMD.THURSDAY = 1 THEN 'x' ELSE '' END,
		Friday = CASE WHEN MBWMD.FRIDAY = 1 THEN 'x' ELSE '' END,
		Saturday = CASE WHEN MBWMD.SATURDAY = 1 THEN 'x' ELSE '' END,
		Sunday = CASE WHEN MBWMD.SUNDAY = 1 THEN 'x' ELSE '' END,
		Spots = (SELECT SUM(SPOTS) as SPOTS
					FROM dbo.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_DATE MBWMDD
					WHERE MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_ID = MBWMD.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_ID
					AND [DATE] BETWEEN wo.WeekOf AND DATEADD(d,6,wo.WeekOf)),
		Cost = (SELECT CAST(COALESCE(SUM(RATE * SPOTS), 0) as decimal(18,2))
				FROM dbo.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_DATE MBWMDD
				WHERE MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_ID = MBWMD.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_ID
				AND MBWMDD.[DATE] between @DATE_START AND @DATE_END),
		[NielsenTVStationCode] = CASE WHEN MBW.RATINGS_SERVICE_ID = 1 THEN COALESCE(V.NIELSEN_TV_STATION_CODE, MBWMD.CABLE_NETWORK_NIELSEN_TV_STATION_CODE)
										WHEN MBW.RATINGS_SERVICE_ID = 2 THEN COALESCE(V.COMSCORE_TV_STATION_ID, MBWMD.CABLE_NETWORK_NIELSEN_TV_STATION_CODE)
									END,
		NCCTVSyscodeID = V.NCC_TV_SYSCODE_ID,
		NielsenMarketNumber = CASE WHEN MBW.RATINGS_SERVICE_ID = 1 THEN CAST(COALESCE(M.NIELSEN_TV_CODE, 0) as integer)
									WHEN MBW.RATINGS_SERVICE_ID = 2 THEN CAST(COALESCE(M.COMSCORE_NEW_MARKET_NUMBER, 0) as integer)
									ELSE 0 END,
		DemographicDescription = CASE WHEN @SHOW_SECONDARY_DEMO = 1 THEN (SELECT [DESCRIPTION] FROM dbo.MEDIA_DEMO WHERE MEDIA_DEMO_ID = MBWM.SECONDARY_MEDIA_DEMO_ID) ELSE (SELECT [DESCRIPTION] FROM dbo.MEDIA_DEMO WHERE MEDIA_DEMO_ID = MBW.PRIMARY_MEDIA_DEMO_ID) END,
		MediaBroadcastWorksheetMarketDetailID = MBWMD.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_ID,
		DetailDaypart = DP.[DAY_PART_CODE],
		MediaBroadcastWorksheetMarketID = MBWM.MEDIA_BROADCAST_WORKSHEET_MARKET_ID,
		RatingsServiceID = MBW.RATINGS_SERVICE_ID,
		RatingSource = CASE MBW.MEDIA_TYPE_CODE 
						WHEN 'T' THEN MBRS.[NAME]
						WHEN 'R' THEN (CASE MBWM.EXTERNAL_RADIO_SOURCE WHEN 0 THEN 'Nielsen' WHEN 1 THEN 'Eastlan' WHEN 2 THEN 'Nielsen County' ELSE 'N/A' END)
						ELSE 'N/A' END,
		Rate = (SELECT RATE FROM dbo.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_DATE 
				WHERE MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_ID = MBWMD.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_ID
				AND [DATE] BETWEEN wo.WeekOf AND DATEADD(d,6,wo.WeekOf)),
		Rating = CASE 
					WHEN MBW.MEDIA_TYPE_CODE = 'T' THEN
						CASE WHEN @SHOW_SECONDARY_DEMO = 1 THEN MBWMD.SECONDARY_RATING ELSE MBWMD.PRIMARY_RATING END
					WHEN MBW.MEDIA_TYPE_CODE = 'R' THEN
						CASE WHEN @SHOW_SECONDARY_DEMO = 1 THEN MBWMD.SECONDARY_AQH_RATING ELSE MBWMD.PRIMARY_AQH_RATING END
					END,
		Impressions = CASE 
						WHEN MBW.MEDIA_TYPE_CODE = 'T' THEN
							CASE
							WHEN @SHOW_SECONDARY_DEMO = 1 THEN 
								CAST(CAST(MBWMD.SECONDARY_IMPRESSIONS as decimal) / 1000 as decimal(18,1))
							ELSE CAST(CAST(MBWMD.PRIMARY_IMPRESSIONS as decimal) / 1000 as decimal(18,1))
							END
						WHEN MBW.MEDIA_TYPE_CODE = 'R' THEN
							CASE
							WHEN @SHOW_SECONDARY_DEMO = 1 THEN 
								CAST(CAST(MBWMD.SECONDARY_AQH as decimal) / 100 as decimal(18,1))
							ELSE CAST(CAST(MBWMD.PRIMARY_AQH as decimal) / 100 as decimal(18,1))
							END
						END,
		ImpressionsRaw = CASE 
							WHEN MBW.MEDIA_TYPE_CODE = 'T' THEN
								CASE
								WHEN @SHOW_SECONDARY_DEMO = 1 THEN 
									MBWMD.SECONDARY_IMPRESSIONS
								ELSE MBWMD.PRIMARY_IMPRESSIONS
								END
							WHEN MBW.MEDIA_TYPE_CODE = 'R' THEN
								CASE
								WHEN @SHOW_SECONDARY_DEMO = 1 THEN 
									MBWMD.SECONDARY_AQH
								ELSE MBWMD.PRIMARY_AQH
								END
							END,
		SharebookID = MBWM.SHAREBOOK_NIELSEN_TV_BOOK_ID,
		HPUTbookID = MBWM.HUTPUT_NIELSEN_TV_BOOK_ID,
		MediaType = MBW.MEDIA_TYPE_CODE,
		StartHour = MBWMD.START_HOUR,
		EndHour = MBWMD.END_HOUR,
		RadioPeriod1 = MBWM.NIELSEN_RADIO_PERIOD_ID1,
		RadioPeriod2 = MBWM.NIELSEN_RADIO_PERIOD_ID2,
		RadioPeriod3 = MBWM.NIELSEN_RADIO_PERIOD_ID3,
		RadioPeriod4 = MBWM.NIELSEN_RADIO_PERIOD_ID4,
		RadioPeriod5 = MBWM.NIELSEN_RADIO_PERIOD_ID5
	FROM @Weeks wo, dbo.MEDIA_BROADCAST_WORKSHEET MBW
		INNER JOIN dbo.CLIENT C ON MBW.CL_CODE = C.CL_CODE
		INNER JOIN dbo.DIVISION D ON MBW.CL_CODE = D.CL_CODE AND MBW.DIV_CODE = D.DIV_CODE
		INNER JOIN dbo.PRODUCT P ON MBW.CL_CODE = P.CL_CODE AND MBW.DIV_CODE = P.DIV_CODE AND MBW.PRD_CODE = P.PRD_CODE 
		INNER JOIN dbo.MEDIA_BROADCAST_WORKSHEET_MARKET MBWM ON MBW.MEDIA_BROADCAST_WORKSHEET_ID = MBWM.MEDIA_BROADCAST_WORKSHEET_ID 
		INNER JOIN dbo.MARKET M ON MBWM.MARKET_CODE = M.MARKET_CODE 
		INNER JOIN dbo.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL MBWMD ON MBWM.MEDIA_BROADCAST_WORKSHEET_MARKET_ID = MBWMD.MEDIA_BROADCAST_WORKSHEET_MARKET_ID 
																		AND MBWMD.ON_HOLD = 0
																		AND MBWMD.REVISION_NUMBER = (SELECT MAX(REVISION_NUMBER)
																									FROM dbo.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL
																									WHERE MEDIA_BROADCAST_WORKSHEET_MARKET_ID = MBWM.MEDIA_BROADCAST_WORKSHEET_MARKET_ID)
		LEFT OUTER JOIN dbo.VENDOR V ON MBWMD.VN_CODE = V.VN_CODE
		LEFT OUTER JOIN dbo.DAY_PART DP ON MBWMD.DAY_PART_ID = DP.DAY_PART_ID 
		LEFT JOIN dbo.RATINGS_SERVICE MBRS ON MBRS.RATINGS_SERVICE_ID = MBW.RATINGS_SERVICE_ID 

	WHERE 
		MBW.MEDIA_BROADCAST_WORKSHEET_ID = @MEDIA_BROADCAST_WORKSHEET_ID
	AND	CAST(MBWMD.MEDIA_BROADCAST_WORKSHEET_MARKET_ID as varchar) + '|' + MBWMD.VN_CODE IN (SELECT items FROM dbo.udf_split_list(@MEDIA_BROADCAST_WORKSHEET_MARKET_ID_VENDOR_CODES, ','))
	ORDER BY MarketName, VendorName

END
GO
