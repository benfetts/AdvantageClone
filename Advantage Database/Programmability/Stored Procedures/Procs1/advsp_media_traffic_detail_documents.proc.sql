CREATE PROCEDURE [dbo].[advsp_media_traffic_detail_documents]
	@MEDIA_TRAFFIC_VENDOR_ID int
AS

BEGIN

	DECLARE @CableVendorWithoutCreativeGroupAssigned bit = 0
	
	IF (SELECT 1 FROM dbo.MEDIA_TRAFFIC_VENDOR MTV INNER JOIN dbo.VENDOR V ON MTV.VN_CODE = V.VN_CODE WHERE MTV.MEDIA_TRAFFIC_VENDOR_ID = @MEDIA_TRAFFIC_VENDOR_ID AND V.IS_CABLE_SYSTEM = 1) = 1
	BEGIN
		SELECT @CableVendorWithoutCreativeGroupAssigned = CASE WHEN COUNT(1) = 0 THEN 1 ELSE 0 END
		FROM dbo.MEDIA_TRAFFIC_VENDOR MTV
			INNER JOIN dbo.VENDOR V ON MTV.VN_CODE = V.VN_CODE 
			INNER JOIN dbo.MEDIA_TRAFFIC_CREATIVE_GROUP MTCG ON MTV.MEDIA_TRAFFIC_REVISION_ID = MTCG.MEDIA_TRAFFIC_REVISION_ID AND (MTCG.IS_DEFAULT = 1 OR V.IS_CABLE_SYSTEM = 1)
			LEFT OUTER JOIN dbo.MEDIA_TRAFFIC_VENDOR_CREATIVE_GROUP MTVCG ON MTVCG.MEDIA_TRAFFIC_CREATIVE_GROUP_ID = MTCG.MEDIA_TRAFFIC_CREATIVE_GROUP_ID AND MTV.MEDIA_TRAFFIC_VENDOR_ID = MTVCG.MEDIA_TRAFFIC_VENDOR_ID 
		WHERE MTVCG.MEDIA_TRAFFIC_VENDOR_ID = @MEDIA_TRAFFIC_VENDOR_ID 
	END
	
	DECLARE @DETAIL_ID TABLE (
		MEDIA_TRAFFIC_DETAIL_ID INT NOT NULL
	)

	INSERT INTO @DETAIL_ID 
	SELECT MTD.MEDIA_TRAFFIC_DETAIL_ID 
	FROM dbo.MEDIA_TRAFFIC_VENDOR MTV
		INNER JOIN dbo.VENDOR V ON MTV.VN_CODE = V.VN_CODE 
		INNER JOIN dbo.MEDIA_TRAFFIC_CREATIVE_GROUP MTCG ON MTV.MEDIA_TRAFFIC_REVISION_ID = MTCG.MEDIA_TRAFFIC_REVISION_ID AND (MTCG.IS_DEFAULT = 1 OR V.IS_CABLE_SYSTEM = 1)
		INNER JOIN dbo.MEDIA_TRAFFIC_DETAIL MTD ON MTCG.MEDIA_TRAFFIC_CREATIVE_GROUP_ID = MTD.MEDIA_TRAFFIC_CREATIVE_GROUP_ID 
	WHERE MTV.MEDIA_TRAFFIC_VENDOR_ID = @MEDIA_TRAFFIC_VENDOR_ID

	DECLARE @DOCUMENTS TABLE (
		[MIME_TYPE] varchar(255) NOT NULL,
		[REPOSITORY_FILENAME] varchar(200) NOT NULL,
		[DOCUMENT_ID] int NOT NULL,
		[MEDIA_TRAFFIC_DETAIL_ID] int NOT NULL,
		[FILENAME] varchar(300) NOT NULL,
		[DESCRIPTION] varchar(200) NULL
	)
	
	INSERT INTO @DOCUMENTS
	SELECT D.MIME_TYPE, D.REPOSITORY_FILENAME, D.DOCUMENT_ID, MTD.MEDIA_TRAFFIC_DETAIL_ID, D.[FILENAME], D.[DESCRIPTION] 
	FROM dbo.MEDIA_TRAFFIC_DETAIL MTD
		INNER JOIN MEDIA_TRAFFIC_DETAIL_DOCUMENT MTDD ON MTD.MEDIA_TRAFFIC_DETAIL_ID = MTDD.MEDIA_TRAFFIC_DETAIL_ID 
		INNER JOIN dbo.DOCUMENTS D ON MTDD.DOCUMENT_ID = D.DOCUMENT_ID 
	WHERE MTD.MEDIA_TRAFFIC_DETAIL_ID IN (SELECT MEDIA_TRAFFIC_DETAIL_ID FROM @DETAIL_ID)
	UNION
	SELECT D.MIME_TYPE, D.REPOSITORY_FILENAME, D.DOCUMENT_ID, MTD.MEDIA_TRAFFIC_DETAIL_ID, D.[FILENAME], D.[DESCRIPTION] 
	FROM dbo.MEDIA_TRAFFIC_DETAIL MTD
		INNER JOIN dbo.AD_NUMBER AN ON MTD.AD_NBR = AN.AD_NBR 
		INNER JOIN dbo.DOCUMENTS D ON AN.DOCUMENT_ID = D.DOCUMENT_ID 
	WHERE MTD.MEDIA_TRAFFIC_DETAIL_ID IN (SELECT MEDIA_TRAFFIC_DETAIL_ID FROM @DETAIL_ID)

	SELECT
		[ParentID] = MTD.MEDIA_TRAFFIC_DETAIL_ID,
		[DaypartDescription] = DP.[DESCRIPTION],
		[Length] = MTD.[LENGTH],
		[StartTime] = MTD.START_TIME,
		[EndTime] = MTD.END_TIME,
		[AdNumber] = MTD.AD_NBR,
		[CreativeTitle] = MTD.CREATIVE_TITLE,
		[Location] = MTD.[LOCATION],
		[IsBookend] = MTD.BOOKEND,
		[BookendName] = MTD.BOOKEND_NAME,
		[Position] = MTD.POSITION,
		[Rotation] = MTD.ROTATION,
		[Filename] = D.[FILENAME],
		[Description] = D.[DESCRIPTION],
		[IsURL] = CAST(CASE WHEN D.MIME_TYPE = 'URL' THEN 1 ELSE 0 END AS bit),
		[Link] = CASE WHEN D.MIME_TYPE = 'URL' THEN 
					CASE WHEN UPPER(D.[REPOSITORY_FILENAME]) LIKE '%HTTPS%' THEN '<a href="' + D.[REPOSITORY_FILENAME] + '" target="_blank" ><u><strong>View</strong></u></a>'
					ELSE '<a href="http://' + D.[REPOSITORY_FILENAME] + '" target="_blank" ><u><strong>View</strong></u></a>'
					END
				ELSE ''
				END,
		[DocumentID] = D.DOCUMENT_ID,
		[CableNetworkStationCodes] = MTVCG.CABLE_NETWORK_STATION_CODES
	FROM dbo.MEDIA_TRAFFIC_VENDOR MTV
		INNER JOIN dbo.VENDOR V ON MTV.VN_CODE = V.VN_CODE 
		INNER JOIN dbo.MEDIA_TRAFFIC_CREATIVE_GROUP MTCG ON MTV.MEDIA_TRAFFIC_REVISION_ID = MTCG.MEDIA_TRAFFIC_REVISION_ID AND (MTCG.IS_DEFAULT = 1 OR V.IS_CABLE_SYSTEM = 1)
		INNER JOIN dbo.MEDIA_TRAFFIC_DETAIL MTD ON MTCG.MEDIA_TRAFFIC_CREATIVE_GROUP_ID = MTD.MEDIA_TRAFFIC_CREATIVE_GROUP_ID 
		LEFT OUTER JOIN dbo.MEDIA_TRAFFIC_VENDOR_CREATIVE_GROUP MTVCG ON MTVCG.MEDIA_TRAFFIC_CREATIVE_GROUP_ID = MTCG.MEDIA_TRAFFIC_CREATIVE_GROUP_ID AND MTV.MEDIA_TRAFFIC_VENDOR_ID = MTVCG.MEDIA_TRAFFIC_VENDOR_ID 
		LEFT OUTER JOIN dbo.DAY_PART DP ON MTD.DAY_PART_ID = DP.DAY_PART_ID 
		INNER JOIN dbo.MEDIA_TRAFFIC_REVISION MTR ON MTV.MEDIA_TRAFFIC_REVISION_ID = MTR.MEDIA_TRAFFIC_REVISION_ID 		
		LEFT OUTER JOIN @DOCUMENTS D ON D.MEDIA_TRAFFIC_DETAIL_ID = MTD.MEDIA_TRAFFIC_DETAIL_ID
	WHERE MTV.MEDIA_TRAFFIC_VENDOR_ID = @MEDIA_TRAFFIC_VENDOR_ID
	AND (
			@CableVendorWithoutCreativeGroupAssigned = 0
		OR
			(@CableVendorWithoutCreativeGroupAssigned = 1 AND MTCG.IS_DEFAULT = 1)
		)
END
