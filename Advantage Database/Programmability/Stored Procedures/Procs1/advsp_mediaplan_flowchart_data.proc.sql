CREATE PROCEDURE [dbo].[advsp_mediaplan_flowchart_data]
	@MediaPlanID AS int,
	@MediaPlanDetailID AS int
AS
BEGIN 

	SET NOCOUNT ON
	
	SELECT 
		[MediaPlanDetailLevelLineDataID] = MPDLLD.MEDIA_PLAN_DTL_LEVEL_LINE_DATA_ID,
		[RowIndex] = MPDLLD.ROW_INDEX,
		[PlanID] = MP.MEDIA_PLAN_ID,
		[EstimateID] = MPD.MEDIA_PLAN_DTL_ID,
		[MediaType] = CASE WHEN MPD.SC_TYPE = 'M' THEN 'Magazine'
						   WHEN MPD.SC_TYPE = 'N' THEN 'Newspaper'
						   WHEN MPD.SC_TYPE = 'R' THEN 'Radio'
						   WHEN MPD.SC_TYPE = 'T' THEN 'Television'
						   WHEN MPD.SC_TYPE = 'O' THEN 'Out of Home'
						   WHEN MPD.SC_TYPE = 'I' THEN 'Internet' END,
		[StartDate] = MPDLLD.[START_DATE],
		[EndDate] = MPDLLD.END_DATE,
		[VendorCode] = V.VendorCode,
		[VendorName] = V.[VendorName],
		[MarketCode] = M.[MarketCode],
		[MarketName] = M.[MarketName],
		[Demo1] = MPDLLD.DEMO1,
		[Demo2] = MPDLLD.DEMO2,
		[Units] = MPDLLD.UNITS,
		[Impressions] = MPDLLD.IMPRESSIONS,
		[Clicks] = MPDLLD.CLICKS,
		[Rate] = MPDLLD.RATE,
		[Dollars] = MPDLLD.DOLLARS,
		[AgencyFee] = MPDLLD.AGENCY_FEE,
		[BillAmount] = MPDLLD.BILL_AMOUNT,
		[Columns] = MPDLLD.[COLUMNS],
		[InchesLines] = MPDLLD.INCHES_LINES,
		[NetCharge] = MPDLLD.NET_CHARGE
	FROM 
		[dbo].[MEDIA_PLAN_DTL_LEVEL_LINE_DATA] AS MPDLLD INNER JOIN
		[dbo].[MEDIA_PLAN] AS MP ON MP.MEDIA_PLAN_ID = MPDLLD.MEDIA_PLAN_ID INNER JOIN 
		[dbo].[MEDIA_PLAN_DTL] AS MPD ON MPD.MEDIA_PLAN_ID = MPDLLD.MEDIA_PLAN_ID AND
										 MPD.MEDIA_PLAN_DTL_ID = MPDLLD.MEDIA_PLAN_DTL_ID LEFT OUTER JOIN
		(SELECT 
			[MediaPlanID] = V.[MediaPlanID], 
			[MediaPlanDetailID] = V.[MediaPlanDetailID],
			[RowIndex] = V.[RowIndex],
			[VendorCode] = V.[VendorCode],
			[VendorName] = V.[VendorName]
		FROM
			(SELECT 
				[ID] = ROW_NUMBER() OVER (PARTITION BY MPDLLT.MEDIA_PLAN_ID, MPDLLT.MEDIA_PLAN_DTL_ID, MPDLL.ROW_INDEX ORDER BY MPDL.ORDER_NUMBER, MPDLL.ROW_INDEX DESC),
				[MediaPlanID] = MPDLLT.MEDIA_PLAN_ID, 
				[MediaPlanDetailID] = MPDLLT.MEDIA_PLAN_DTL_ID,
				[RowIndex] = MPDLL.ROW_INDEX,
				[VendorCode] = V.VN_CODE,
				[VendorName] = V.VN_NAME
			FROM 
				[dbo].[MEDIA_PLAN_DTL_LEVEL_LINE_TAG] AS MPDLLT INNER JOIN
				[dbo].[MEDIA_PLAN_DTL_LEVEL_LINE] AS MPDLL ON MPDLL.MEDIA_PLAN_DTL_LEVEL_LINE_ID = MPDLLT.MEDIA_PLAN_DTL_LEVEL_LINE_ID INNER JOIN
				[dbo].[MEDIA_PLAN_DTL_LEVEL] AS MPDL ON MPDL.MEDIA_PLAN_DTL_LEVEL_ID = MPDLL.MEDIA_PLAN_DTL_LEVEL_ID INNER JOIN
				[dbo].[VENDOR] AS V ON V.VN_CODE = MPDLLT.VN_CODE
			WHERE
				MPDL.TAG_TYPE = 2 AND
				MPDLLT.VN_CODE IS NOT NULL) AS V
		WHERE
			V.[ID] = 1) AS V ON V.MediaPlanID  = MPDLLD.MEDIA_PLAN_ID AND
								V.[MediaPlanDetailID] = MPDLLD.MEDIA_PLAN_DTL_ID AND
								V.[RowIndex] = MPDLLD.ROW_INDEX LEFT OUTER JOIN
		(SELECT 
			[MediaPlanID] = M.[MediaPlanID], 
			[MediaPlanDetailID] = M.[MediaPlanDetailID],
			[RowIndex] = M.[RowIndex],
			[MarketCode] = M.[MarketCode],
			[MarketName] = M.[MarketName]
		FROM
			(SELECT 
				[ID] = ROW_NUMBER() OVER (PARTITION BY MPDLLT.MEDIA_PLAN_ID, MPDLLT.MEDIA_PLAN_DTL_ID, MPDLL.ROW_INDEX ORDER BY MPDL.ORDER_NUMBER, MPDLL.ROW_INDEX DESC),
				[MediaPlanID] = MPDLLT.MEDIA_PLAN_ID, 
				[MediaPlanDetailID] = MPDLLT.MEDIA_PLAN_DTL_ID,
				[RowIndex] = MPDLL.ROW_INDEX,
				[MarketCode] = M.MARKET_CODE,
				[MarketName] = M.MARKET_DESC
			FROM 
				[dbo].[MEDIA_PLAN_DTL_LEVEL_LINE_TAG] AS MPDLLT INNER JOIN
				[dbo].[MEDIA_PLAN_DTL_LEVEL_LINE] AS MPDLL ON MPDLL.MEDIA_PLAN_DTL_LEVEL_LINE_ID = MPDLLT.MEDIA_PLAN_DTL_LEVEL_LINE_ID INNER JOIN
				[dbo].[MEDIA_PLAN_DTL_LEVEL] AS MPDL ON MPDL.MEDIA_PLAN_DTL_LEVEL_ID = MPDLL.MEDIA_PLAN_DTL_LEVEL_ID INNER JOIN
				[dbo].[MARKET] AS M ON M.MARKET_CODE = MPDLLT.MARKET_CODE
			WHERE
				MPDL.TAG_TYPE = 1 AND
				MPDLLT.MARKET_CODE IS NOT NULL) AS M
		WHERE
			M.[ID] = 1) AS M ON M.MediaPlanID  = MPDLLD.MEDIA_PLAN_ID AND
								M.[MediaPlanDetailID] = MPDLLD.MEDIA_PLAN_DTL_ID AND
								M.[RowIndex] = MPDLLD.ROW_INDEX
	WHERE 
		MPDLLD.MEDIA_PLAN_ID = @MediaPlanID AND
		MPDLLD.MEDIA_PLAN_DTL_ID = @MediaPlanDetailID
		
END

GO