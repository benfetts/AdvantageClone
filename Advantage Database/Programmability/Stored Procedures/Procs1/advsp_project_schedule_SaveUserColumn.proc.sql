CREATE PROCEDURE [dbo].[advsp_project_schedule_SaveUserColumn]
	@EMP_CODE VARCHAR(6),
	@COLUMN_ID INT,
	@SHOW_ON_GRID BIT,
	@SHOW_ON_ADDNEW BIT,
	@ORDER SMALLINT
AS
BEGIN

	DECLARE @COLUMN_TABLE TABLE(COLUMN_ID INT, DISPLAY_TYPE VARCHAR(4), COLUMN_ORDER SMALLINT, DEFAULT_ORDER SMALLINT, COLUMN_EXISTS BIT)
	DECLARE @CURRENT_ORDER SMALLINT

	INSERT INTO @COLUMN_TABLE (COLUMN_ID, DISPLAY_TYPE, COLUMN_ORDER, DEFAULT_ORDER, COLUMN_EXISTS)
		SELECT
			[COLUMN_ID] = HDR.ID,
			[DISPLAY_TYPE] = CASE	
								WHEN HDR.ID <> @COLUMN_ID THEN DTL.DISPLAY_TYPE
								WHEN @SHOW_ON_GRID = 1 AND @SHOW_ON_ADDNEW = 1 THEN 'GA'
								WHEN @SHOW_ON_GRID = 1 AND @SHOW_ON_ADDNEW = 0 THEN 'G'
								WHEN @SHOW_ON_GRID = 0 AND @SHOW_ON_ADDNEW = 1 THEN 'A'
								ELSE NULL
							 END,
			[COLUMN_ORDER] = ISNULL(DTL.COLUMN_ORDER, HDR.COLUMN_ORDER),
			[DEFAULT_ORDER] = HDR.COLUMN_ORDER,
			[EXISTS] = DTL.ID
		FROM
			dbo.JOB_TRAFFIC_SETUP_ITEMS HDR
		LEFT OUTER JOIN
			(SELECT	
				*
			 FROM
				dbo.JOB_TRAFFIC_SETUP_DTL
			 WHERE
				HDR_CODE = @EMP_CODE) DTL ON HDR.ID = DTL.COLUMN_ID

	SELECT 
		@CURRENT_ORDER = COLUMN_ORDER 
	FROM 
		@COLUMN_TABLE
	WHERE
		COLUMN_ID = @COLUMN_ID
	
	IF @ORDER < @CURRENT_ORDER -- moving up
	BEGIN

		UPDATE 
			@COLUMN_TABLE
		SET 
			COLUMN_ORDER = COLUMN_ORDER + 1
		WHERE
			COLUMN_ORDER >= @ORDER AND
			COLUMN_ORDER < @CURRENT_ORDER

	END

	IF @ORDER > @CURRENT_ORDER -- moving down
	BEGIN

		UPDATE 
			@COLUMN_TABLE
		SET 
			COLUMN_ORDER = COLUMN_ORDER - 1
		WHERE
			COLUMN_ORDER <= @ORDER AND
			COLUMN_ORDER > @CURRENT_ORDER

	END

	UPDATE 
		@COLUMN_TABLE
	SET
		COLUMN_ORDER = @ORDER
	WHERE
		COLUMN_ID = @COLUMN_ID
	
	DELETE FROM 
		dbo.JOB_TRAFFIC_SETUP_DTL
	WHERE
		HDR_CODE = @EMP_CODE AND
		COLUMN_ID IN (	SELECT
							COLUMN_ID
						FROM
							@COLUMN_TABLE
						WHERE
							COLUMN_EXISTS = 1 AND
							DISPLAY_TYPE IS NULL AND
							COLUMN_ORDER = DEFAULT_ORDER)
	
	DECLARE @COL_ID INT
	DECLARE @MAX_SEQ INT
	DECLARE NEWDTL CURSOR FOR
		SELECT 
			COLUMN_ID 
		FROM 
			@COLUMN_TABLE 
		WHERE
			ISNULL(COLUMN_EXISTS, 0) = 0 AND
			(ISNULL(DISPLAY_TYPE, '') <> '' OR
			 COLUMN_ORDER <> DEFAULT_ORDER)

	SELECT @MAX_SEQ = ISNULL(SEQ, 0) + 1 FROM dbo.JOB_TRAFFIC_SETUP_DTL WHERE HDR_CODE = @EMP_CODE

	OPEN NEWDTL
	FETCH NEXT FROM NEWDTL INTO @COL_ID

	WHILE @@FETCH_STATUS = 0
	BEGIN

		INSERT INTO dbo.JOB_TRAFFIC_SETUP_DTL (HDR_CODE, COLUMN_ID, SHOW_LONG_DESC, SEQ, DISPLAY_TYPE, COLUMN_ORDER)
			SELECT
				@EMP_CODE,
				@COL_ID,
				NULL,
				@MAX_SEQ,
				DISPLAY_TYPE,
				COLUMN_ORDER
			FROM
				@COLUMN_TABLE
			WHERE
				COLUMN_ID = @COL_ID
	
		SET @MAX_SEQ = @MAX_SEQ + 1
		FETCH NEXT FROM NEWDTL INTO @COL_ID

	END

	CLOSE NEWDTL
	DEALLOCATE NEWDTL

	UPDATE
		DTL
	SET
		COLUMN_ORDER = C.COLUMN_ORDER,
		DISPLAY_TYPE = C.DISPLAY_TYPE
	FROM
		dbo.JOB_TRAFFIC_SETUP_DTL DTL JOIN
		@COLUMN_TABLE C ON DTL.COLUMN_ID = C.COLUMN_ID
	WHERE
		C.COLUMN_EXISTS = 1 AND
		DTL.HDR_CODE = @EMP_CODE

END
	