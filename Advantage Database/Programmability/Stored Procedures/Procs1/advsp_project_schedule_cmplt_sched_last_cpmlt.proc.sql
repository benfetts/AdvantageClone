IF EXISTS (
		SELECT *
		FROM dbo.sysobjects
		WHERE id = object_id(N'[dbo].[advsp_project_schedule_cmplt_sched_last_cpmlt]')
			AND OBJECTPROPERTY(id, N'IsProcedure') = 1
		)
	DROP PROCEDURE [dbo].[advsp_project_schedule_cmplt_sched_last_cpmlt]
GO
CREATE PROCEDURE [dbo].[advsp_project_schedule_cmplt_sched_last_cpmlt] 
@JOB_NUMBER INT,
@JOB_COMPONENT_NBR SMALLINT
AS
BEGIN
/*=========== QUERY ===========*/
    IF (SELECT CAST (ISNULL(AGY_SETTINGS_VALUE, AGY_SETTINGS_DEF) AS BIT) FROM AGY_SETTINGS WHERE AGY_SETTINGS_CODE = 'TRF_CMPLT_ON_LAST') = CAST(1 AS BIT)
    BEGIN
		IF EXISTS (SELECT JOB_NUMBER FROM JOB_TRAFFIC WHERE JOB_NUMBER = @JOB_NUMBER AND JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR AND COMPLETED_DATE IS NULL)
		BEGIN

			DECLARE
				@MAX_COMPLETED_DATE SMALLDATETIME,
				@SCHEDULE_CALC INT

			SELECT @SCHEDULE_CALC = ISNULL(SCHEDULE_CALC, 0) FROM JOB_TRAFFIC WITH(NOLOCK) WHERE JOB_NUMBER = @JOB_NUMBER AND JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR

			IF @SCHEDULE_CALC IS NULL OR @SCHEDULE_CALC = 0 -- NOT PRED
			BEGIN
				IF NOT EXISTS (SELECT JOB_NUMBER FROM JOB_TRAFFIC_DET WHERE JOB_NUMBER = @JOB_NUMBER AND JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR AND JOB_COMPLETED_DATE IS NULL)
				BEGIN
					SELECT @MAX_COMPLETED_DATE = MAX(JOB_COMPLETED_DATE) 
					FROM JOB_TRAFFIC_DET WITH(NOLOCK)
					WHERE JOB_NUMBER = @JOB_NUMBER AND JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR;
				END
			END
			ELSE
			BEGIN
				IF NOT EXISTS ( SELECT
									J.JOB_NUMBER
								FROM
								JOB_TRAFFIC_DET J WITH(NOLOCK)
								LEFT OUTER JOIN JOB_TRAFFIC_DET_PREDS P WITH(NOLOCK) ON J.JOB_NUMBER = P.JOB_NUMBER AND J.JOB_COMPONENT_NBR = P.JOB_COMPONENT_NBR AND J.SEQ_NBR = P.SEQ_NBR
								WHERE
									J.JOB_NUMBER = @JOB_NUMBER
									AND J.JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR
									AND NOT PREDECESSOR_SEQ_NBR IS NULL
									AND J.JOB_COMPLETED_DATE IS NULL
								)
				BEGIN
					SELECT
						@MAX_COMPLETED_DATE = MAX(J.JOB_COMPLETED_DATE)
					FROM
					JOB_TRAFFIC_DET J WITH(NOLOCK)
					LEFT OUTER JOIN JOB_TRAFFIC_DET_PREDS P WITH(NOLOCK) ON J.JOB_NUMBER = P.JOB_NUMBER AND J.JOB_COMPONENT_NBR = P.JOB_COMPONENT_NBR AND J.SEQ_NBR = P.SEQ_NBR
					WHERE
						J.JOB_NUMBER = @JOB_NUMBER
						AND J.JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR
						AND NOT PREDECESSOR_SEQ_NBR IS NULL
				END
			END
			IF NOT @MAX_COMPLETED_DATE IS NULL
			BEGIN
				UPDATE JOB_TRAFFIC WITH(ROWLOCK) SET COMPLETED_DATE = @MAX_COMPLETED_DATE 
				WHERE JOB_NUMBER = @JOB_NUMBER AND JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR AND COMPLETED_DATE IS NULL;
			END

		END	   

    END
/*=========== QUERY ===========*/
END