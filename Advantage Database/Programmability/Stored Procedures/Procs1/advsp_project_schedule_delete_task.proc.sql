IF EXISTS
(
    SELECT *
    FROM dbo.sysobjects
    WHERE id = OBJECT_ID(N'[dbo].[advsp_project_schedule_delete_task]')
          AND OBJECTPROPERTY(id, N'IsProcedure') = 1
)
    DROP PROCEDURE [dbo].[advsp_project_schedule_delete_task];
GO
CREATE PROCEDURE [dbo].[advsp_project_schedule_delete_task] 
@JOB_NUMBER INT, 
@JOB_COMPONENT_NBR SMALLINT,
@TASK_SEQ_NBR SMALLINT
AS
/*=========== QUERY ===========*/
BEGIN
    DECLARE @PARENT_TASK_SEQ SMALLINT
	DECLARE @TIME_ENTERED BIT 

	SET @TIME_ENTERED = 0

	DECLARE @ALERTS TABLE(ALERT_ID INT);
		INSERT INTO @ALERTS(ALERT_ID)
			   SELECT ALERT_ID
			   FROM ALERT A
			   WHERE A.JOB_NUMBER = @JOB_NUMBER
					 AND A.JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR
					 AND A.TASK_SEQ_NBR = @TASK_SEQ_NBR
					 AND A.ALERT_LEVEL IN('BRD', 'PST');

	IF EXISTS (SELECT 1 FROM dbo.AGENCY WHERE TIME_REQ_ASSN = 1)
	BEGIN

		IF EXISTS (SELECT 1 FROM dbo.EMP_TIME_DTL WHERE ALERT_ID IN (SELECT ALERT_ID FROM @ALERTS))
		BEGIN
			
			SET @TIME_ENTERED = 1

		END

	END

	IF @TIME_ENTERED = 0
	BEGIN

		SELECT
			@PARENT_TASK_SEQ = PARENT_TASK_SEQ
		FROM
			dbo.JOB_TRAFFIC_DET
		WHERE
			JOB_NUMBER = @JOB_NUMBER AND
			JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR AND
			SEQ_NBR = @TASK_SEQ_NBR;

		UPDATE 
			dbo.JOB_TRAFFIC_DET
		SET
			PARENT_TASK_SEQ = @PARENT_TASK_SEQ
		WHERE 
			JOB_NUMBER = @JOB_NUMBER AND
			JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR AND
			PARENT_TASK_SEQ = @TASK_SEQ_NBR;	
		
		DECLARE @THIS_ALERT_ID AS INT;
		DECLARE ALERT_CURSOR CURSOR
		FOR SELECT ALERT_ID
			FROM @ALERTS;
		OPEN ALERT_CURSOR;
		FETCH NEXT FROM ALERT_CURSOR INTO @THIS_ALERT_ID;
		WHILE(@@FETCH_STATUS = 0)
			BEGIN
				
				UPDATE dbo.EMP_TIME_DTL SET ALERT_ID = NULL WHERE ALERT_ID = @THIS_ALERT_ID

				EXEC [dbo].[advsp_alert_system_delete_alert]
					 @THIS_ALERT_ID,
					 0;
				FETCH NEXT FROM ALERT_CURSOR INTO @THIS_ALERT_ID;
			END;
		CLOSE ALERT_CURSOR;
		DEALLOCATE ALERT_CURSOR;
		DELETE FROM [JOB_TRAFFIC_DET_PREDS]
		WHERE [JOB_NUMBER] = @JOB_NUMBER
			  AND [JOB_COMPONENT_NBR] = @JOB_COMPONENT_NBR
			  AND ([SEQ_NBR] = @TASK_SEQ_NBR OR [PREDECESSOR_SEQ_NBR] = @TASK_SEQ_NBR);
		DELETE FROM [JOB_TRAFFIC_DET_EMPS]
		WHERE [JOB_NUMBER] = @JOB_NUMBER
			  AND [JOB_COMPONENT_NBR] = @JOB_COMPONENT_NBR
			  AND [SEQ_NBR] = @TASK_SEQ_NBR;
		DELETE FROM [JOB_TRAFFIC_DET_CNTS]
		WHERE [JOB_NUMBER] = @JOB_NUMBER
			  AND [JOB_COMPONENT_NBR] = @JOB_COMPONENT_NBR
			  AND [SEQ_NBR] = @TASK_SEQ_NBR;
		DELETE FROM [JOB_TRAFFIC_DET]
		WHERE [JOB_NUMBER] = @JOB_NUMBER
			  AND [JOB_COMPONENT_NBR] = @JOB_COMPONENT_NBR
			  AND [SEQ_NBR] = @TASK_SEQ_NBR;

	END

END;