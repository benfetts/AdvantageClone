IF EXISTS ( SELECT *
            FROM dbo.sysobjects
            WHERE id = OBJECT_ID(N'[dbo].[advsp_proofing_add_upload_comment]')
                  AND 
                  OBJECTPROPERTY(id , N'IsProcedure') = 1
          ) 
    BEGIN
        DROP PROCEDURE [dbo].[advsp_proofing_add_upload_comment];
END;
GO
CREATE PROCEDURE [dbo].[advsp_proofing_add_upload_comment] 
@USER_CODE VARCHAR(100),
@ALERT_ID INT,
@DOCUMENT_ID INT = NULL
AS
/*=========== QUERY ===========*/
BEGIN

	DECLARE
		@EMP_CODE VARCHAR(6),
		@EMP_FULL_NAME VARCHAR(100),
		@ALRT_NOTIFY_HDR_ID INT,
		@ALERT_STATE_ID INT,
		@IS_PROOF BIT,
		@IS_ROUTED BIT,
		@FILENAME VARCHAR(300),
		@VERSION_COUNT INT,
		@VERSION_NUMBER INT,
		@COMMENT VARCHAR(MAX),
		@RIGHT_NOW SMALLDATETIME,
		@ASSIGNED_EMP_CODE VARCHAR(6)
	;
	SELECT @RIGHT_NOW = GETDATE();
	SELECT
		@ALRT_NOTIFY_HDR_ID = COALESCE(@ALRT_NOTIFY_HDR_ID, A.ALRT_NOTIFY_HDR_ID),
		@ALERT_STATE_ID = COALESCE(@ALERT_STATE_ID, A.ALERT_STATE_ID),
		@IS_ROUTED =	CASE
						WHEN A.ALRT_NOTIFY_HDR_ID IS NOT NULL AND A.ALERT_STATE_ID IS NOT NULL THEN 1
						ELSE 0
					END,
		@IS_PROOF =	CASE
						WHEN A.ALERT_CAT_ID = 79 THEN 1
						ELSE 0
					END
	FROM
		ALERT A WITH(NOLOCK)
	WHERE
		A.ALERT_ID = @ALERT_ID
	;
	IF @IS_PROOF = 1
	BEGIN
		IF @DOCUMENT_ID IS NULL
		BEGIN
			SELECT
				@DOCUMENT_ID = MAX(DOCUMENT_ID)
			FROM
				ALERT_ATTACHMENT AA WITH(NOLOCK)
			WHERE
				AA.ALERT_ID = @ALERT_ID
			;
		END
		SELECT
			@FILENAME = D.[FILENAME],
			@USER_CODE = D.USER_CODE,
			@VERSION_NUMBER = D.VERSION_NUMBER
		FROM
			DOCUMENTS D WITH(NOLOCK)
		WHERE
			D.DOCUMENT_ID = @DOCUMENT_ID;
		;
		SELECT 
			@EMP_CODE = A.EMP_CODE,
			@EMP_FULL_NAME = ISNULL(A.EMP_FNAME + ' ', '') + ISNULL(A.EMP_MI + ' .', '') + ISNULL(A.EMP_LNAME, '')
		FROM
			(
				SELECT TOP 1
					E.EMP_CODE,
					E.EMP_FNAME,
					E.EMP_MI,
					E.EMP_LNAME
				FROM
					SEC_USER SU WITH(NOLOCK)
					INNER JOIN EMPLOYEE_CLOAK E WITH(NOLOCK) ON SU.EMP_CODE = E.EMP_CODE
				WHERE
					SU.USER_CODE = @USER_CODE
				ORDER BY
					SU.USER_CODE DESC
			) AS A
		;
		IF @IS_ROUTED = 1
		BEGIN
			SELECT
				@ASSIGNED_EMP_CODE = R.EMP_CODE
			FROM
				ALERT_RCPT R WITH(NOLOCK)
			WHERE
				R.EMP_CODE = @EMP_CODE
				AND R.CURRENT_NOTIFY = 1
				AND R.ALRT_NOTIFY_HDR_ID = @ALRT_NOTIFY_HDR_ID
				AND R.ALERT_STATE_ID = @ALERT_STATE_ID
			;
		END
		ELSE
		BEGIN
			SELECT
				@ASSIGNED_EMP_CODE = R.EMP_CODE
			FROM
				ALERT_RCPT R WITH(NOLOCK)
			WHERE
				R.EMP_CODE = @EMP_CODE
				AND R.CURRENT_NOTIFY = 1
				AND R.ALRT_NOTIFY_HDR_ID IS NULL
				AND R.ALERT_STATE_ID IS NULL
			;
		END
			
		SELECT
			@VERSION_COUNT = COUNT(1)
		FROM
			DOCUMENTS D WITH(NOLOCK)
			INNER JOIN ALERT_ATTACHMENT AA WITH(NOLOCK) ON D.DOCUMENT_ID = AA.DOCUMENT_ID
		WHERE
			D.[FILENAME] = @FILENAME
			AND AA.ALERT_ID = @ALERT_ID
		;
		SELECT
			@VERSION_COUNT = ISNULL(@VERSION_COUNT, 1),
			@VERSION_NUMBER =	CASE
									WHEN @VERSION_COUNT IS NULL OR @VERSION_COUNT = 0 THEN @VERSION_NUMBER
									ELSE @VERSION_COUNT
								END
		;
		IF @VERSION_COUNT = 1
		BEGIN
			SELECT @COMMENT = 'NEW FILE | ' + @EMP_FULL_NAME;
			SELECT @COMMENT = @COMMENT +	'<br/>Filename:  ' + @FILENAME;
			SELECT @COMMENT = @COMMENT +	', v.' + CAST(@VERSION_COUNT AS VARCHAR);
		END
		ELSE
		BEGIN
			SELECT @COMMENT = 'NEW VERSION | ' + @EMP_FULL_NAME;
			SELECT @COMMENT = @COMMENT +	'<br/>Filename:  ' + @FILENAME;
			SELECT @COMMENT = @COMMENT +	', v.' + CAST(@VERSION_COUNT AS VARCHAR);
		END
		IF NOT EXISTS (		
			SELECT 1 
			FROM 
				ALERT_COMMENT WITH(NOLOCK)
			WHERE 
				ALERT_ID = @ALERT_ID 
				AND UPPER(RTRIM(LTRIM(CAST(COMMENT AS VARCHAR(MAX))))) = UPPER(RTRIM(LTRIM(@COMMENT)))
		)
		BEGIN
			INSERT INTO ALERT_COMMENT WITH(ROWLOCK) (ALERT_ID, ALRT_NOTIFY_HDR_ID, ALERT_STATE_ID, GENERATED_DATE, COMMENT, USER_CODE, DOCUMENT_ID, ASSIGNED_EMP_CODE, IS_SYSTEM)
			VALUES (
				@ALERT_ID,
				@ALRT_NOTIFY_HDR_ID,
				@ALERT_STATE_ID,
				@RIGHT_NOW,
				@COMMENT,
				@USER_CODE,
				@DOCUMENT_ID,
				NULL,
				1
			);
		END
	END
END
/*=========== QUERY ===========*/