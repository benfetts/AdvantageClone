IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'[dbo].[advsp_proofing_has_assignee]') AND OBJECTPROPERTY(id, N'IsProcedure') = 1)
    DROP PROCEDURE [dbo].[advsp_proofing_has_assignee]
GO
CREATE PROCEDURE [dbo].[advsp_proofing_has_assignee]
@ALERT_ID INT,
@ALRT_NOTIFY_HDR_ID INT,
@ALERT_STATE_ID INT,
@EMP_CODE VARCHAR(6)
AS
/*=========== QUERY ===========*/
BEGIN
	DECLARE
		@HAS_ASSIGNEE AS BIT,
		@IS_ROUTED AS BIT,
		@CURR_ALRT_NOTIFY_HDR_ID INT,
		@CURR_ALERT_STATE_ID INT
	;
	SELECT
		@HAS_ASSIGNEE = 0,
		@IS_ROUTED =	CASE
							WHEN A.ALRT_NOTIFY_HDR_ID IS NOT NULL AND A.ALRT_NOTIFY_HDR_ID > 0 AND A.ALERT_STATE_ID IS NOT NULL AND A.ALERT_STATE_ID > 0 THEN 1
							ELSE 0
						END,
		@CURR_ALRT_NOTIFY_HDR_ID = A.ALRT_NOTIFY_HDR_ID,
		@CURR_ALERT_STATE_ID = A.ALERT_STATE_ID
	FROM
		ALERT A WITH(NOLOCK)
	WHERE
		A.ALERT_ID = @ALERT_ID
	;
	IF @CURR_ALRT_NOTIFY_HDR_ID = @ALRT_NOTIFY_HDR_ID AND @CURR_ALERT_STATE_ID = @ALERT_STATE_ID
	BEGIN
		IF EXISTS (	SELECT 1 
					FROM ALERT_RCPT AR WITH(NOLOCK) 
					WHERE 
						AR.ALERT_ID = @ALERT_ID 
						AND AR.EMP_CODE <> @EMP_CODE
						AND
						1 = CASE 
								WHEN @IS_ROUTED = 1 AND AR.CURRENT_NOTIFY = 1 AND AR.ALRT_NOTIFY_HDR_ID = @ALRT_NOTIFY_HDR_ID AND AR.ALERT_STATE_ID = @ALERT_STATE_ID THEN 1
								WHEN @IS_ROUTED = 0 AND AR.CURRENT_NOTIFY = 1 THEN 1
								ELSE 0
							END
				  )
		BEGIN
			SELECT @HAS_ASSIGNEE = 1;
		END
		IF @HAS_ASSIGNEE IS NULL OR @HAS_ASSIGNEE = 0
		BEGIN
			IF EXISTS (	SELECT 1 
						FROM ALERT_RCPT_DISMISSED ARD WITH(NOLOCK) 
						WHERE 
							ARD.ALERT_ID = @ALERT_ID 
							AND ARD.EMP_CODE <> @EMP_CODE
							AND
							1 = CASE 
									WHEN @IS_ROUTED = 1 AND ARD.CURRENT_NOTIFY = 1 AND ARD.ALRT_NOTIFY_HDR_ID = @ALRT_NOTIFY_HDR_ID AND ARD.ALERT_STATE_ID = @ALERT_STATE_ID THEN 1
									WHEN @IS_ROUTED = 0 AND ARD.CURRENT_NOTIFY = 1 THEN 1
									ELSE 0
								END
					  )
			BEGIN
				SELECT @HAS_ASSIGNEE = 1;
			END
		END
	END
	ELSE
	BEGIN
		IF EXISTS	(
						SELECT 1
						FROM
							ALERT_NOTIFY_EMPS_ASSIGNMENT E WITH(NOLOCK)
						WHERE
							E.ALERT_ID = @ALERT_ID
							AND E.ALRT_NOTIFY_HDR_ID = @ALRT_NOTIFY_HDR_ID
							AND E.ALERT_STATE_ID = @ALERT_STATE_ID
							AND E.EMP_CODE <> @EMP_CODE
							AND (ISNULL(IS_SELECTED, 0) = 1 OR ISNULL(IS_DEFAULT, 0) = 1)
					)
		BEGIN
			SELECT @HAS_ASSIGNEE = 1;
		END
	END
	SELECT
		[HasAssignee] = ISNULL(@HAS_ASSIGNEE, 0)
	;
END
/*=========== QUERY ===========*/
