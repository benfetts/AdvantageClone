CREATE PROCEDURE [dbo].[advsp_traffic_schedule_FindAndReplace] 
	@TYPE SMALLINT,
	@OLD_VALUE VARCHAR(40), 
	@NEW_VALUE VARCHAR(40),
	@COMPONENTS VARCHAR(MAX)
AS
BEGIN

/*

@TYPE
---------------
0 = Due Time
1 = Task Status

*/

	DECLARE @JOB_COMPONENTS TABLE(JOB_NUMBER INT, JOB_COMPONENT_NBR SMALLINT)
		
	INSERT INTO @JOB_COMPONENTS
		SELECT DISTINCT
			[JOB_NUMBER] = CONVERT(INT, LEFT(items, CHARINDEX(',', items) - 1)),
			[JOB_COMPONENT_NBR] = CONVERT(SMALLINT, RIGHT(items, LEN(items) - CHARINDEX(',', items)))
		FROM
			dbo.udf_split_list(@COMPONENTS, '|')
	
	UPDATE
		JTD
	SET
		REVISED_DUE_TIME = CASE WHEN @TYPE = 0 THEN @NEW_VALUE ELSE REVISED_DUE_TIME END,
		TASK_STATUS = CASE WHEN @TYPE = 1 THEN @NEW_VALUE ELSE TASK_STATUS END
	FROM 
		dbo.JOB_TRAFFIC_DET JTD
	INNER JOIN
		@JOB_COMPONENTS JC ON JTD.JOB_NUMBER = JC.JOB_NUMBER AND
							  JTD.JOB_COMPONENT_NBR = JC.JOB_COMPONENT_NBR
	WHERE
		1 = CASE
				WHEN @TYPE = 0 AND REVISED_DUE_TIME = @OLD_VALUE THEN 1
				WHEN @TYPE = 1 AND TASK_STATUS = @OLD_VALUE THEN 1
				ELSE 0 
			END
		
	SELECT @@ROWCOUNT

END