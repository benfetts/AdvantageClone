CREATE PROCEDURE [dbo].[advsp_traffic_schedule_FindAndReplace_ClientContactAssignment] 
	@CL_CODE VARCHAR(6),
	@OLD_CONT_CODE VARCHAR(6), 
	@NEW_CONT_CODE VARCHAR(6),
	@COMPONENTS VARCHAR(MAX)
AS
BEGIN

	DECLARE @JOB_COMPONENTS TABLE(JOB_NUMBER INT, JOB_COMPONENT_NBR SMALLINT)
	DECLARE @NEW_CDP_CONTACT_ID INT

	INSERT INTO @JOB_COMPONENTS
		SELECT DISTINCT
			[JOB_NUMBER] = CONVERT(INT, LEFT(items, CHARINDEX(',', items) - 1)),
			[JOB_COMPONENT_NBR] = CONVERT(SMALLINT, RIGHT(items, LEN(items) - CHARINDEX(',', items)))
		FROM
			dbo.udf_split_list(@COMPONENTS, '|')
		
	IF ISNULL(@NEW_CONT_CODE, '') = ''
	BEGIN

		DELETE 
			JTDC
		FROM
			dbo.JOB_TRAFFIC_DET_CNTS JTDC 
		INNER JOIN
			@JOB_COMPONENTS JC ON JTDC.JOB_NUMBER = JC.JOB_NUMBER AND
								  JTDC.JOB_COMPONENT_NBR = JC.JOB_COMPONENT_NBR
		INNER JOIN
			dbo.JOB_TRAFFIC_DET JTD ON JTDC.JOB_NUMBER = JTD.JOB_NUMBER AND
									   JTDC.JOB_COMPONENT_NBR = JTD.JOB_COMPONENT_NBR AND
									   JTDC.SEQ_NBR = JTD.SEQ_NBR
		INNER JOIN
			dbo.CDP_CONTACT_HDR CON ON JTDC.CDP_CONTACT_ID = CON.CDP_CONTACT_ID 
		WHERE
			JTDC.CDP_CONTACT_ID = CON.CDP_CONTACT_ID AND
			CON.CL_CODE = @CL_CODE AND
			CON.CONT_CODE = @OLD_CONT_CODE 

	END
	ELSE
	BEGIN
	
		SELECT
			@NEW_CDP_CONTACT_ID = CDP_CONTACT_ID
		FROM
			dbo.CDP_CONTACT_HDR 
		WHERE
			CL_CODE = @CL_CODE AND
			CONT_CODE = @NEW_CONT_CODE

		UPDATE 
			JTDC
		SET
			JTDC.CDP_CONTACT_ID = @NEW_CDP_CONTACT_ID
		FROM
			dbo.JOB_TRAFFIC_DET_CNTS JTDC
		INNER JOIN
			@JOB_COMPONENTS JC ON JTDC.JOB_NUMBER = JC.JOB_NUMBER AND
								  JTDC.JOB_COMPONENT_NBR = JC.JOB_COMPONENT_NBR
		INNER JOIN
			dbo.JOB_TRAFFIC_DET JTD ON JTDC.JOB_NUMBER = JTD.JOB_NUMBER AND
									   JTDC.JOB_COMPONENT_NBR = JTD.JOB_COMPONENT_NBR AND
									   JTDC.SEQ_NBR = JTD.SEQ_NBR
		LEFT OUTER JOIN
			dbo.JOB_TRAFFIC_DET_CNTS ASSIGNED ON JTDC.JOB_NUMBER = ASSIGNED.JOB_NUMBER AND
												 JTDC.JOB_COMPONENT_NBR = ASSIGNED.JOB_COMPONENT_NBR AND
												 JTDC.SEQ_NBR = ASSIGNED.SEQ_NBR AND
												 ASSIGNED.CDP_CONTACT_ID = @NEW_CDP_CONTACT_ID
		INNER JOIN
			dbo.CDP_CONTACT_HDR CON ON JTDC.CDP_CONTACT_ID = CON.CDP_CONTACT_ID
		WHERE
			JTDC.CDP_CONTACT_ID = CON.CDP_CONTACT_ID AND
			ASSIGNED.CDP_CONTACT_ID IS NULL AND
			CON.CL_CODE = @CL_CODE AND
			CON.CONT_CODE = @OLD_CONT_CODE

	END

	SELECT @@ROWCOUNT

END