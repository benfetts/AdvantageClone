CREATE PROCEDURE [dbo].[advsp_traffic_schedule_FindAndReplace_EmployeeAssignment] 
	@OLD_EMP_CODE VARCHAR(6), 
	@NEW_EMP_CODE VARCHAR(6),
	@FNC_CODE VARCHAR(10),
	@COMPONENTS VARCHAR(MAX)
AS
BEGIN

	DECLARE @JOB_COMPONENTS TABLE(JOB_NUMBER INT, JOB_COMPONENT_NBR SMALLINT)
	
	INSERT INTO @JOB_COMPONENTS
		SELECT DISTINCT
			[JOB_NUMBER] = CONVERT(INT, LEFT(items, CHARINDEX(',', items) - 1)),
			[JOB_COMPONENT_NBR] = CONVERT(SMALLINT, RIGHT(items, LEN(items) - CHARINDEX(',', items)))
		FROM
			dbo.udf_split_list(@COMPONENTS, '|')
	
	IF ISNULL(@OLD_EMP_CODE, '') = '' AND ISNULL(@NEW_EMP_CODE, '') <> ''
	BEGIN
	
		INSERT INTO 
			dbo.JOB_TRAFFIC_DET_EMPS (JOB_NUMBER, JOB_COMPONENT_NBR, SEQ_NBR, EMP_CODE, HOURS_ALLOWED)
		SELECT
			JC.JOB_NUMBER,
			JC.JOB_COMPONENT_NBR,
			JTD.SEQ_NBR,
			@NEW_EMP_CODE, 
			JTD.JOB_HRS
		FROM
			@JOB_COMPONENTS JC
		INNER JOIN
			dbo.JOB_TRAFFIC_DET JTD ON JC.JOB_NUMBER = JTD.JOB_NUMBER AND
										JC.JOB_COMPONENT_NBR = JTD.JOB_COMPONENT_NBR
		LEFT OUTER JOIN
			dbo.JOB_TRAFFIC_DET_EMPS JTDE ON JTD.JOB_NUMBER = JTDE.JOB_NUMBER AND
											 JTD.JOB_COMPONENT_NBR = JTDE.JOB_COMPONENT_NBR AND
											 JTD.SEQ_NBR = JTDE.SEQ_NBR AND
											 JTDE.EMP_CODE = @NEW_EMP_CODE
		WHERE JTD.JOB_COMPLETED_DATE IS NULL AND JTDE.TEMP_COMP_DATE IS NULL AND
			JTDE.JOB_NUMBER IS NULL AND
			1 = CASE WHEN ISNULL(@FNC_CODE, '') = '' THEN 1 WHEN @FNC_CODE = JTD.FNC_CODE THEN 1 ELSE 0 END
		

	END
	ELSE IF ISNULL(@NEW_EMP_CODE, '') = ''
	BEGIN

		DELETE 
			JTDE
		FROM
			dbo.JOB_TRAFFIC_DET_EMPS JTDE 
		INNER JOIN
			@JOB_COMPONENTS JC ON JTDE.JOB_NUMBER = JC.JOB_NUMBER AND
								  JTDE.JOB_COMPONENT_NBR = JC.JOB_COMPONENT_NBR
		INNER JOIN
			dbo.JOB_TRAFFIC_DET JTD ON JTDE.JOB_NUMBER = JTD.JOB_NUMBER AND
									   JTDE.JOB_COMPONENT_NBR = JTD.JOB_COMPONENT_NBR AND
									   JTDE.SEQ_NBR = JTD.SEQ_NBR
		WHERE JTD.JOB_COMPLETED_DATE IS NULL AND JTDE.TEMP_COMP_DATE IS NULL AND
			JTDE.EMP_CODE = @OLD_EMP_CODE AND
			1 = CASE WHEN ISNULL(@FNC_CODE, '') = '' THEN 1 WHEN @FNC_CODE = JTD.FNC_CODE THEN 1 ELSE 0 END

	END
	ELSE
	BEGIN

		UPDATE 
			JTDE
		SET
			JTDE.EMP_CODE = @NEW_EMP_CODE
		FROM
			dbo.JOB_TRAFFIC_DET_EMPS JTDE
		INNER JOIN
			@JOB_COMPONENTS JC ON JTDE.JOB_NUMBER = JC.JOB_NUMBER AND
								  JTDE.JOB_COMPONENT_NBR = JC.JOB_COMPONENT_NBR
		INNER JOIN
			dbo.JOB_TRAFFIC_DET JTD ON JTDE.JOB_NUMBER = JTD.JOB_NUMBER AND
									   JTDE.JOB_COMPONENT_NBR = JTD.JOB_COMPONENT_NBR AND
									   JTDE.SEQ_NBR = JTD.SEQ_NBR
		LEFT OUTER JOIN
			dbo.JOB_TRAFFIC_DET_EMPS ASSIGNED ON JTDE.JOB_NUMBER = ASSIGNED.JOB_NUMBER AND
												 JTDE.JOB_COMPONENT_NBR = ASSIGNED.JOB_COMPONENT_NBR AND
												 JTDE.SEQ_NBR = ASSIGNED.SEQ_NBR AND
												 ASSIGNED.EMP_CODE = @NEW_EMP_CODE
		WHERE JTD.JOB_COMPLETED_DATE IS NULL AND JTDE.TEMP_COMP_DATE IS NULL AND 
			JTDE.EMP_CODE = @OLD_EMP_CODE AND
			1 = CASE WHEN ISNULL(@FNC_CODE, '') = '' THEN 1 WHEN @FNC_CODE = JTD.FNC_CODE THEN 1 ELSE 0 END AND
			ASSIGNED.EMP_CODE IS NULL

	END

	SELECT @@ROWCOUNT

END