CREATE PROCEDURE [dbo].[advsp_traffic_schedule_GetTaskDocuments]
		@JobNumber INT,
		@JobComponentNumber SMALLINT,
		@SequenceNumber SMALLINT,
		@Filter VARCHAR(30),
		@History smallint,
		@Private Int,
		@UserCode VARCHAR(100)
AS
BEGIN

	DECLARE @LIMITED BIT

	SET @LIMITED = 0

	SELECT 
		@LIMITED = CASE WHEN COUNT(*) > 0 THEN 1 ELSE 0 END
	FROM 
		dbo.SEC_CLIENT 
	WHERE 
		SEC_CLIENT.[USER_ID] = @UserCode

	SELECT
		[LEVEL] = 'Project Schedule Task',
		[JOB_NUMBER] = JOB_TRAFFIC_DET_DOCS.JOB_NUMBER,
		[JOB_COMPONENT_NBR] = JOB_TRAFFIC_DET_DOCS.JOB_COMPONENT_NBR,
		[SEQ_NBR] = JOB_TRAFFIC_DET_DOCS.SEQ_NBR,
		[FNC_CODE] = JOB_TRAFFIC_DET.FNC_CODE,
		[FNC_DESCRIPTION] = JOB_TRAFFIC_DET.TASK_DESCRIPTION,
		[DOCUMENT_ID] = DOCUMENTS.DOCUMENT_ID, 
		[FILENAME] = DOCUMENTS.[FILENAME],
		[MIME_TYPE] = DOCUMENTS.MIME_TYPE, 
		[DESCRIPTION] = DOCUMENTS.[DESCRIPTION], 
		[KEYWORDS] = DOCUMENTS.KEYWORDS, 
		[UPLOADED_DATE] = DOCUMENTS.UPLOADED_DATE, 
		[USER_CODE] = DOCUMENTS.USER_CODE, 
		[FILE_SIZE] = DOCUMENTS.FILE_SIZE, 
		[USER_NAME] = SEC_USER.[USER_NAME], 
		[REPOSITORY_FILENAME] = DOCUMENTS.REPOSITORY_FILENAME, 
		[DOCUMENT_TYPE_DESC] = DOCUMENT_TYPE.DOCUMENT_TYPE_DESC, 
		[PRIVATE_FLAG] = ISNULL(DOCUMENTS.PRIVATE_FLAG, 0), 
		[PROOFHQ_URL] = DOCUMENTS.PROOFHQ_URL
	FROM
		dbo.JOB_TRAFFIC_DET_DOCS INNER JOIN
		dbo.JOB_TRAFFIC_DET ON JOB_TRAFFIC_DET_DOCS.JOB_NUMBER = JOB_TRAFFIC_DET.JOB_NUMBER AND
							   JOB_TRAFFIC_DET_DOCS.JOB_COMPONENT_NBR = JOB_TRAFFIC_DET.JOB_COMPONENT_NBR AND
							   JOB_TRAFFIC_DET_DOCS.SEQ_NBR = JOB_TRAFFIC_DET.SEQ_NBR INNER JOIN
		dbo.JOB_LOG ON JOB_TRAFFIC_DET_DOCS.JOB_NUMBER = JOB_LOG.JOB_NUMBER INNER JOIN
		dbo.DOCUMENTS ON JOB_TRAFFIC_DET_DOCS.DOCUMENT_ID = DOCUMENTS.DOCUMENT_ID INNER JOIN
		dbo.DOCUMENT_TYPE ON DOCUMENTS.DOCUMENT_TYPE_ID = DOCUMENT_TYPE.DOCUMENT_TYPE_ID LEFT OUTER JOIN
		(SELECT	
			[MOST_RECENT_DOC_ID] = MAX(DOCUMENTS.DOCUMENT_ID),
			[FILENAME] = DOCUMENTS.[FILENAME]
		 FROM
			dbo.JOB_TRAFFIC_DET_DOCS INNER JOIN
			dbo.DOCUMENTS ON JOB_TRAFFIC_DET_DOCS.DOCUMENT_ID = DOCUMENTS.DOCUMENT_ID
		 WHERE
			JOB_TRAFFIC_DET_DOCS.JOB_NUMBER = @JobNumber AND
			JOB_TRAFFIC_DET_DOCS.JOB_COMPONENT_NBR = @JobComponentNumber AND
			JOB_TRAFFIC_DET_DOCS.SEQ_NBR = @SequenceNumber
		 GROUP BY
			DOCUMENTS.[FILENAME]) CURRENT_DOCS ON DOCUMENTS.DOCUMENT_ID = CURRENT_DOCS.MOST_RECENT_DOC_ID LEFT OUTER JOIN
		dbo.SEC_USER ON UPPER(DOCUMENTS.USER_CODE) = UPPER(SEC_USER.USER_CODE) LEFT OUTER JOIN
		(SELECT
			CL_CODE,
			DIV_CODE,
			PRD_CODE
		 FROM
			dbo.SEC_CLIENT
		 WHERE
			SEC_CLIENT.[USER_ID] = @UserCode) SEC_CLIENT ON JOB_LOG.CL_CODE = SEC_CLIENT.CL_CODE AND
															JOB_LOG.DIV_CODE = SEC_CLIENT.DIV_CODE AND
															JOB_LOG.PRD_CODE = SEC_CLIENT.PRD_CODE
	WHERE
		JOB_TRAFFIC_DET_DOCS.JOB_NUMBER = @JobNumber AND
		JOB_TRAFFIC_DET_DOCS.JOB_COMPONENT_NBR = @JobComponentNumber AND
		JOB_TRAFFIC_DET_DOCS.SEQ_NBR = @SequenceNumber AND
		1 = CASE WHEN @History = 0 THEN 1 WHEN CURRENT_DOCS.MOST_RECENT_DOC_ID IS NOT NULL THEN 1 ELSE 0 END AND
		1 = CASE WHEN @Private = 1 THEN 1 WHEN ISNULL(DOCUMENTS.PRIVATE_FLAG, 0) = 0 THEN 1 ELSE 0 END AND
		1 = CASE 
				WHEN ISNULL(@Filter, '') = '' THEN 1
				WHEN LOWER(DOCUMENTS.KEYWORDS) LIKE '%' + LOWER(@Filter) + '%' THEN 1
				WHEN LOWER(DOCUMENTS.[FILENAME]) LIKE '%' + LOWER(@Filter) + '%' THEN 1
				WHEN LOWER(DOCUMENTS.[DESCRIPTION]) LIKE '%' + LOWER(@Filter) + '%' THEN 1
				ELSE 0
			END AND
		1 = CASE 
				WHEN @LIMITED = 0 THEN 1 
				WHEN SEC_CLIENT.CL_CODE IS NOT NULL THEN 1 
				ELSE 0 
			END
			
END
