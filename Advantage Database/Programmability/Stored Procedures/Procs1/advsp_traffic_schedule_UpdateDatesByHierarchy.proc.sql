CREATE PROCEDURE [dbo].[advsp_traffic_schedule_UpdateDatesByHierarchy]
	@JOB_NUMBER INT,
	@JOB_COMPONENT_NBR INT
AS 
BEGIN

	DECLARE @LEVEL VARCHAR(50),
		@SEQ_NBR SMALLINT

	DECLARE @HierarchyDates TABLE ( [LEVEL] VARCHAR(50), 
									SEQ_NBR SMALLINT, 
									JOB_DAYS SMALLINT,
									HAS_CHILDREN BIT)
				
	UPDATE
		JTD
	SET
		TASK_START_DATE = HD.TASK_START_DATE,
		JOB_REVISED_DATE = HD.JOB_REVISED_DATE,
		JOB_DUE_DATE = HD.JOB_DUE_DATE,
		JOB_COMPLETED_DATE = HD.JOB_COMPLETED_DATE
	FROM 
		dbo.JOB_TRAFFIC_DET JTD JOIN
		dbo.advtf_traffic_schedule_GetHierarchyDates(@JOB_NUMBER, @JOB_COMPONENT_NBR) HD ON JTD.JOB_NUMBER = HD.JOB_NUMBER AND
																							JTD.JOB_COMPONENT_NBR = HD.JOB_COMPONENT_NBR AND
																							JTD.SEQ_NBR = HD.SEQ_NBR
	WHERE
		HD.HAS_CHILDREN = 1
	
	INSERT INTO @HierarchyDates
		SELECT
			[LEVEL], 
			SEQ_NBR, 
			JOB_DAYS,
			HAS_CHILDREN
		FROM 
			dbo.advtf_traffic_schedule_GetHierarchyDates(@JOB_NUMBER, @JOB_COMPONENT_NBR)

	UPDATE
		JTD
	SET
		JOB_DAYS = (SELECT SUM(ISNULL(HD2.JOB_DAYS, 0)) FROM @HierarchyDates HD2 WHERE HD2.HAS_CHILDREN = 0 AND HD2.[LEVEL] LIKE HD1.[LEVEL] + '.%')
	FROM
		dbo.JOB_TRAFFIC_DET JTD
	INNER JOIN
		@HierarchyDates HD1 ON JTD.SEQ_NBR = HD1.SEQ_NBR
	WHERE
		HD1.HAS_CHILDREN = 1 AND
		JTD.JOB_NUMBER = @JOB_NUMBER AND
		JTD.JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR

END