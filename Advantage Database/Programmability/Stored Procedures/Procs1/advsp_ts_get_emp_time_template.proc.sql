CREATE PROCEDURE [dbo].[advsp_ts_get_emp_time_template] 
	@EMP_CODE    VARCHAR(6),
	@USER_CODE   VARCHAR(100)
AS
BEGIN

	DECLARE @IS_LIMITED INT
	DECLARE @SEC_USER_ID INT

	SELECT @SEC_USER_ID = SEC_USER_ID
	FROM [dbo].[SEC_USER]
	WHERE UPPER(USER_CODE) = UPPER(@USER_CODE)

	SET @IS_LIMITED = 0
	
	IF EXISTS(SELECT * FROM [dbo].[SEC_USER_SETTING] WHERE SEC_USER_ID = @SEC_USER_ID AND SETTING_CODE = 'EMP_TS_FNC' AND STRING_VALUE = 'Y') BEGIN

		SET @IS_LIMITED = 1

	END

	SELECT [ID] = EMP_TIME_TMPLT.EMP_TIME_TMPLT_ID,
		   [EmployeeCode] = EMP_TIME_TMPLT.EMP_CODE,
		   [ClientCode] = JOB_LOG.CL_CODE,
		   [ClientName] = CLIENT.CL_NAME,
		   [DivisionCode] = JOB_LOG.DIV_CODE,
		   [DivisionName] = DIVISION.DIV_NAME,
		   [ProductCode] = JOB_LOG.PRD_CODE,
		   [ProductDescription] = PRODUCT.PRD_DESCRIPTION,
		   [JobNumber] = EMP_TIME_TMPLT.JOB_NUMBER,
		   [JobDescription] = JOB_LOG.JOB_DESC,
		   [JobCompNumber] = EMP_TIME_TMPLT.JOB_COMPONENT_NBR,
		   [JobCompDescription] = JOB_COMPONENT.JOB_COMP_DESC,
		   [FunctionCode] = EMP_TIME_TMPLT.FNC_CODE,
		   [FunctionDescription] = FUNCTIONS.FNC_DESCRIPTION,
		   [Comment] = CAST(EMP_TIME_TMPLT.DFLT_COMMENT AS VARCHAR(MAX)),
		   [DepartmentTeamCode] = EMP_TIME_TMPLT.DP_TM_CODE,
		   [DepartmentTeamDescription] = DEPT_TEAM.DP_TM_DESC,
		   [ProductCategoryCode] = EMP_TIME_TMPLT.PROD_CAT_CODE,
		   [ProductCategoryDescription] = PRODUCT_CATEGORY.PROD_CAT_DESC,
		   [EmployeeHours] = EMP_TIME_TMPLT.EMP_HOURS,
		   [ApplyToDays] = EMP_TIME_TMPLT.APPLY_TO_DAYS,
		   [IsClosed] = CASE 
							WHEN JOB_COMPONENT.JOB_PROCESS_CONTRL IN (2, 3, 5, 6, 9, 10, 12) THEN 
								 1
							ELSE 0
						 END
		FROM dbo.EMP_TIME_TMPLT
		INNER JOIN dbo.EMPLOYEE ON EMP_TIME_TMPLT.EMP_CODE = EMPLOYEE.EMP_CODE
		INNER JOIN dbo.JOB_LOG ON EMP_TIME_TMPLT.JOB_NUMBER = JOB_LOG.JOB_NUMBER
		INNER JOIN dbo.JOB_COMPONENT ON EMP_TIME_TMPLT.JOB_NUMBER = JOB_COMPONENT.JOB_NUMBER AND
										EMP_TIME_TMPLT.JOB_COMPONENT_NBR = JOB_COMPONENT.JOB_COMPONENT_NBR
		INNER JOIN dbo.CLIENT ON JOB_LOG.CL_CODE = CLIENT.CL_CODE
		INNER JOIN dbo.DIVISION ON JOB_LOG.CL_CODE = DIVISION.CL_CODE AND
								   JOB_LOG.DIV_CODE = DIVISION.DIV_CODE 
		INNER JOIN dbo.PRODUCT ON JOB_LOG.CL_CODE = PRODUCT.CL_CODE AND
								  JOB_LOG.DIV_CODE = PRODUCT.DIV_CODE AND
								  JOB_LOG.PRD_CODE = PRODUCT.PRD_CODE
		INNER JOIN dbo.FUNCTIONS ON EMP_TIME_TMPLT.FNC_CODE = FUNCTIONS.FNC_CODE
		INNER JOIN dbo.DEPT_TEAM ON EMP_TIME_TMPLT.DP_TM_CODE = DEPT_TEAM.DP_TM_CODE
		LEFT OUTER JOIN dbo.PRODUCT_CATEGORY ON JOB_LOG.CL_CODE = PRODUCT_CATEGORY.CL_CODE AND
												JOB_LOG.DIV_CODE = PRODUCT_CATEGORY.DIV_CODE AND
												JOB_LOG.PRD_CODE = PRODUCT_CATEGORY.PRD_CODE AND
												EMP_TIME_TMPLT.PROD_CAT_CODE = PRODUCT_CATEGORY.PROD_CAT_CODE
		LEFT OUTER JOIN dbo.EMP_TS_FNC ON FUNCTIONS.FNC_CODE = EMP_TS_FNC.FNC_CODE AND
										  EMP_TS_FNC.EMP_CODE = @EMP_CODE
		WHERE EMP_TIME_TMPLT.EMP_CODE = @EMP_CODE AND 
			  EMP_TIME_TMPLT.JOB_NUMBER > 0 AND 
			  ( FUNCTIONS.FNC_INACTIVE = 0 OR 
				FUNCTIONS.FNC_INACTIVE IS NULL ) AND
			  1 = CASE WHEN @IS_LIMITED = 0 THEN 1 WHEN EMP_TS_FNC.FNC_CODE IS NOT NULL AND EMP_TS_FNC.FNC_CODE <> '' THEN 1 ELSE 0 END
		UNION
		SELECT [ID] = EMP_TIME_TMPLT.EMP_TIME_TMPLT_ID,
			   [EmployeeCode] = EMP_TIME_TMPLT.EMP_CODE,
			   [ClientCode] = NULL,
			   [ClientName] = NULL,
			   [DivisionCode] = NULL,
			   [DivisionName] = NULL,
			   [ProductCode] = NULL,
			   [ProductDescription] = NULL,
			   [JobNumber] = NULL,
			   [JobDescription] = NULL,
			   [JobCompNumber] = NULL,
			   [JobCompDescription] = NULL,
			   [FunctionCode] = EMP_TIME_TMPLT.FNC_CODE,
			   [FunctionDescription] = TIME_CATEGORY.[DESCRIPTION],
			   [Comment] = CAST(EMP_TIME_TMPLT.DFLT_COMMENT AS VARCHAR(MAX)),
			   [DepartmentTeamCode] = EMP_TIME_TMPLT.DP_TM_CODE,
			   [DepartmentTeamDescription] = DEPT_TEAM.DP_TM_DESC,
			   [ProductCategoryCode] = NULL,
			   [ProductCategoryDescription] = NULL,
			   [EmployeeHours] = EMP_TIME_TMPLT.EMP_HOURS,
			   [ApplyToDays] = EMP_TIME_TMPLT.APPLY_TO_DAYS,
			   [IsClosed] = 0
		FROM dbo.EMP_TIME_TMPLT
		INNER JOIN dbo.EMPLOYEE ON EMP_TIME_TMPLT.EMP_CODE = EMPLOYEE.EMP_CODE
		INNER JOIN dbo.TIME_CATEGORY ON EMP_TIME_TMPLT.FNC_CODE = TIME_CATEGORY.CATEGORY
		INNER JOIN dbo.DEPT_TEAM ON EMP_TIME_TMPLT.DP_TM_CODE = DEPT_TEAM.DP_TM_CODE
		WHERE EMP_TIME_TMPLT.EMP_CODE = @EMP_CODE AND 
			 ( EMP_TIME_TMPLT.JOB_NUMBER = 0 OR 
			   EMP_TIME_TMPLT.JOB_NUMBER IS NULL ) AND 
			 ( TIME_CATEGORY.INACTIVE_FLAG = 0 OR 
			   TIME_CATEGORY.INACTIVE_FLAG IS NULL )


	ORDER BY JOB_LOG.CL_CODE,JOB_LOG.DIV_CODE,JOB_LOG.PRD_CODE, EMP_TIME_TMPLT.JOB_NUMBER DESC, EMP_TIME_TMPLT.JOB_COMPONENT_NBR
END
GO
