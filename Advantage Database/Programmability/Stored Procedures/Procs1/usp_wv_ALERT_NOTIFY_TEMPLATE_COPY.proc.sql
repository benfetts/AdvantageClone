/****** Object:  StoredProcedure [dbo].[usp_wv_ALERT_NOTIFY_TEMPLATE_COPY]    Script Date: 9/24/2019 10:48:34 AM ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

--DROP PROCEDURE [dbo].[usp_wv_ALERT_NOTIFY_TEMPLATE_COPY]
CREATE PROCEDURE [dbo].[usp_wv_ALERT_NOTIFY_TEMPLATE_COPY] /*WITH ENCRYPTION*/
@ALRT_NOTIFY_HDR_ID_TO_COPY AS INT,
@NEW_ALERT_NOTIFY_NAME AS VARCHAR(100),
@COPY_TEAM AS SMALLINT
AS
/*=========== QUERY ===========*/
DECLARE
	@NEW_ALRT_NOTIFY_HDR_ID AS INT,
	@ALERT_NOTIFY_NAME_COUNT AS INT,
	@AUTO_NXT_STATE as bit = 0
	
SELECT @ALERT_NOTIFY_NAME_COUNT = COUNT(1) FROM ALERT_NOTIFY_HDR WHERE ALERT_NOTIFY_NAME = @NEW_ALERT_NOTIFY_NAME;
SET @ALERT_NOTIFY_NAME_COUNT = ISNULL(@ALERT_NOTIFY_NAME_COUNT,0);

IF @ALERT_NOTIFY_NAME_COUNT = 0
BEGIN
	select @AUTO_NXT_STATE = AUTO_NXT_STATE from ALERT_NOTIFY_HDR WHERE ALRT_NOTIFY_HDR_ID = @ALRT_NOTIFY_HDR_ID_TO_COPY

	INSERT INTO ALERT_NOTIFY_HDR WITH(ROWLOCK)
	(
		ALERT_NOTIFY_NAME,
		ACTIVE_FLAG,
		AUTO_NXT_STATE
	)
	VALUES
	(
		@NEW_ALERT_NOTIFY_NAME,
		NULL,
		@AUTO_NXT_STATE
	);

	SELECT @NEW_ALRT_NOTIFY_HDR_ID = SCOPE_IDENTITY();
	SET @NEW_ALRT_NOTIFY_HDR_ID = ISNULL(@NEW_ALRT_NOTIFY_HDR_ID,0);

	IF @NEW_ALRT_NOTIFY_HDR_ID > 0
	BEGIN
		--INSERT STATES
		INSERT INTO ALERT_NOTIFY_STATES WITH(ROWLOCK)
		(
			ALRT_NOTIFY_HDR_ID,
			ALERT_STATE_ID,
			SORT_ORDER,
			DFLT_EMP_CODE,
			IS_DFLT,
			IS_COMPLETED,
			EMP_LOOKUP_TYPE,
			DFLT_ROLE_CODE,
			DFLT_DEPT_TEAM_CODE,
			DFLT_ALERT_GROUP_CODE
		)
		SELECT
			@NEW_ALRT_NOTIFY_HDR_ID,
			ALERT_STATE_ID,
			SORT_ORDER,
			DFLT_EMP_CODE,
			IS_DFLT,
			IS_COMPLETED,
			EMP_LOOKUP_TYPE,
			DFLT_ROLE_CODE,
			DFLT_DEPT_TEAM_CODE,
			DFLT_ALERT_GROUP_CODE
		FROM 
			ALERT_NOTIFY_STATES WITH(NOLOCK)
		WHERE 
			ALRT_NOTIFY_HDR_ID = @ALRT_NOTIFY_HDR_ID_TO_COPY;
		--INSERT EMPS
		IF @COPY_TEAM = 1
		BEGIN
			--	MANUALLY SELECTED EMPLOYEES
			BEGIN
				INSERT INTO ALERT_NOTIFY_EMPS WITH(ROWLOCK)
				(
					ALERT_STATE_ID,
					ALRT_NOTIFY_HDR_ID,
					EMP_CODE
				)
				SELECT
					ANE.ALERT_STATE_ID,
					@NEW_ALRT_NOTIFY_HDR_ID,
					EMP_CODE
				FROM 
					ALERT_NOTIFY_EMPS ANE WITH(NOLOCK)	INNER JOIN ALERT_NOTIFY_STATES ANS WITH(NOLOCK) ON ANE.ALRT_NOTIFY_HDR_ID = ANS.ALRT_NOTIFY_HDR_ID AND ANS.ALERT_STATE_ID = ANE.ALERT_STATE_ID
				WHERE 
					ANE.ALRT_NOTIFY_HDR_ID = @ALRT_NOTIFY_HDR_ID_TO_COPY AND (ANS.EMP_LOOKUP_TYPE = 0 OR ANS.EMP_LOOKUP_TYPE IS NULL);
			END
			--	BY ROLE
			BEGIN
				INSERT INTO ALERT_NOTIFY_ROLE WITH(ROWLOCK)
				(
					ALRT_NOTIFY_HDR_ID,
					ALERT_STATE_ID,
					ROLE_CODE
				)
				SELECT 
					@NEW_ALRT_NOTIFY_HDR_ID,
					SRC.ALERT_STATE_ID,
					SRC.ROLE_CODE
				FROM
					ALERT_NOTIFY_ROLE SRC WITH(NOLOCK) INNER JOIN ALERT_NOTIFY_STATES ANS WITH(NOLOCK) ON SRC.ALRT_NOTIFY_HDR_ID = ANS.ALRT_NOTIFY_HDR_ID AND ANS.ALERT_STATE_ID = SRC.ALERT_STATE_ID
				WHERE 
					SRC.ALRT_NOTIFY_HDR_ID = @ALRT_NOTIFY_HDR_ID_TO_COPY AND (ANS.EMP_LOOKUP_TYPE = 1);
			END
			--	BY DEPARTMENT
			BEGIN
				INSERT INTO ALERT_NOTIFY_DEPT_TEAM WITH(ROWLOCK)
				(
					ALRT_NOTIFY_HDR_ID,
					ALERT_STATE_ID,
					DEPT_TEAM_CODE
				)
				SELECT 
					@NEW_ALRT_NOTIFY_HDR_ID,
					SRC.ALERT_STATE_ID,
					SRC.DEPT_TEAM_CODE
				FROM
					ALERT_NOTIFY_DEPT_TEAM SRC WITH(NOLOCK) INNER JOIN ALERT_NOTIFY_STATES ANS WITH(NOLOCK) ON SRC.ALRT_NOTIFY_HDR_ID = ANS.ALRT_NOTIFY_HDR_ID AND ANS.ALERT_STATE_ID = SRC.ALERT_STATE_ID
				WHERE 
					SRC.ALRT_NOTIFY_HDR_ID = @ALRT_NOTIFY_HDR_ID_TO_COPY AND (ANS.EMP_LOOKUP_TYPE = 2);
			END
			--	BY ALERT GROUP
			BEGIN
				INSERT INTO ALERT_NOTIFY_EMAIL_GROUP WITH(ROWLOCK)
				(
					ALRT_NOTIFY_HDR_ID,
					ALERT_STATE_ID,
					EMAIL_GR_CODE
				)
				SELECT 
					@NEW_ALRT_NOTIFY_HDR_ID,
					SRC.ALERT_STATE_ID,
					SRC.EMAIL_GR_CODE
				FROM
					ALERT_NOTIFY_EMAIL_GROUP SRC WITH(NOLOCK) INNER JOIN ALERT_NOTIFY_STATES ANS WITH(NOLOCK) ON SRC.ALRT_NOTIFY_HDR_ID = ANS.ALRT_NOTIFY_HDR_ID AND ANS.ALERT_STATE_ID = SRC.ALERT_STATE_ID
				WHERE 
					SRC.ALRT_NOTIFY_HDR_ID = @ALRT_NOTIFY_HDR_ID_TO_COPY AND (ANS.EMP_LOOKUP_TYPE = 3);
			END
		END
	END		
END
ELSE
BEGIN
	SET @NEW_ALRT_NOTIFY_HDR_ID = -2;
END
SELECT @NEW_ALRT_NOTIFY_HDR_ID;
/*=========== QUERY ===========*/
GO


