--DROP PROCEDURE [dbo].[usp_wv_BA_BILL_APPR_AUTOSAVE]
CREATE PROCEDURE [dbo].[usp_wv_BA_BILL_APPR_AUTOSAVE] /*WITH ENCRYPTION*/
	@BA_ID              INT,
	@BA_BATCH_ID        INT,
	@JOB_NUMBER         INT,
	@JOB_COMPONENT_NBR  SMALLINT,
	@USER_CODE          VARCHAR(100)
AS
/*=========== QUERY ===========*/
	DECLARE @AUTO_SAVE          TINYINT,
	 @RTN_VALUE SMALLINT,
	 @CURR_APPR_STATUS SMALLINT
	 	
	SET @AUTO_SAVE = 0;
	
	SELECT @AUTO_SAVE = ISNULL(COUNT(1), 0)
	FROM   BILL_APPR_HDR WITH(NOLOCK)
	WHERE  BA_ID = @BA_ID
		   AND JOB_NUMBER = @JOB_NUMBER
		   AND JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR;
/* */
	IF @AUTO_SAVE = 0
	BEGIN
		--SAVE A NEW HEADER REC:
		DECLARE @AE_EMP_CODE            VARCHAR(6),
				@DEFAULT_AB_FLAG        SMALLINT,
				@DEFAULT_JOB_BILL_HOLD  SMALLINT,
				@DEFAULT_BILL_TYPE      TINYINT,
				@AB_CALC_METHOD         SMALLINT,
				@AB_ADV_BILL            BIT,
				@AB_BILLED              DECIMAL(15, 2),
				@AB_UNBILLED            DECIMAL(15, 2),
				@AB_INCOME_REC          DECIMAL(15, 2),
				@NEW_BILL_APPR_HDR_ID   INT
    
		SELECT @AE_EMP_CODE = EMP_CODE,
			   @DEFAULT_AB_FLAG = ISNULL(AB_FLAG, 0),
			   @DEFAULT_JOB_BILL_HOLD = ISNULL(JOB_BILL_HOLD, 0)
		FROM   JOB_COMPONENT WITH(NOLOCK)
		WHERE  JOB_NUMBER = @JOB_NUMBER
			   AND JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR;
    
		SELECT @AB_ADV_BILL = ISNULL(ADV_BILL, 0),
			   @AB_CALC_METHOD = ISNULL(CALC_METHOD, 0),
			   @AB_BILLED = BILLED,
			   @AB_UNBILLED = UNBILLED,
			   @AB_INCOME_REC = INCOME_REC
		FROM   dbo.advtf_advance_bill_by_job(@JOB_NUMBER, @JOB_COMPONENT_NBR)
    
		--SET DEFAULT BILL TYPE:
		IF ((@DEFAULT_AB_FLAG = 0)) --THEN IT IS BILL HOLD OR PROGRESS
			BEGIN
				IF (@DEFAULT_JOB_BILL_HOLD = 0)
				BEGIN
					SET @DEFAULT_BILL_TYPE = 0
				END
	        
				IF (@DEFAULT_JOB_BILL_HOLD = 1 OR @DEFAULT_JOB_BILL_HOLD = 2)
				BEGIN
					SET @DEFAULT_BILL_TYPE = 1
				END
			END
		ELSE
			--IT IS ADVANCE BILLING (ASSUME @AB_ADV_BILL AND @DEFAULT_AB_FLAG ARE BOTH NOT ZERO
			BEGIN
				IF (@AB_CALC_METHOD = 4)
				BEGIN
					SET @DEFAULT_BILL_TYPE = 3
				END
				ELSE
					--CALC_METHOD 0,1,2,NULL:
				BEGIN
					SET @DEFAULT_BILL_TYPE = 2
				END
			END
    
		INSERT INTO BILL_APPR_HDR WITH(ROWLOCK)
		  (
			BA_ID,
			JOB_NUMBER,
			JOB_COMPONENT_NBR,
			ACCT_EXEC,
			CREATE_DATE,
			CREATE_USER,
			BILL_TYPE,
			APPR_STATUS
		  )
		VALUES
		  (
			@BA_ID,
			@JOB_NUMBER,
			@JOB_COMPONENT_NBR,
			@AE_EMP_CODE,
			GETDATE(),
			@USER_CODE,
			ISNULL(@DEFAULT_BILL_TYPE,0),
			1
		  );
		  SET @NEW_BILL_APPR_HDR_ID = 0;
		  SELECT @NEW_BILL_APPR_HDR_ID = SCOPE_IDENTITY()

		--SAVE THE DTL ITEMS:
		IF @NEW_BILL_APPR_HDR_ID > 0
		BEGIN

			CREATE TABLE #APPROVAL_DETAILS
			(
				[ROW_ID]               [INT] IDENTITY(1, 1) NOT NULL,
				[BA_DTL_ID]            [INT] NOT NULL,
				[BA_ID]                [INT] NOT NULL,
				[JOB_NUMBER]           [INT] NOT NULL,
				[JOB_COMPONENT_NBR]    [SMALLINT] NOT NULL,
				[FNC_TYPE]             [VARCHAR](1) NULL,
				[FNC_TYPE_DESC]        [VARCHAR] (30) COLLATE SQL_Latin1_General_CP1_CS_AS NULL,
				[FNC_CODE]             [VARCHAR] (6) COLLATE SQL_Latin1_General_CP1_CS_AS NOT NULL,
				[FNC_DESC]             [VARCHAR] (30) COLLATE SQL_Latin1_General_CP1_CS_AS NULL,
				[DEFAULT_DESC]         [VARCHAR] (30) COLLATE SQL_Latin1_General_CP1_CS_AS NULL,
				[QUOTE_AMT]            [DECIMAL](15, 2) NULL,
				[ACTUALS]              [DECIMAL](15, 2) NULL,
				[BILLED_REC]           [DECIMAL](15, 2) NULL,
				[BILL_HOLD]            [DECIMAL](15, 2) NULL,
				[NON_BILL_FEE]         [DECIMAL](15, 2) NULL,
				[UNBILLED]             [DECIMAL](15, 2) NULL,
				[OPEN_PO]              [DECIMAL](15, 2) NULL,
				[QTY_HRS]              [DECIMAL](15, 2) NULL,
				[QUANTITY]             [DECIMAL](15, 2) NULL,
				[RATE]                 [DECIMAL](15, 2) NULL,
				[APPROVED_AMT]         [DECIMAL](15, 2) NULL,

				[TEMP_CUTOFF_APPROVED_AMT] [DECIMAL] (15,2) NULL,

				[FNC_COMMENTS]         [TEXT] COLLATE SQL_Latin1_General_CP1_CS_AS NULL,
				[CLIENT_COMMENTS]      [TEXT] COLLATE SQL_Latin1_General_CP1_CS_AS NULL,
				[ROW_TYPE]             [TINYINT] NULL,
				[IS_EXISTING_ROW]      [INT] NULL,
				[ITEM_EXISTS]          [BIT] NULL,
				[FNC_HEADING_ID]       [INT] NULL,
				[FNC_HEADING_DESC]     [VARCHAR] (60) COLLATE SQL_Latin1_General_CP1_CS_AS NULL,
				[FNC_HEADING_SEQ]      [INT],
				[FNC_ORDER]            [SMALLINT] NULL,
				[QUOTE_NET]            [DECIMAL](15, 2) NULL,
				[UNBILLED_NET]         [DECIMAL](15, 2) NULL,

				[TEMP_UNBILLED_NET] [DECIMAL](15, 2) NULL ,

				[APPR_NET]             [DECIMAL](14, 2) NULL,
				
				[TEMP_APPR_NET] [DECIMAL](14, 2) NULL,
	            
				[APPR_MARKUP_PCT]      [DECIMAL](6, 3) NULL,
				[APPR_MARKUP_AMT]      [DECIMAL](14, 2) NULL,
				[APPR_TAX_CODE]        [VARCHAR](4) COLLATE SQL_Latin1_General_CP1_CS_AS NULL,
				[APPR_TAX_COMM]        [SMALLINT] NULL,
				[APPR_TAX_COMM_ONLY]   [SMALLINT] NULL,
				[APPR_TAX_RESALE]      [SMALLINT] NULL,
				[APPR_RESALE_TAX]      [DECIMAL](14, 2) NULL,
				[APPR_VENDOR_TAX]      [DECIMAL](14, 2) NULL,
				[APPR_TAX_STATE_PCT]   [DECIMAL](8, 4) NULL,
				[APPR_TAX_COUNTY_PCT]  [DECIMAL](8, 4) NULL,
				[APPR_TAX_CITY_PCT]    [DECIMAL](8, 4) NULL,
				[QUOTE_MARKUP]         [DECIMAL](15, 2) NULL,
				[QUOTE_RESALE_TAX]     [DECIMAL](15, 2) NULL,
				[QUOTE_VENDOR_TAX]     [DECIMAL](15, 2) NULL,
				[UNBILLED_MARKUP]      [DECIMAL](15, 2) NULL,
				[UNBILLED_RESALE_TAX]  [DECIMAL](15, 2) NULL,
				[UNBILLED_VENDOR_TAX]  [DECIMAL](15, 2) NULL,
				[IS_USER_ROW]          [BIT] NULL,
				[IS_USING_TEMP_APPROVED_AMT] BIT NULL,

				[QUOTE_QTY_HRS] DECIMAL(15,2) NULL,
				[ACTUAL_QTY_HRS] DECIMAL(15,2) NULL,

				[TEMP_MARKUP] DECIMAL (15,2),
				[TEMP_RESALE_TAX] DECIMAL (16,2),
				[TEMP_TOTAL] DECIMAL (16,2),
				[TEMP_UNBILLED_MU] DECIMAL (15,2),
				[TEMP_UNBILLED_TAX] DECIMAL (15,2),
				[TEMP_UNBILLED_NR] DECIMAL (15,2),
				[TEMP_UNBILLED_TOT] DECIMAL (15,2),
				[TEMP_PO] DECIMAL (15,2)

			);

			INSERT INTO #APPROVAL_DETAILS
			  (
				BA_DTL_ID,
				BA_ID,
				JOB_NUMBER,
				JOB_COMPONENT_NBR,
				FNC_TYPE,
				FNC_TYPE_DESC,
				FNC_CODE,
				FNC_DESC,
				DEFAULT_DESC,
				QUOTE_AMT,
				ACTUALS,
				BILLED_REC,
				BILL_HOLD,
				NON_BILL_FEE,
				UNBILLED,
				OPEN_PO,
				QTY_HRS,
				QUANTITY,
				RATE,
				APPROVED_AMT,
				TEMP_CUTOFF_APPROVED_AMT,
				FNC_COMMENTS,
				CLIENT_COMMENTS,
				ROW_TYPE,
				IS_EXISTING_ROW,
				ITEM_EXISTS,
				FNC_HEADING_ID,
				FNC_HEADING_DESC,
				FNC_HEADING_SEQ,
				FNC_ORDER,
				QUOTE_NET,
				UNBILLED_NET,
				TEMP_UNBILLED_NET,
				APPR_NET,
				TEMP_APPR_NET,
				APPR_MARKUP_PCT,
				APPR_MARKUP_AMT,
				APPR_TAX_CODE,
				APPR_TAX_COMM,
				APPR_TAX_COMM_ONLY,
				APPR_TAX_RESALE,
				APPR_RESALE_TAX,
				APPR_VENDOR_TAX,
				APPR_TAX_STATE_PCT,
				APPR_TAX_COUNTY_PCT,
				APPR_TAX_CITY_PCT,
				QUOTE_MARKUP,
				QUOTE_RESALE_TAX,
				QUOTE_VENDOR_TAX,
				UNBILLED_MARKUP,
				UNBILLED_RESALE_TAX,
				UNBILLED_VENDOR_TAX,
				IS_USER_ROW,
				IS_USING_TEMP_APPROVED_AMT,
				QUOTE_QTY_HRS,
				ACTUAL_QTY_HRS,
				TEMP_MARKUP,
				TEMP_RESALE_TAX,
				TEMP_TOTAL,
				TEMP_UNBILLED_MU,
				TEMP_UNBILLED_TAX,
				TEMP_UNBILLED_NR,
				TEMP_UNBILLED_TOT,
				TEMP_PO
			  )
			EXEC usp_wv_BA_APPROVAL_JC_GET_DETAILS @JOB_NUMBER,
				 @JOB_COMPONENT_NBR,
				 @BA_ID,
				 @BA_BATCH_ID,
				 0,
				 NULL,
				 NULL
		 
			DECLARE @THIS_ROW_ID          AS INT,
					@FNC_CODE             AS VARCHAR(6),
					@QTY_HRS              AS DECIMAL(15, 3),
					@BILLING_RATE         AS DECIMAL(9, 2),
					@FNC_DESC             AS VARCHAR(30),
					@APPROVED_AMT         AS DECIMAL(15, 2),
					@ROW_TYPE             AS TINYINT,
					@APPR_NET             AS DECIMAL(14, 2),
					@APPR_MARKUP_PCT      DECIMAL(6, 3),
					@APPR_MARKUP_AMT      DECIMAL(14, 2),
					@APPR_TAX_CODE        VARCHAR(4),
					@APPR_TAX_COMM        SMALLINT,
					@APPR_TAX_COMM_ONLY   SMALLINT,
					@APPR_TAX_RESALE      SMALLINT,
					@APPR_RESALE_TAX      DECIMAL(14, 2),
					@APPR_VENDOR_TAX      DECIMAL(14, 2),
					@APPR_TAX_STATE_PCT   DECIMAL  (8, 4),
					@APPR_TAX_COUNTY_PCT  DECIMAL(8, 4),
					@APPR_TAX_CITY_PCT    DECIMAL(8, 4),
					@IS_USER_ROW          BIT ,
					@NEW_BA_DTL_ID		  INT
		 
			DECLARE MY_ROWS                        CURSOR  
			FOR
				SELECT ROW_ID
				FROM   #APPROVAL_DETAILS

			OPEN MY_ROWS;
				FETCH NEXT FROM MY_ROWS INTO @THIS_ROW_ID;
			WHILE @@FETCH_STATUS = 0
			BEGIN

				SET @NEW_BA_DTL_ID = 0;
				SELECT @FNC_CODE = #APPROVAL_DETAILS.FNC_CODE,
					   @QTY_HRS = #APPROVAL_DETAILS.QTY_HRS,
					   @BILLING_RATE = #APPROVAL_DETAILS.RATE,
					   @FNC_DESC = #APPROVAL_DETAILS.FNC_DESC,
					   @APPROVED_AMT = #APPROVAL_DETAILS.APPROVED_AMT,
					   @ROW_TYPE = #APPROVAL_DETAILS.ROW_TYPE,
					   @APPR_NET = #APPROVAL_DETAILS.APPR_NET,
					   @APPR_MARKUP_PCT = #APPROVAL_DETAILS.APPR_MARKUP_PCT,
					   @APPR_MARKUP_AMT = #APPROVAL_DETAILS.APPR_MARKUP_AMT,
					   @APPR_TAX_CODE = #APPROVAL_DETAILS.APPR_TAX_CODE,
					   @APPR_TAX_COMM = #APPROVAL_DETAILS.APPR_TAX_COMM,
					   @APPR_TAX_COMM_ONLY = #APPROVAL_DETAILS.APPR_TAX_COMM_ONLY,
					   @APPR_TAX_RESALE = #APPROVAL_DETAILS.APPR_TAX_RESALE,
					   @APPR_RESALE_TAX = #APPROVAL_DETAILS.APPR_RESALE_TAX,
					   @APPR_VENDOR_TAX = #APPROVAL_DETAILS.APPR_VENDOR_TAX,
					   @APPR_TAX_STATE_PCT = #APPROVAL_DETAILS.APPR_TAX_STATE_PCT,
					   @APPR_TAX_COUNTY_PCT = #APPROVAL_DETAILS.APPR_TAX_COUNTY_PCT,
					   @APPR_TAX_CITY_PCT = #APPROVAL_DETAILS.APPR_TAX_CITY_PCT,
					   @IS_USER_ROW = #APPROVAL_DETAILS.IS_USER_ROW
				FROM   #APPROVAL_DETAILS
				WHERE  #APPROVAL_DETAILS.ROW_ID = @THIS_ROW_ID;

				INSERT INTO BILL_APPR_DTL WITH
				  (
					ROWLOCK
				  )(
					   BA_ID,
					   JOB_NUMBER,
					   JOB_COMPONENT_NBR,
					   FNC_CODE,
					   QTY_HRS,
					   BILLING_RATE,
					   FNC_DESC,
					   APPROVED_AMT,
					   ROW_TYPE,
					   APPR_NET,
					   APPR_MARKUP_PCT,
					   APPR_MARKUP_AMT,
					   APPR_TAX_CODE,
					   APPR_TAX_COMM,
					   APPR_TAX_COMM_ONLY,
					   APPR_TAX_RESALE,
					   APPR_RESALE_TAX,
					   APPR_VENDOR_TAX,
					   APPR_TAX_STATE_PCT,
					   APPR_TAX_COUNTY_PCT,
					   APPR_TAX_CITY_PCT,
					   IS_USER_ROW
				   )
				VALUES
				  (
					@BA_ID,
					@JOB_NUMBER,
					@JOB_COMPONENT_NBR,
					@FNC_CODE,
					@QTY_HRS,
					@BILLING_RATE,
					@FNC_DESC,
					@APPROVED_AMT,
					@ROW_TYPE,
					@APPR_NET,
					@APPR_MARKUP_PCT,
					@APPR_MARKUP_AMT,
					@APPR_TAX_CODE,
					@APPR_TAX_COMM,
					@APPR_TAX_COMM_ONLY,
					@APPR_TAX_RESALE,
					@APPR_RESALE_TAX,
					@APPR_VENDOR_TAX,
					@APPR_TAX_STATE_PCT,
					@APPR_TAX_COUNTY_PCT,
					@APPR_TAX_CITY_PCT,
					0
				  );

				SELECT @NEW_BA_DTL_ID = SCOPE_IDENTITY();
				IF NOT @NEW_BA_DTL_ID IS NULL AND @NEW_BA_DTL_ID > 0
				BEGIN
					EXEC advsp_billing_approval_detail_calculate_amounts @NEW_BA_DTL_ID;
				END
		    
				--MOVE TO NEXT
				FETCH NEXT FROM MY_ROWS INTO @THIS_ROW_ID;

			END

			CLOSE MY_ROWS;
			DEALLOCATE MY_ROWS;
			DROP TABLE #APPROVAL_DETAILS;

		END
	END

	SELECT @CURR_APPR_STATUS = ISNULL(APPR_STATUS,0)
	FROM   BILL_APPR_HDR WITH(NOLOCK)
	WHERE  BA_ID = @BA_ID
		   AND JOB_NUMBER = @JOB_NUMBER
		   AND JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR;

	SET @CURR_APPR_STATUS = ISNULL(@CURR_APPR_STATUS,0);

	IF @AUTO_SAVE = 0 OR @CURR_APPR_STATUS = 1 --EITHER AN AUTOSAVE HAPPENED OR RECORD IS STILL IN LIMBO (APPR_STATUS = 1)
	BEGIN
		SET @RTN_VALUE = 1;
	END
	ELSE
	BEGIN
		SET @RTN_VALUE = 0;
	END

	SELECT @RTN_VALUE;

/*=========== QUERY ===========*/