SET QUOTED_IDENTIFIER ON 
GO
SET ANSI_NULLS OFF 
GO

if exists (select * from dbo.sysobjects where id = object_id(N'[dbo].[usp_wv_EmpListFromString]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
drop procedure [dbo].[usp_wv_EmpListFromString]
GO

CREATE PROCEDURE [dbo].[usp_wv_EmpListFromString] 
@EMP_LIST VARCHAR(MAX),
@ALERT_ID INT
AS
/*=========== QUERY ===========*/
	CREATE TABLE
		#USER_ENTERED (EMP_CODE VARCHAR(6) COLLATE DATABASE_DEFAULT);
	DECLARE
		@EMP_CODE VARCHAR(6),
		@POS INT

	SET @EMP_LIST = LTRIM(RTRIM(@EMP_LIST)) + ','
	SET @POS = CHARINDEX(',',@EMP_LIST,1)

	IF REPLACE(@EMP_LIST,',','') <> ''
	BEGIN
		WHILE @POS > 0
			BEGIN
				SET @EMP_CODE = LTRIM(RTRIM(LEFT(@EMP_LIST, @POS - 1)))
				IF @EMP_CODE <> ''
				BEGIN
					INSERT INTO #USER_ENTERED(EMP_CODE) VALUES(@EMP_CODE)
				END
			SET @EMP_LIST = RIGHT(@EMP_LIST,LEN(@EMP_LIST) - @POS)
			SET @POS = CHARINDEX(',',@EMP_LIST,1)
			END
	END

	IF @ALERT_ID > 0
	BEGIN

		DECLARE @ASSIGNEE_EMP_CODE VARCHAR(6)

		SELECT @ASSIGNEE_EMP_CODE = A.EMP_CODE FROM
		(
		SELECT ALERT_RCPT.EMP_CODE FROM ALERT_RCPT WITH(NOLOCK) WHERE ALERT_RCPT.ALERT_ID = @ALERT_ID AND ALERT_RCPT.CURRENT_NOTIFY = 1
		UNION
		SELECT ALERT_RCPT_DISMISSED.EMP_CODE FROM ALERT_RCPT_DISMISSED WITH(NOLOCK) WHERE ALERT_RCPT_DISMISSED.ALERT_ID = @ALERT_ID AND ALERT_RCPT_DISMISSED.CURRENT_NOTIFY = 1
		) AS A;

		SELECT B.EMP_CODE, B.EMP_FNAME, B.EMP_MI, B.EMP_LNAME, B.EMP_FML_NAME, B.EMP_EMAIL, B.MAILBEE_TITLE
		FROM (
		SELECT
			#USER_ENTERED.EMP_CODE,
			EMPLOYEE.EMP_FNAME,
			EMPLOYEE.EMP_MI,
			EMPLOYEE.EMP_LNAME,
			ISNULL(EMPLOYEE.EMP_FNAME+' ', '') + ISNULL(EMPLOYEE.EMP_MI+'. ', '') +  ISNULL(EMPLOYEE.EMP_LNAME, '') AS EMP_FML_NAME,
			EMPLOYEE.EMP_EMAIL,
			ISNULL(EMPLOYEE.EMP_FNAME+' ', '') + ISNULL(EMPLOYEE.EMP_MI+'. ', '') +  ISNULL(EMPLOYEE.EMP_LNAME, '') + ' <' + EMPLOYEE.EMP_EMAIL + '>' AS MAILBEE_TITLE,
			CASE WHEN EMPLOYEE.EMP_CODE = @ASSIGNEE_EMP_CODE THEN 1
			ELSE 0 END AS CURRENT_NOTIFY
		FROM
			#USER_ENTERED INNER JOIN EMPLOYEE WITH(NOLOCK) ON #USER_ENTERED.EMP_CODE = EMPLOYEE.EMP_CODE 
		WHERE
			 (EMPLOYEE.ALERT_EMAIL = 1 OR EMPLOYEE.ALERT_EMAIL = 3)
			AND (EMPLOYEE.EMP_TERM_DATE IS NULL OR EMPLOYEE.EMP_TERM_DATE > GETDATE())
		) AS B
		ORDER BY
			B.CURRENT_NOTIFY DESC, B.EMP_FNAME, B.EMP_LNAME;

	END
	ELSE
	BEGIN

		SELECT
			#USER_ENTERED.EMP_CODE,
			EMPLOYEE.EMP_FNAME,
			EMPLOYEE.EMP_MI,
			EMPLOYEE.EMP_LNAME,
			ISNULL(EMPLOYEE.EMP_FNAME+' ', '') + ISNULL(EMPLOYEE.EMP_MI+'. ', '') +  ISNULL(EMPLOYEE.EMP_LNAME, '') AS EMP_FML_NAME,
			EMPLOYEE.EMP_EMAIL,
			ISNULL(EMPLOYEE.EMP_FNAME+' ', '') + ISNULL(EMPLOYEE.EMP_MI+'. ', '') +  ISNULL(EMPLOYEE.EMP_LNAME, '') + ' <' + EMPLOYEE.EMP_EMAIL + '>' AS MAILBEE_TITLE
		FROM
			#USER_ENTERED INNER JOIN EMPLOYEE WITH(NOLOCK) ON #USER_ENTERED.EMP_CODE = EMPLOYEE.EMP_CODE 
		WHERE
			 (EMPLOYEE.ALERT_EMAIL = 1 OR EMPLOYEE.ALERT_EMAIL = 3)
			AND (EMPLOYEE.EMP_TERM_DATE IS NULL OR EMPLOYEE.EMP_TERM_DATE > GETDATE())
		ORDER BY
			EMPLOYEE.EMP_FNAME, EMPLOYEE.EMP_LNAME;

	END
	
	DROP TABLE #USER_ENTERED;	
/*=========== QUERY ===========*/
GO
SET QUOTED_IDENTIFIER ON 
GO
SET ANSI_NULLS ON 
GO