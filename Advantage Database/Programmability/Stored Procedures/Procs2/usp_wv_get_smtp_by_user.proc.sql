SET QUOTED_IDENTIFIER ON 
GO
SET ANSI_NULLS OFF 
GO
if exists (select * from dbo.sysobjects where id = object_id(N'[dbo].[usp_wv_get_smtp_by_user]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
drop procedure [dbo].[usp_wv_get_smtp_by_user]
GO

CREATE PROCEDURE [dbo].[usp_wv_get_smtp_by_user] 
@UserID AS VARCHAR(100) 
AS
	DECLARE @SMTP_SERVER         VARCHAR(40),
			@SMTP_SENDER         VARCHAR(50),
			@EMAIL_USERNAME      VARCHAR(50),
			@EMAIL_PWD           VARCHAR(50),
			@SMTP_AUTH_METHOD    SMALLINT,
			@SMTP_PORT_NUMBER    SMALLINT,
			@EMAIL_REPLY_TO      VARCHAR(50),
			@SMTP_SECURE_TYPE    SMALLINT,
			@SMTP_USE_EMP_FROM   SMALLINT,
			@USE_SMTP_FOR_PDF    SMALLINT,
			@MB_KEY              VARCHAR(50),
			@SMTP_USE_EMP_LOGIN  SMALLINT,
			@POP3_SERVER         VARCHAR(40),
			@POP3_USERNAME       VARCHAR(50),
			@POP3_PWD            VARCHAR(25),
			@POP3_REPLY_TO       VARCHAR(50)

	SELECT @SMTP_SERVER = ISNULL(SMTP_SERVER, ''),
		   @SMTP_SENDER = ISNULL(SMTP_SENDER, ''),
		   @EMAIL_USERNAME = ISNULL(EMAIL_USERNAME, ''),
		   @EMAIL_PWD = ISNULL(EMAIL_PWD, ''),
		   @SMTP_AUTH_METHOD = CASE WHEN SMTP_AUTH_METHOD IS NULL THEN 16
									WHEN SMTP_AUTH_METHOD = 0 THEN 0
									WHEN SMTP_AUTH_METHOD = 1 THEN 16
									WHEN SMTP_AUTH_METHOD = 2 THEN 8
									WHEN SMTP_AUTH_METHOD = 3 THEN 32
									WHEN SMTP_AUTH_METHOD = 4 THEN 128
									WHEN SMTP_AUTH_METHOD = 5 THEN 256 ELSE 1 END,
		   @SMTP_PORT_NUMBER = ISNULL(SMTP_PORT_NUMBER, 0),
		   @EMAIL_REPLY_TO = ISNULL(EMAIL_REPLY_TO, ''),
		   @SMTP_SECURE_TYPE = ISNULL(SMTP_SECURE_TYPE, 0),
		   @SMTP_USE_EMP_FROM = ISNULL(SMTP_USE_EMP_FROM, 0),
		   @USE_SMTP_FOR_PDF = ISNULL(USE_SMTP_FOR_PDF, 0),
		   @MB_KEY = MB_KEY,
		   @SMTP_USE_EMP_LOGIN = ISNULL(SMTP_USE_EMP_LOGIN, 0),
		   @POP3_SERVER = POP3_SERVER,
		   @POP3_USERNAME = POP3_USERNAME,
		   @POP3_PWD = POP3_PWD,
		   @POP3_REPLY_TO = POP3_REPLY_TO
	FROM   AGENCY WITH(NOLOCK);

	DECLARE @EMP_SMTP_SERVER     VARCHAR(40),
			@EMP_EMAIL           VARCHAR(50),
			@EMP_EMAIL_USERNAME  VARCHAR(50),
			@EMP_EMAIL_PWD       VARCHAR(50),
			@EMP_EMAIL_REPLY_TO  VARCHAR(50);

	SELECT @EMP_SMTP_SERVER = ISNULL(SMTP_SERVER, ''),
		   @EMP_EMAIL = ISNULL(EMP_EMAIL, ''),
		   @EMP_EMAIL_USERNAME = ISNULL(EMAIL_USERNAME, ''),
		   @EMP_EMAIL_PWD = ISNULL(EMAIL_PWD, ''),
		   @EMP_EMAIL_REPLY_TO = ISNULL(EMAIL_REPLY_TO, '')
	FROM   EMPLOYEE WITH(NOLOCK)
		   INNER JOIN SEC_USER WITH(NOLOCK)
				ON  (EMPLOYEE.EMP_CODE = SEC_USER.EMP_CODE)
	WHERE  UPPER(SEC_USER.USER_CODE) = UPPER(@UserID);

	IF @SMTP_USE_EMP_LOGIN = 1 --Same as @SMTP_USE_EMP_FROM
	BEGIN
		IF @EMP_EMAIL <> ''
		BEGIN
			SET @SMTP_SENDER = @EMP_EMAIL;
		END
    
		IF @EMP_EMAIL_USERNAME <> ''
		BEGIN
			SET @EMAIL_USERNAME = @EMP_EMAIL_USERNAME;
		END
    
		IF @EMP_EMAIL_PWD <> ''
		BEGIN
			SET @EMAIL_PWD = @EMP_EMAIL_PWD;
		END
    
		IF @EMP_EMAIL_REPLY_TO <> ''
		BEGIN
			SET @EMAIL_REPLY_TO = @EMP_EMAIL_REPLY_TO;
		END
	END

	IF (NOT(@POP3_SERVER IS NULL))
	   AND (NOT(@POP3_USERNAME IS NULL))
	   AND (NOT(@POP3_PWD IS NULL))
	BEGIN
		--USING LISTENER ACCOUNT
		SET @EMAIL_REPLY_TO = @POP3_REPLY_TO;
	END

	SELECT ISNULL(@SMTP_SERVER, '') AS SMTP_SERVER,
		   ISNULL(@SMTP_SENDER, '') AS SMTP_SENDER,
		   ISNULL(@EMAIL_USERNAME, '') AS EMAIL_USERNAME,
		   ISNULL(@EMAIL_PWD, '') AS EMAIL_PWD,
		   ISNULL(@SMTP_AUTH_METHOD, 0) AS SMTP_AUTH_METHOD,
		   ISNULL(@SMTP_PORT_NUMBER, 0) AS SMTP_PORT_NUMBER,
		   ISNULL(@EMAIL_REPLY_TO, '') AS EMAIL_REPLY_TO,
		   ISNULL(@SMTP_SECURE_TYPE, 0) AS SMTP_SECURE_TYPE,
		   ISNULL(@SMTP_USE_EMP_FROM, 0) AS SMTP_USE_EMP_FROM,
		   ISNULL(@USE_SMTP_FOR_PDF, 0) AS USE_SMTP_FOR_PDF,
		   ISNULL(@MB_KEY, '') AS MB_KEY,
		   ISNULL(@SMTP_USE_EMP_LOGIN, 0) AS SMTP_USE_EMP_LOGIN;



GO
SET QUOTED_IDENTIFIER ON 
GO
SET ANSI_NULLS ON 
GO