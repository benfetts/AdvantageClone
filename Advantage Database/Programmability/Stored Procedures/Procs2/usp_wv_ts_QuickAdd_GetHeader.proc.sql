IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'[dbo].[usp_wv_ts_QuickAdd_GetHeader]') AND OBJECTPROPERTY(id, N'IsProcedure') = 1)
DROP PROCEDURE [dbo].[usp_wv_ts_QuickAdd_GetHeader]
GO

CREATE PROCEDURE [dbo].[usp_wv_ts_QuickAdd_GetHeader] 
@JOB_NUMBER INT,
@JOB_COMPONENT_NBR SMALLINT,
@SEQ_NBR SMALLINT,
@EMP_CODE VARCHAR(6)
AS
/*=========== QUERY ===========*/

	DECLARE @EMP_DEF_FNC_CODE AS VARCHAR(6), @EMP_DEF_DP_TM_CODE AS VARCHAR(4);
	IF NOT @EMP_CODE IS NULL
		BEGIN
			SELECT @EMP_DEF_DP_TM_CODE = DP_TM_CODE,
				   @EMP_DEF_FNC_CODE = DEF_FNC_CODE
			FROM EMPLOYEE WITH (NOLOCK)
			WHERE EMP_CODE = @EMP_CODE;
		END;
	SELECT TOP 1 CLIENT.CL_CODE,
				 DIVISION.DIV_CODE,
				 PRODUCT.PRD_CODE,
				 CLIENT.CL_CODE+' - '+CLIENT.CL_NAME AS THE_CLIENT,
				 DIVISION.DIV_CODE+' - '+DIVISION.DIV_NAME AS THE_DIVISION,
				 PRODUCT.PRD_CODE+' - '+PRODUCT.PRD_DESCRIPTION AS THE_PRODUCT,
				 CAST(JOB_TRAFFIC_DET.JOB_NUMBER AS VARCHAR(20))+' - '+JOB_LOG.JOB_DESC AS THE_JOB,
				 CAST(JOB_TRAFFIC_DET.JOB_COMPONENT_NBR AS VARCHAR(20))+' - '+JOB_COMPONENT.JOB_COMP_DESC AS THE_JOB_COMPONENT,
				 JOB_TRAFFIC_DET.FNC_CODE,
				 JOB_TRAFFIC_DET.TASK_DESCRIPTION,
				 JOB_TRAFFIC_DET.FNC_EST,
				 ISNULL(DEPT_TEAM.DP_TM_CODE, @EMP_DEF_DP_TM_CODE) AS DP_TM_CODE,
				 JOB_TRAFFIC_DET_EMPS.TEMP_COMP_DATE,
				 JOB_TRAFFIC_DET_EMPS.EMP_CODE,
				 JOB_TRAFFIC_DET.FNC_COMMENTS,
				 @EMP_DEF_FNC_CODE AS EMP_DEF_FNC_CODE,
				 CLIENT.CL_NAME,
				 DIVISION.DIV_NAME,
				 PRODUCT.PRD_DESCRIPTION,
				 JOB_LOG.JOB_DESC,
				 JOB_COMPONENT.JOB_COMP_DESC,
				 JOB_TRAFFIC_DET.JOB_NUMBER,
				 JOB_TRAFFIC_DET.JOB_COMPONENT_NBR,
				 CASE
					 WHEN A.ALERT_LEVEL = 'BRD'
					 THEN A.ALERT_ID
					 ELSE NULL
				 END AS ALERT_ID
	FROM JOB_TRAFFIC_DET
		 INNER JOIN JOB_LOG ON JOB_TRAFFIC_DET.JOB_NUMBER = JOB_LOG.JOB_NUMBER
		 INNER JOIN JOB_COMPONENT ON JOB_TRAFFIC_DET.JOB_NUMBER = JOB_COMPONENT.JOB_NUMBER
									 AND JOB_TRAFFIC_DET.JOB_COMPONENT_NBR = JOB_COMPONENT.JOB_COMPONENT_NBR
		 INNER JOIN CLIENT ON JOB_LOG.CL_CODE = CLIENT.CL_CODE
		 INNER JOIN PRODUCT ON JOB_LOG.CL_CODE = PRODUCT.CL_CODE
							   AND JOB_LOG.DIV_CODE = PRODUCT.DIV_CODE
							   AND JOB_LOG.PRD_CODE = PRODUCT.PRD_CODE
		 INNER JOIN DIVISION ON JOB_LOG.CL_CODE = DIVISION.CL_CODE
								AND JOB_LOG.DIV_CODE = DIVISION.DIV_CODE
		 LEFT OUTER JOIN JOB_TRAFFIC_DET_EMPS ON JOB_TRAFFIC_DET.JOB_NUMBER = JOB_TRAFFIC_DET_EMPS.JOB_NUMBER
												 AND JOB_TRAFFIC_DET.JOB_COMPONENT_NBR = JOB_TRAFFIC_DET_EMPS.JOB_COMPONENT_NBR
												 AND JOB_TRAFFIC_DET.SEQ_NBR = JOB_TRAFFIC_DET_EMPS.SEQ_NBR
		 LEFT OUTER JOIN DEPT_TEAM ON JOB_COMPONENT.DP_TM_CODE = DEPT_TEAM.DP_TM_CODE
		 LEFT OUTER JOIN ALERT A ON JOB_TRAFFIC_DET.JOB_NUMBER = A.JOB_NUMBER
									AND JOB_TRAFFIC_DET.JOB_COMPONENT_NBR = A.JOB_COMPONENT_NBR
									AND A.TASK_SEQ_NBR = JOB_TRAFFIC_DET.SEQ_NBR
									AND A.ALERT_LEVEL = 'BRD'
	WHERE(JOB_TRAFFIC_DET.JOB_NUMBER = @JOB_NUMBER)
		 AND (JOB_TRAFFIC_DET.JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR)
		 AND (JOB_TRAFFIC_DET.SEQ_NBR = @SEQ_NBR)
		 AND 1 = CASE
					 WHEN @EMP_CODE IS NULL
					 THEN 1
					 WHEN JOB_TRAFFIC_DET_EMPS.EMP_CODE = @EMP_CODE
					 THEN 1
					 ELSE 0
				 END;

/*=========== QUERY ===========*/