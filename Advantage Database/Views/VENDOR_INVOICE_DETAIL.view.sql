CREATE VIEW [dbo].[VENDOR_INVOICE_DETAIL]
WITH SCHEMABINDING 
AS

SELECT
	[ID] = A.AP_ID,
	[SequenceNumber] = A.AP_SEQ,
	[VendorCode] = A.VN_FRL_EMP_CODE,
	[VendorName] = V.VN_NAME,
	[VendorCategory] = V.VN_CATEGORY,
	[OfficeCode] = A.OFFICE_CODE,
	[OfficeName] = O.OFFICE_NAME,
	[MarketCode] = V.MARKET_CODE,
	[MarketDescription] = V.MARKET_DESC,
	[InvoiceNumber] = A.AP_INV_VCHR,
	[InvoiceDate] = A.AP_INV_DATE,
	[EntryDate] = A.CREATE_DATE,
	[InvoiceDescription] = A.AP_DESC,
	[InvoiceAmount] = A.AP_INV_AMT + COALESCE(A.AP_SHIPPING, 0) + COALESCE(A.AP_SALES_TAX, 0),
	[ApprovalStatus] = CASE APPROVALS.[STATUS]
						WHEN 0 THEN 'Approval Not Required'
						WHEN 1 THEN 'Pending Approval'
						WHEN 2 THEN 'Approved'
						WHEN 3 THEN 'Approved With Changes'
						ELSE '' END,
	[PaymentHold] = A.PAYMENT_HOLD,
	[Is1099Invoice] = A.FLAG_1099,
	[PONumber] = PO.PO_NUMBER,
	[JobNumber] = JOB.JOB_NUMBER,
	[OrderNumber] = ORDERS.ORDER_NBR,
	[GLACode] = EXPENSE_ACCOUNTS.GLACODE,
	[VendorOfficeCode] = V.OFFICE_CODE
FROM [dbo].AP_HEADER A
	INNER JOIN 
		(SELECT VN_CODE, VN_NAME, VN_CATEGORY, M.MARKET_CODE, M.MARKET_DESC, OFFICE_CODE
		FROM [dbo].VENDOR 
		LEFT OUTER JOIN [dbo].MARKET M ON VENDOR.MARKET_CODE=M.MARKET_CODE) AS V ON A.VN_FRL_EMP_CODE = V.VN_CODE 
	LEFT OUTER JOIN [dbo].OFFICE O ON A.OFFICE_CODE=O.OFFICE_CODE
	LEFT OUTER JOIN
	(SELECT DISTINCT PO_NUMBER, AP_ID
		FROM [dbo].AP_PRODUCTION
		WHERE (MODIFY_DELETE IS NULL OR MODIFY_DELETE = 0)
		AND PO_NUMBER IS NOT NULL

		UNION

		SELECT PO_NUMBER, AP_ID
		FROM [dbo].AP_GL_DIST
		WHERE (MODIFY_DELETE IS NULL OR MODIFY_DELETE = 0)
		AND PO_NUMBER IS NOT NULL) AS PO ON A.AP_ID = PO.AP_ID 

	LEFT OUTER JOIN
	(SELECT DISTINCT JOB_NUMBER, AP_ID
		FROM [dbo].AP_PRODUCTION
		WHERE (MODIFY_DELETE IS NULL OR MODIFY_DELETE = 0)
		AND JOB_NUMBER IS NOT NULL) AS JOB ON A.AP_ID = JOB.AP_ID 
	
	LEFT OUTER JOIN
	(SELECT DISTINCT ORDER_NBR, AP_ID 
		FROM [dbo].AP_INTERNET
		WHERE (MODIFY_DELETE IS NULL OR MODIFY_DELETE = 0)

		UNION

		SELECT ORDER_NBR, AP_ID 
		FROM [dbo].AP_MAGAZINE 
		WHERE (MODIFY_DELETE IS NULL OR MODIFY_DELETE = 0)

		UNION
		
		SELECT ORDER_NBR, AP_ID 
		FROM [dbo].AP_NEWSPAPER 
		WHERE (MODIFY_DELETE IS NULL OR MODIFY_DELETE = 0)

		UNION
		
		SELECT ORDER_NBR, AP_ID 
		FROM [dbo].AP_OUTDOOR 
		WHERE (MODIFY_DELETE IS NULL OR MODIFY_DELETE = 0)

		UNION
		
		SELECT ORDER_NBR, AP_ID 
		FROM [dbo].AP_RADIO
		WHERE (MODIFY_DELETE IS NULL OR MODIFY_DELETE = 0)

		UNION
		
		SELECT ORDER_NBR, AP_ID 
		FROM [dbo].AP_TV
		WHERE (MODIFY_DELETE IS NULL OR MODIFY_DELETE = 0)
	) AS ORDERS ON A.AP_ID = ORDERS.AP_ID
	
	LEFT OUTER JOIN
	(SELECT AP_ID, ORDER_NBR, [STATUS]
		FROM [dbo].AP_MEDIA_APPROVAL
		WHERE ACTIVE_REV = 1) AS APPROVALS ON ORDERS.AP_ID = APPROVALS.AP_ID AND ORDERS.ORDER_NBR = APPROVALS.ORDER_NBR 
	
	LEFT OUTER JOIN
	(SELECT DISTINCT GLACODE, AP_ID
		FROM [dbo].AP_GL_DIST 
		WHERE (MODIFY_DELETE IS NULL OR MODIFY_DELETE = 0)

		UNION

		SELECT GLACODE, AP_ID
		FROM [dbo].AP_PRODUCTION
		WHERE (MODIFY_DELETE IS NULL OR MODIFY_DELETE = 0)

		UNION

		SELECT GLACODE, AP_ID
		FROM [dbo].AP_INTERNET
		WHERE (MODIFY_DELETE IS NULL OR MODIFY_DELETE = 0)

		UNION

		SELECT GLACODE, AP_ID
		FROM [dbo].AP_MAGAZINE 
		WHERE (MODIFY_DELETE IS NULL OR MODIFY_DELETE = 0)

		UNION

		SELECT GLACODE, AP_ID
		FROM [dbo].AP_NEWSPAPER 
		WHERE (MODIFY_DELETE IS NULL OR MODIFY_DELETE = 0)

		UNION

		SELECT GLACODE, AP_ID
		FROM [dbo].AP_OUTDOOR 
		WHERE (MODIFY_DELETE IS NULL OR MODIFY_DELETE = 0)

		UNION 

		SELECT GLACODE, AP_ID
		FROM [dbo].AP_RADIO 
		WHERE (MODIFY_DELETE IS NULL OR MODIFY_DELETE = 0)

		UNION

		SELECT GLACODE, AP_ID
		FROM [dbo].AP_TV 
		WHERE (MODIFY_DELETE IS NULL OR MODIFY_DELETE = 0)) AS EXPENSE_ACCOUNTS ON A.AP_ID = EXPENSE_ACCOUNTS.AP_ID
	
WHERE (A.ARCHIVE_FLAG IS NULL OR A.ARCHIVE_FLAG = 0)
AND A.MODIFY_FLAG IS NULL
AND A.DELETE_FLAG IS NULL
GO