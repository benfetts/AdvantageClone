if exists (select * from dbo.sysobjects where id = object_id(N'[dbo].[V_DRPT_CRM_PROSPECTS]') and OBJECTPROPERTY(id, N'IsView') = 1)
drop view [dbo].[V_DRPT_CRM_PROSPECTS]
GO

CREATE VIEW [dbo].[V_DRPT_CRM_PROSPECTS]

WITH SCHEMABINDING 
AS
	-- 09/13/2016 - changed report to have an option to include inactive CDPs
	SELECT 
		[ID] = NEWID(),
		[ClientCode] = A.CL_CODE,
		[ClientName] = CL.CL_NAME,
		[ClientInactive] = CAST(CASE WHEN COALESCE(CL.ACTIVE_FLAG, 0) = 0 THEN 1 ELSE 0 END AS BIT),
		[DivisionCode] = A.DIV_CODE,
		[DivisionName] = D.DIV_NAME,
		[DivisionInactive] = CAST(CASE WHEN COALESCE(D.ACTIVE_FLAG, 0) = 0 THEN 1 ELSE 0 END AS BIT),
		[ProductCode] = A.PRD_CODE,
		[ProductName] = P.PRD_DESCRIPTION,
		[ProductInactive] = CAST(CASE WHEN COALESCE(P.ACTIVE_FLAG, 0) = 0 THEN 1 ELSE 0 END AS BIT),
		[IndustryDescription] = I.[DESCRIPTION],
		[SpecialtyDescription] = S.[DESCRIPTION],
		[Region] = REG.REGION_DESC,
		[Revenue] = ISNULL(CP.REVENUE,0),
		[NumberOfEmployees] = CP.NUM_EMPLOYEES,
		[CaseStudyDone] = CAST(COALESCE(CP.CASE_STUDY, 0) AS BIT),
		[UseAsReference] = CAST(COALESCE(CP.REFERENCE, 0) AS BIT),
		[Notes] = CP.NOTES,
		[LeadDate] = A.LEAD_DATE,
		[SourceDescription] = SRC.[DESCRIPTION],
		LAD.LastActivityDate,
		LAD.LastActivityType,
		LAD.LastActivityEmployee,
		LAD.LastActivitySubject,
		LAD.LastActivityBody,
		[LastContactDate] = A.LAST_CONTACT_DATE,
		[Probability] = A.PROBABILITY,
		[RatingDescription] = R.[DESCRIPTION],
		[CurrentProvider] = A.CURRENT_PROVIDER,
		[TotalOpportunityValue] = C.TotalContractValue,
		[DefaultAECode] = AE.EMP_CODE,
		[DefaultAEName] = COALESCE((RTRIM(EMP_FNAME) + ' '),'') + COALESCE((EMP_MI + '. '),'') + COALESCE(EMP_LNAME,''),
		[SoldDate] = A.SOLD_DATE,
		[LostDate] = A.LOST_DATE,
		[TurnoverPercent] = ISNULL(CP.TURNOVER_PERCENT,0),
		[Type1Code] = CP.CLIENT_TYPE1_ID,
		[Type1Description] = CT1.[DESCRIPTION],
		[Type2Code] = CP.CLIENT_TYPE2_ID,
		[Type2Description] = CT2.[DESCRIPTION],
		[Type3Code] = CP.CLIENT_TYPE3_ID,
		[Type3Description] = CT3.[DESCRIPTION]
		
	FROM
		[dbo].[ACTIVITY] AS A LEFT OUTER JOIN
		[dbo].[CLIENT] AS CL ON A.CL_CODE = CL.CL_CODE LEFT OUTER JOIN
		[dbo].[DIVISION] AS D ON A.CL_CODE = D.CL_CODE AND A.DIV_CODE = D.DIV_CODE LEFT OUTER JOIN
		[dbo].[PRODUCT] AS P ON A.CL_CODE = P.CL_CODE AND A.DIV_CODE = P.DIV_CODE AND A.PRD_CODE = P.PRD_CODE LEFT OUTER JOIN
		(SELECT CL_CODE, DIV_CODE, PRD_CODE,
			SUM(ISNULL(FEE_RETAINER,0) + 
			ISNULL(FEE_INCENTIVE_BONUS,0) + 
			ISNULL(FEE_ROYALTY,0) + 
			ISNULL(FEE_PROJECT_HOURLY,0) + 
			ISNULL(MEDIA_COMMISSION,0) + 
			ISNULL(PRODUCTION_COMMISSION,0)) AS TotalContractValue
		 FROM [dbo].[CONTRACT] C
		 WHERE	C.IS_CONTRACT = 0
		 AND	C.INACTIVE_FLAG = 0
		 GROUP BY CL_CODE, DIV_CODE, PRD_CODE) AS C ON A.CL_CODE = C.CL_CODE AND A.DIV_CODE = C.DIV_CODE AND A.PRD_CODE = C.PRD_CODE LEFT OUTER JOIN
		[dbo].[COMPANY_PROFILE] AS CP ON A.CL_CODE = CP.CL_CODE AND A.DIV_CODE = CP.DIV_CODE AND A.PRD_CODE = CP.PRD_CODE LEFT OUTER JOIN
		[dbo].[REGION] AS REG ON CP.REGION_CODE = REG.REGION_CODE LEFT OUTER JOIN
		[dbo].[INDUSTRY] AS I ON CP.INDUSTRY_ID = I.INDUSTRY_ID LEFT OUTER JOIN 
		[dbo].[SPECIALTY] AS S ON CP.SPECIALTY_ID = S.SPECIALTY_ID LEFT OUTER JOIN
		[dbo].[RATING] AS R ON A.RATING_ID = R.RATING_ID LEFT OUTER JOIN
		[dbo].[SOURCE] AS SRC ON A.SOURCE_ID = SRC.SOURCE_ID LEFT OUTER JOIN
		(SELECT LA.CL_CODE, LA.DIV_CODE, LA.PRD_CODE, LA.LastActivityBody, LA.LastActivityDate, LA.LastActivitySubject, LA.LastActivityType, LA.LastActivityEmployee
		 FROM [dbo].[V_CRM_LAST_ACTIVITY] AS LA INNER JOIN
		 (SELECT MAX(LastActivityDate) AS LastActivityDate, CL_CODE, DIV_CODE, PRD_CODE 
		  FROM [dbo].[V_CRM_LAST_ACTIVITY]
		  GROUP BY CL_CODE, DIV_CODE, PRD_CODE) AS AD ON LA.LastActivityDate = AD.LastActivityDate AND LA.CL_CODE = AD.CL_CODE AND LA.DIV_CODE = AD.DIV_CODE AND LA.PRD_CODE = AD.PRD_CODE
		 ) AS LAD ON A.CL_CODE = LAD.CL_CODE AND A.DIV_CODE = LAD.DIV_CODE AND A.PRD_CODE = LAD.PRD_CODE LEFT OUTER JOIN
		[dbo].[ACCOUNT_EXECUTIVE] AE ON P.CL_CODE = AE.CL_CODE AND P.DIV_CODE = AE.DIV_CODE AND P.PRD_CODE = AE.PRD_CODE AND AE.DEFAULT_AE = 1 LEFT OUTER JOIN
		[dbo].[EMPLOYEE_CLOAK] E ON AE.EMP_CODE = E.EMP_CODE LEFT OUTER JOIN
		[dbo].[CLIENT_TYPE1] AS CT1 ON CT1.CLIENT_TYPE1_ID = CP.CLIENT_TYPE1_ID LEFT OUTER JOIN
		[dbo].[CLIENT_TYPE2] AS CT2 ON CT2.CLIENT_TYPE2_ID = CP.CLIENT_TYPE2_ID LEFT OUTER JOIN
		[dbo].[CLIENT_TYPE3] AS CT3 ON CT3.CLIENT_TYPE3_ID = CP.CLIENT_TYPE3_ID
		
	WHERE
			CL.NEW_BUSINESS = 1
GO