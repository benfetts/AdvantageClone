-- V_PROD_BILLING_DATA_TAX_IND
-- #00 08/17/12 - initial version

SET QUOTED_IDENTIFIER ON 
GO

SET ANSI_NULLS ON 
GO

SET ANSI_PADDING OFF
GO

IF EXISTS ( SELECT * FROM dbo.sysobjects WHERE id = OBJECT_ID( N'[dbo].[V_PROD_BILLING_DATA_TAX_IND]' ) AND OBJECTPROPERTY( id, N'IsView' ) = 1 )
	DROP VIEW [dbo].[V_PROD_BILLING_DATA_TAX_IND]
GO

CREATE VIEW dbo.V_PROD_BILLING_DATA_TAX_IND 
AS

	SELECT
		p.[USER_ID],
		p.INVOICE_NUMBER,
		p.FNC_CODE,
		CASE
			WHEN ABS(SUM(p.EXT_NONRESALE_TAX + p.EXT_CITY_RESALE + p.EXT_COUNTY_RESALE + p.EXT_STATE_RESALE)) < .01 THEN ''
			WHEN s.FUNCTION_OPT = 0 THEN ''
			ELSE '*'
		END AS CUR_TAX_IND,
		CASE
			WHEN ABS(SUM(p.EXT_NONRESALE_TAX + p.EXT_CITY_RESALE + p.EXT_COUNTY_RESALE + p.EXT_STATE_RESALE +
				p.PRIOR_EXT_NONRESALE_TAX + p.PRIOR_EXT_CITY_RESALE + p.PRIOR_EXT_COUNTY_RESALE + p.PRIOR_EXT_STATE_RESALE)) < .01 THEN ''
			WHEN s.FUNCTION_OPT = 0 THEN ''
			ELSE '*'
		END AS TTD_TAX_IND,
		CASE
			WHEN ABS(SUM(p.EXT_NONRESALE_TAX + p.EXT_CITY_RESALE + p.EXT_COUNTY_RESALE + p.EXT_STATE_RESALE +
				p.PRIOR_EXT_NONRESALE_TAX + p.PRIOR_EXT_CITY_RESALE + p.PRIOR_EXT_COUNTY_RESALE + p.PRIOR_EXT_STATE_RESALE +
				p.EST_EXT_NONRESALE_TAX + p.EST_EXT_CITY_RESALE + p.EST_EXT_COUNTY_RESALE + p.EST_EXT_STATE_RESALE)) < .01 THEN ''
			WHEN s.FUNCTION_OPT = 0 THEN ''
			ELSE '*'
		END AS EPCT_TAX_IND
	FROM dbo.PROD_BILLING_DATA AS p
	JOIN dbo.RPT_RUNTIME_SPECS AS s
		ON p.[USER_ID] = s.[USER_ID]
	WHERE s.APP_TYPE = 'PI'	
	GROUP BY p.[USER_ID], p.INVOICE_NUMBER, p.FNC_CODE, s.FUNCTION_OPT

GO

IF ( @@ERROR = 0 )
	GRANT ALL ON [V_PROD_BILLING_DATA_TAX_IND] TO PUBLIC AS dbo
GO	
