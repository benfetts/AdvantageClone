-- V_PROD_ESTIMATE
-- #00 08/31/12 - initial version

SET QUOTED_IDENTIFIER ON 
GO

SET ANSI_NULLS ON 
GO

SET ANSI_PADDING OFF
GO

if exists (select * from dbo.sysobjects where id = object_id(N'[dbo].[V_PROD_ESTIMATE]') and OBJECTPROPERTY(id, N'IsView') = 1)
drop view [dbo].[V_PROD_ESTIMATE]
GO

CREATE VIEW dbo.V_PROD_ESTIMATE 
AS

	SELECT
		s.[USER_ID],
		s.APP_TYPE,
		p.OFFICE_CODE,
		p.PRD_CONSOL_FUNC,
		e.SC_CODE,
		e.CL_CODE,
		e.DIV_CODE,
		e.PRD_CODE,
		e.ESTIMATE_NUMBER,
		e.EST_LOG_DESC,
		e.EST_LOG_COMMENT,
		ec.EST_COMPONENT_NBR,
		ec.EST_COMP_DESC,
		ec.EST_COMP_COMMENT,
		ec.CDP_CONTACT_ID,
		eq.EST_QUOTE_NBR,
		eq.EST_QUOTE_DESC,
		eq.EST_QTE_COMMENT,
		er.EST_REV_NBR,
		er.EST_REV_COMMENT,
		ed.SEQ_NBR,
		ed.FNC_CODE,
		ed.EST_REV_SUP_BY_CDE,
		ed.EMPLOYEE_TITLE_ID,
		ed.EST_FNC_COMMENT,
		ed.EST_FNC_TYPE,
		ed.EST_NONBILL_FLAG,
		ed.EST_REV_QUANTITY,
		ed.EST_REV_RATE,
		ed.EST_REV_EXT_AMT,
		ed.EXT_MARKUP_AMT,
		ed.EXT_NONRESALE_TAX,
		ed.EXT_STATE_RESALE,
		ed.EXT_COUNTY_RESALE,
		ed.EXT_CITY_RESALE,
		ed.EXT_NONRESALE_TAX + ed.EXT_STATE_RESALE + ed.EXT_COUNTY_RESALE + ed.EXT_CITY_RESALE AS EXT_TOTAL_TAX,
		ed.LINE_TOTAL,
		ed.EXT_CONTINGENCY,
		ed.EXT_MU_CONT,
		ed.EXT_NR_CONT,
		ed.EXT_STATE_CONT,
		ed.EXT_COUNTY_CONT,
		ed.EXT_CITY_CONT,
		ed.EXT_NR_CONT + ed.EXT_STATE_CONT + ed.EXT_COUNTY_CONT + ed.EXT_CITY_CONT AS EXT_TAX_CONT,
		ed.LINE_TOTAL_CONT,
		ed.EST_REV_EXT_AMT + ed.EXT_CONTINGENCY AS TTL_REV_EXT_AMT,
		ed.EXT_MARKUP_AMT + ed.EXT_MU_CONT AS TTL_MARKUP_AMT,
		ed.EXT_NONRESALE_TAX + ed.EXT_NR_CONT AS TTL_NONRESALE_TAX,
		ed.EXT_STATE_RESALE + ed.EXT_STATE_CONT AS TTL_STATE_RESALE,
		ed.EXT_COUNTY_RESALE + ed.EXT_COUNTY_CONT AS TTL_COUNTY_RESALE,
		ed.EXT_CITY_RESALE + ed.EXT_CITY_CONT AS TTL_CITY_RESALE,
		ed.EXT_NONRESALE_TAX + ed.EXT_STATE_RESALE + ed.EXT_COUNTY_RESALE + ed.EXT_CITY_RESALE +
			ed.EXT_NR_CONT + ed.EXT_STATE_CONT + ed.EXT_COUNTY_CONT + ed.EXT_CITY_CONT AS TTL_TOTAL_TAX,
		ed.LINE_TOTAL + ed.LINE_TOTAL_CONT AS TTL_LINE_TOTAL,
		s.ADDRESS_OPT,
		a.CL_NAME,
		a.DIV_NAME,
		a.PRD_DESCRIPTION,
		CASE s.ADDRESS_OPT
			WHEN 4 THEN	ch.CONT_ADDRESS1
			ELSE a.ADDRESS1
		END AS ADDRESS1,
		CASE s.ADDRESS_OPT
			WHEN 4 THEN	ch.CONT_ADDRESS2
			ELSE a.ADDRESS2
		END AS ADDRESS2,
		CASE s.ADDRESS_OPT
			WHEN 4 THEN	ch.CONT_CITY
			ELSE a.CITY
		END AS CITY,
		CASE s.ADDRESS_OPT
			WHEN 4 THEN	ch.CONT_STATE
			ELSE a.[STATE] 
		END AS STATE,
		CASE s.ADDRESS_OPT
			WHEN 4 THEN	ch.CONT_ZIP
			ELSE a.ZIP
		END AS ZIP,
		CASE s.ADDRESS_OPT
			WHEN 4 THEN	ISNULL(ch.CONT_CITY,'') + ', ' + ISNULL(ch.CONT_STATE,'') + ' ' + ISNULL(ch.CONT_ZIP,'')
			ELSE ISNULL(a.CITY,'') + ', ' + ISNULL(a.[STATE],'') + ' ' + ISNULL(a.ZIP,'')
		END AS CSZ,
		CASE s.ADDRESS_OPT
			WHEN 4 THEN	ISNULL(ch.CONT_FNAME,'') + ' ' + ISNULL(ch.CONT_MI,'') + ' ' + ISNULL(ch.CONT_LNAME,'')
			ELSE a.ATTN
		END AS ATTN,
		CASE s.ADDRESS_OPT
			WHEN 4 THEN	ch.CONT_COUNTY
			ELSE a.COUNTY
		END AS COUNTY,
		CASE s.ADDRESS_OPT
			WHEN 4 THEN	ch.CONT_COUNTRY
			ELSE a.COUNTRY
		END AS COUNTRY	
		 
		FROM dbo.RPT_RUNTIME_SPECS AS s
		JOIN dbo.RPT_SEL_NBRS AS n
			ON s.[USER_ID] = n.[USER_ID]
			AND s.APP_TYPE = n.APP_TYPE
		JOIN dbo.ESTIMATE_LOG AS e
			ON n.ESTIMATE_NBR = e.ESTIMATE_NUMBER  
		LEFT JOIN dbo.ESTIMATE_COMPONENT AS ec  
			ON e.ESTIMATE_NUMBER = ec.ESTIMATE_NUMBER 
		LEFT JOIN dbo.ESTIMATE_QUOTE AS eq 
			ON ec.ESTIMATE_NUMBER = eq.ESTIMATE_NUMBER
			AND ec.EST_COMPONENT_NBR = eq.EST_COMPONENT_NBR
		LEFT JOIN dbo.ESTIMATE_REV AS er 
			ON eq.ESTIMATE_NUMBER = er.ESTIMATE_NUMBER
			AND eq.EST_COMPONENT_NBR = er.EST_COMPONENT_NBR 
			AND eq.EST_QUOTE_NBR = er.EST_QUOTE_NBR 
		LEFT JOIN dbo.ESTIMATE_REV_DET AS ed 
			ON er.ESTIMATE_NUMBER = ed.ESTIMATE_NUMBER
			AND er.EST_COMPONENT_NBR = ed.EST_COMPONENT_NBR 
			AND er.EST_QUOTE_NBR = ed.EST_QUOTE_NBR    
			AND er.EST_REV_NBR = ed.EST_REV_NBR
		JOIN dbo.PRODUCT AS p
			ON e.CL_CODE = p.CL_CODE
			AND e.DIV_CODE = p.DIV_CODE
			AND e.PRD_CODE = p.PRD_CODE
		LEFT JOIN dbo.V_ADDRESS_TABLE AS a
			ON e.CL_CODE = a.CL_CODE
			AND e.DIV_CODE = a.DIV_CODE
			AND e.PRD_CODE = a.PRD_CODE
		LEFT JOIN dbo.CDP_CONTACT_HDR AS ch
			ON ec.CDP_CONTACT_ID = ch.CDP_CONTACT_ID			
		WHERE 
			s.[USER_ID] = dbo.fn_AdvanUser()
			AND s.APP_TYPE = 'ES'
			AND er.EST_REV_NBR = (SELECT MAX(er2.EST_REV_NBR) FROM dbo.ESTIMATE_REV AS er2
				WHERE er.ESTIMATE_NUMBER = er2.ESTIMATE_NUMBER
				AND er.EST_COMPONENT_NBR = er2.EST_COMPONENT_NBR 
				AND er.EST_QUOTE_NBR = er2.EST_QUOTE_NBR)
GO

IF ( @@ERROR = 0 )
	GRANT ALL ON V_PROD_ESTIMATE TO PUBLIC AS dbo
GO	
