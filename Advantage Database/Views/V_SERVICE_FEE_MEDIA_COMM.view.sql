CREATE VIEW [dbo].[V_SERVICE_FEE_MEDIA_COMM]

AS

	SELECT 
		[ID] = ISNULL(ROW_NUMBER() OVER(ORDER BY (SELECT 1)), 0),
		[JobNumber] = AR.JOB_NUMBER,
		[ComponentNumber] = AR.JOB_COMPONENT_NBR, 
		[ClientCode] = AR.CL_CODE,
		[ClientDescription] = C.CL_NAME,
		[DivisionCode] = AR.DIV_CODE,
		[DivisionDescription] = D.DIV_NAME,
		[ProductCode] = AR.PRD_CODE,
		[ProductDescription] = P.PRD_DESCRIPTION,
		[MediaType] = AR.REC_TYPE, 
		[PostPeriodCode] = AR.AR_POST_PERIOD,
		[MediaCommissionAmount] = SUM(AR.AR_COMM_AMT),
		[ServiceFeeTypeCode] = SFT.CODE  
	FROM  
		[dbo].[ACCT_REC] AR INNER JOIN
		[dbo].[CLIENT] AS C ON C.CL_CODE = AR.CL_CODE LEFT OUTER JOIN
		[dbo].[DIVISION] AS D ON D.CL_CODE = AR.CL_CODE AND
								 D.DIV_CODE = AR.DIV_CODE LEFT OUTER JOIN
		[dbo].[PRODUCT] AS P ON P.CL_CODE = AR.CL_CODE AND
								P.DIV_CODE = AR.DIV_CODE AND
								P.PRD_CODE = AR.PRD_CODE LEFT OUTER JOIN
		[dbo].[JOB_COMPONENT] AS JC ON JC.JOB_NUMBER = AR.JOB_NUMBER AND
									   JC.JOB_COMPONENT_NBR = AR.JOB_COMPONENT_NBR LEFT OUTER JOIN
		[dbo].[SERVICE_FEE_TYPE] AS SFT ON SFT.SERVICE_FEE_TYPE_ID = JC.SERVICE_FEE_TYPE_ID 
	WHERE 
		(AR.AR_INV_SEQ = 0 OR 
		 AR.AR_INV_SEQ = 99) AND 
		(AR.MANUAL_INV = 0 OR 
		 AR.MANUAL_INV IS NULL) 
	GROUP BY 
		AR.JOB_NUMBER,
		AR.JOB_COMPONENT_NBR, 
		AR.CL_CODE,
		C.CL_NAME,
		AR.DIV_CODE,
		D.DIV_NAME,
		AR.PRD_CODE,
		P.PRD_DESCRIPTION,
		AR.REC_TYPE,
		AR.AR_POST_PERIOD,
		SFT.CODE

GO


