--DROP VIEW [dbo].[WV_MYALERTSLIST]
CREATE VIEW [dbo].[WV_MYALERTSLIST]
AS
	SELECT 
	       --TOP 100 PERCENT 
	       ALERT.ALERT_ID,
		   ALERT.SUBJECT,
		   COALESCE(
			   (
				   SELECT MAX(GENERATED_DATE) AS MAX_DATE
				   FROM   ALERT_COMMENT WITH (NOLOCK)
				   WHERE  (ALERT_ID = ALERT.ALERT_ID)
			   ),
			   ALERT.GENERATED
		   ) AS GENERATED,
		   ALERT_RCPT.PROCESSED,
		   CASE 
				WHEN ALERT.CP_ALERT = 1 AND 
					 ISNUMERIC(ALERT_USER) = 1 THEN (
						 SELECT CONT_FML
						 FROM   CDP_CONTACT_HDR WITH (NOLOCK)
						 WHERE  CDP_CONTACT_ID = CAST(ALERT_USER AS INT)
					 )
				ELSE [dbo].[advfn_get_user_name](ALERT.ALERT_USER)
		   END AS USER_NAME,
		   ALERT_RCPT.EMP_CODE,
		   CASE 
				WHEN ALERT.PRIORITY = 1 THEN 1
				WHEN ALERT.PRIORITY = 3 THEN 3
				ELSE 2
		   END AS PRIORITY,
		   ALERT_TYPE.ALERT_TYPE_DESC AS TYPE,
		   ALERT_CATEGORY.ALERT_DESC AS CATEGORY,
		   ALERT_TYPE.ALERT_TYPE_ID,
		   ALERT_CATEGORY.ALERT_CAT_ID,
		   (
			   SELECT COUNT(1) AS ATTACHMENTCOUNT
			   FROM   ALERT_ATTACHMENT WITH (NOLOCK)
			   WHERE  (ALERT_ID = ALERT.ALERT_ID)
		   ) AS ATTACHMENTCOUNT,
		   ALERT.ALERT_LEVEL,
		   ALERT.OFFICE_CODE,
		   ALERT.CL_CODE,
		   ALERT.DIV_CODE,
		   ALERT.PRD_CODE,
		   ALERT.CMP_CODE,
		   CASE ALERT.JOB_NUMBER
				WHEN 0 THEN NULL
				ELSE ALERT.JOB_NUMBER
		   END AS JOB_NUMBER,
		   CASE ALERT.JOB_COMPONENT_NBR
				WHEN 0 THEN NULL
				ELSE ALERT.JOB_COMPONENT_NBR
		   END AS JOB_COMPONENT_NBR,
		   ALERT.EMP_CODE AS SENDER_EMP_CODE,
		   CONVERT(CHAR(10), ALERT_RCPT.PROCESSED, 110) AS DISMISSED_DATE,
		   CLIENT.CL_NAME,
		   DIVISION.DIV_NAME,
		   PRODUCT.PRD_DESCRIPTION,
		   CAMPAIGN.CMP_NAME,
		   ALERT.BODY,
		   LOWER(ALERT.SUBJECT) AS LOWER_SUBJECT,
		   LOWER(CAST(ALERT.BODY AS VARCHAR)) AS LOWER_BODY,
		   OFFICE.OFFICE_NAME,
		   JOB_LOG.JOB_DESC,
		   JOB_COMPONENT.JOB_COMP_DESC,
		   ALERT.ALERT_USER,
		   ALERT.CP_ALERT,
		   ALERT_RCPT.NEW_ALERT,
		   ALERT_RCPT.READ_ALERT,
		   ALERT.VN_CODE,
		   ALERT.DUE_DATE,
		   ALERT.TIME_DUE,
		   ALERT.ALERT_STATE_ID,
		   ALERT_STATES.ALERT_STATE_NAME,
		   ALERT_RCPT.CURRENT_NOTIFY,
		   CASE 
				WHEN ALERT_RCPT.CURRENT_NOTIFY = 1 THEN ALERT_RCPT.EMP_CODE
				ELSE NULL
		   END AS CURRENT_NOTIFY_EMP_CODE,
		   CASE 
				WHEN ALERT_RCPT.CURRENT_NOTIFY = 1 AND (ALERT_RCPT.PROCESSED IS NULL) THEN (
						 ISNULL([EMPLOYEE].EMP_FNAME + ' ', '') 
						 + ISNULL([EMPLOYEE].EMP_MI + '. ', '') + ISNULL([EMPLOYEE].EMP_LNAME, '')
					 )
				WHEN ALERT_RCPT.CURRENT_NOTIFY = 1 AND 
					 (NOT (ALERT_RCPT.PROCESSED IS NULL)) THEN 'Completed'
				WHEN ALERT.ALERT_STATE_ID > 0 AND ALERT.ALRT_NOTIFY_HDR_ID > 0 AND 
					 ALERT_RCPT.CURRENT_NOTIFY IS NULL THEN 'Unassigned'
				ELSE NULL
		   END AS CURRENT_NOTIFY_EMP_FML,
		   OFFICE.OFFICE_CODE + ' - ' + OFFICE.OFFICE_NAME AS GRP_OFFICE,
		   CLIENT.CL_CODE + ' - ' + CLIENT.CL_NAME AS GRP_CLIENT,
		   CLIENT.CL_CODE + ', ' + DIVISION.DIV_CODE + ' - ' + DIVISION.DIV_NAME AS 
		   GRP_DIVISION,
		   CLIENT.CL_CODE + ', ' + DIVISION.DIV_CODE + ', ' + PRODUCT.PRD_CODE + 
		   ' - ' + PRODUCT.PRD_DESCRIPTION AS GRP_PRODUCT,
		   RIGHT(
			   REPLICATE('0', 6) 
			   + CONVERT(VARCHAR(20), JOB_LOG.JOB_NUMBER),
			   6
		   ) + ' - ' + ISNULL(JOB_LOG.JOB_DESC, '') AS GRP_JOB,
		   RIGHT(
			   REPLICATE('0', 6) + CONVERT(VARCHAR(20), JOB_COMPONENT.JOB_NUMBER),
			   6
		   ) + ' - ' + RIGHT(
			   REPLICATE('0', 2) + CONVERT(VARCHAR(20), JOB_COMPONENT.JOB_COMPONENT_NBR),
			   2
		   ) 
		   + '  ' + ISNULL(JOB_LOG.JOB_DESC, '') + ' / ' + ISNULL(JOB_COMPONENT.JOB_COMP_DESC, '') AS 
		   GRP_COMPONENT,
		   ALERT.TASK_SEQ_NBR,
		   JOB_TRAFFIC_DET.FNC_CODE AS TASK_FNC_CODE,
		   JOB_TRAFFIC_DET.TASK_DESCRIPTION AS TASK_FNC_DESC,
		   ALERT.ESTIMATE_NUMBER,
		   ALERT.EST_COMPONENT_NBR,
		   RIGHT(
			   REPLICATE('0', 6) + CONVERT(VARCHAR(20), ALERT.ESTIMATE_NUMBER),
			   6
		   ) + '-' + ISNULL(ESTIMATE_LOG.EST_LOG_DESC, '') AS GRP_ESTIMATE,
		   RIGHT(
			   REPLICATE('0', 6) + CONVERT(VARCHAR(20), ALERT.ESTIMATE_NUMBER),
			   6
		   ) + '-' + RIGHT(
			   REPLICATE('0', 2) + CONVERT(VARCHAR(20), ALERT.EST_COMPONENT_NBR),
			   2
		   ) + '  ' + ISNULL(ESTIMATE_LOG.EST_LOG_DESC, '') + ' / ' + ISNULL(ESTIMATE_COMPONENT.EST_COMP_DESC, '') AS 
		   GRP_ESTIMATE_COMPONENT,
		   CASE 
				WHEN JOB_TRAFFIC_DET.FNC_CODE IS NULL OR
					 JOB_TRAFFIC_DET.FNC_CODE = '' THEN JOB_TRAFFIC_DET.TASK_DESCRIPTION
				ELSE JOB_TRAFFIC_DET.FNC_CODE + '-' + JOB_TRAFFIC_DET.TASK_DESCRIPTION
		   END AS GRP_TASK,
		   ISNULL(CAMPAIGN.CMP_CODE + ' - ', '') + ISNULL(CAMPAIGN.CMP_NAME, '') AS 
		   GRP_CAMPAIGN,
		   CAMPAIGN.CMP_IDENTIFIER,
		   ALERT.VER AS VERSION_ID,
		   ALERT.BUILD AS BUILD_ID,
		   SOFTWARE_VERSION.VERSION AS VER,
		   SOFTWARE_BUILD.BUILD AS BUILD,
		   ALERT.ALERT_SEQ_NBR,
		   CASE 
				WHEN ALERT.PRIORITY = 1 THEN 'High'
				WHEN ALERT.PRIORITY = 2 THEN 'Medium'
				WHEN ALERT.PRIORITY = 3 THEN 'Low'
				ELSE 'Medium'
		   END AS GRP_PRIORITY,
		   dbo.udf_format_date_time(ALERT.DUE_DATE, 'YYYY-MM-DD') AS GRP_DUE_DATE,
		   dbo.udf_format_date_time(ALERT.DUE_DATE, 'LONGDATE') AS 
		   GRP_DUE_DATE_DISPLAY,
		   COALESCE(ALERT.ALERT_SEQ_NBR, ALERT.ALERT_ID) AS ID,
		   ALERT.SENT,
		   ALERT_NOTIFY_HDR.ALRT_NOTIFY_HDR_ID,
		   ALERT_NOTIFY_HDR.ALERT_NOTIFY_NAME,
		   '' AS VN_NAME
	FROM   ALERT WITH (NOLOCK)
		   INNER JOIN ALERT_TYPE WITH (NOLOCK)
				ON  ALERT.ALERT_TYPE_ID = ALERT_TYPE.ALERT_TYPE_ID
		   INNER JOIN ALERT_CATEGORY WITH (NOLOCK)
				ON  ALERT.ALERT_CAT_ID = ALERT_CATEGORY.ALERT_CAT_ID
		   LEFT OUTER JOIN EMPLOYEE WITH (NOLOCK)
		   INNER JOIN ALERT_RCPT WITH (NOLOCK)
				ON  EMPLOYEE.EMP_CODE = ALERT_RCPT.EMP_CODE
				ON  ALERT.ALERT_ID = ALERT_RCPT.ALERT_ID
		   LEFT OUTER JOIN ALERT_NOTIFY_HDR WITH (NOLOCK)
				ON  ALERT.ALRT_NOTIFY_HDR_ID = ALERT_NOTIFY_HDR.ALRT_NOTIFY_HDR_ID
		   LEFT OUTER JOIN ESTIMATE_COMPONENT WITH (NOLOCK)
				ON  ALERT.ESTIMATE_NUMBER = ESTIMATE_COMPONENT.ESTIMATE_NUMBER
				AND ALERT.EST_COMPONENT_NBR = ESTIMATE_COMPONENT.EST_COMPONENT_NBR
		   LEFT OUTER JOIN ESTIMATE_LOG WITH (NOLOCK)
				ON  ALERT.ESTIMATE_NUMBER = ESTIMATE_LOG.ESTIMATE_NUMBER
		   LEFT OUTER JOIN JOB_COMPONENT WITH (NOLOCK)
				ON  ALERT.JOB_NUMBER = JOB_COMPONENT.JOB_NUMBER
				AND ALERT.JOB_COMPONENT_NBR = JOB_COMPONENT.JOB_COMPONENT_NBR
		   LEFT OUTER JOIN JOB_TRAFFIC_DET WITH (NOLOCK)
				ON  ALERT.JOB_NUMBER = JOB_TRAFFIC_DET.JOB_NUMBER
				AND ALERT.JOB_COMPONENT_NBR = JOB_TRAFFIC_DET.JOB_COMPONENT_NBR
				AND ALERT.TASK_SEQ_NBR = JOB_TRAFFIC_DET.SEQ_NBR
		   LEFT OUTER JOIN ALERT_STATES WITH (NOLOCK)
				ON  ALERT.ALERT_STATE_ID = ALERT_STATES.ALERT_STATE_ID
		   LEFT OUTER JOIN JOB_LOG WITH (NOLOCK)
				ON  ALERT.JOB_NUMBER = JOB_LOG.JOB_NUMBER
		   LEFT OUTER JOIN OFFICE WITH (NOLOCK)
				ON  ALERT.OFFICE_CODE = OFFICE.OFFICE_CODE
		   LEFT OUTER JOIN PRODUCT WITH (NOLOCK)
				ON  ALERT.CL_CODE = PRODUCT.CL_CODE
				AND ALERT.DIV_CODE = PRODUCT.DIV_CODE
				AND ALERT.PRD_CODE = PRODUCT.PRD_CODE
		   LEFT OUTER JOIN CAMPAIGN WITH (NOLOCK)
				ON  ALERT.CMP_IDENTIFIER = CAMPAIGN.CMP_IDENTIFIER
		   LEFT OUTER JOIN DIVISION WITH (NOLOCK)
				ON  ALERT.CL_CODE = DIVISION.CL_CODE
				AND ALERT.DIV_CODE = DIVISION.DIV_CODE
		   LEFT OUTER JOIN CLIENT WITH (NOLOCK)
				ON  ALERT.CL_CODE = CLIENT.CL_CODE
		   LEFT OUTER JOIN SOFTWARE_VERSION WITH (NOLOCK)
			    ON ALERT.VER = SOFTWARE_VERSION.VERSION_ID
           LEFT OUTER JOIN SOFTWARE_BUILD WITH (NOLOCK)
		        ON ALERT.BUILD = SOFTWARE_BUILD.BUILD_ID
	WHERE  ((EMPLOYEE.EMP_TERM_DATE IS NULL) OR (EMPLOYEE.EMP_TERM_DATE > GETDATE()))
