CREATE FUNCTION [dbo].[advtf_nielsen_radio_audience_spot_radio_research_aqh](
	@NIELSEN_RADIO_SEGMENT_PARENT_IDs varchar(max),
	@NIELSEN_RADIO_DAYPART_IDs varchar(max),
	@LISTENING_LOCATION char(1),
	@NIELSEN_RADIO_STATION_COMBO_IDs varchar(max),
	@MEDIA_SPOT_RADIO_RESEARCH_DAYPART_TYPE dbo.MEDIA_SPOT_RADIO_RESEARCH_DAYPART_TYPE READONLY,
	@NIELSEN_RADIO_QUALITATIVE_ID int
)
RETURNS
@RETURN_TABLE TABLE (
	NIELSEN_DEMO_CODE varchar(7) NOT NULL,
	AQH bigint NOT NULL,
	NIELSEN_RADIO_SEGMENT_PARENT_ID int NOT NULL,
	NIELSEN_RADIO_SEGMENT_CHILD_ID int NOT NULL,
	NIELSEN_RADIO_STATION_COMBO_ID int NOT NULL,
	NIELSEN_RADIO_DAYPART_ID int NOT NULL,
	MEDIA_SPOT_RADIO_RESEARCH_DAYPART_ID int NOT NULL
)
WITH SCHEMABINDING
AS
BEGIN
	INSERT INTO @RETURN_TABLE
	SELECT
		u.nielsen_demo_code, u.aqh, NIELSEN_RADIO_SEGMENT_PARENT_ID, NIELSEN_RADIO_SEGMENT_CHILD_ID, NIELSEN_RADIO_STATION_COMBO_ID, NIELSEN_RADIO_DAYPART_ID, ID
	FROM (
		SELECT
			[251] = a.[MALES_6TO11_AQH],
			[42] = MALES_12TO17_AQH + MALES_18TO20_AQH + MALES_18TO24_AQH + MALES_21TO24_AQH + MALES_25TO34_AQH + MALES_35TO44_AQH + MALES_35TO49_AQH + MALES_45TO49_AQH + MALES_50TO54_AQH + MALES_55TO64_AQH + MALES_65PLUS_AQH + 
				   FEMALES_12TO17_AQH + FEMALES_18TO20_AQH + FEMALES_18TO24_AQH + FEMALES_21TO24_AQH + FEMALES_25TO34_AQH + FEMALES_35TO44_AQH + FEMALES_35TO49_AQH + FEMALES_45TO49_AQH + FEMALES_50TO54_AQH + FEMALES_55TO64_AQH + FEMALES_65PLUS_AQH,
			[1] = a.[MALES_12TO17_AQH],
			[264] = a.[FEMALES_6TO11_AQH],
			[8] = a.[FEMALES_12TO17_AQH],
			[254] = a.[MALES_18TO20_AQH] + CASE WHEN @NIELSEN_RADIO_QUALITATIVE_ID > 1 THEN a.MALES_18TO24_AQH ELSE 0 END,
			[255] = a.[MALES_21TO24_AQH],
			[3] = a.[MALES_25TO34_AQH],
			[4] = a.[MALES_35TO44_AQH] + CASE WHEN @NIELSEN_RADIO_QUALITATIVE_ID > 1 THEN a.MALES_35TO49_AQH ELSE 0 END,
			[5] = a.[MALES_45TO49_AQH],
			[6] = a.[MALES_50TO54_AQH],
			[82] = a.[MALES_55TO64_AQH],
			[7] = a.[MALES_65PLUS_AQH],
			[267] = a.[FEMALES_18TO20_AQH] + CASE WHEN @NIELSEN_RADIO_QUALITATIVE_ID > 1 THEN a.FEMALES_18TO24_AQH ELSE 0 END,
			[268] = a.[FEMALES_21TO24_AQH],
			[10] = a.[FEMALES_25TO34_AQH],
			[11] = a.[FEMALES_35TO44_AQH] + CASE WHEN @NIELSEN_RADIO_QUALITATIVE_ID > 1 THEN a.FEMALES_35TO49_AQH ELSE 0 END,
			[12] = a.[FEMALES_45TO49_AQH],
			[13] = a.[FEMALES_50TO54_AQH],
			[14] = a.[FEMALES_55TO64_AQH],
			[15] = a.[FEMALES_65PLUS_AQH],
			a.NIELSEN_RADIO_SEGMENT_PARENT_ID,
			a.NIELSEN_RADIO_SEGMENT_CHILD_ID,
			c.NIELSEN_RADIO_STATION_COMBO_ID,
			c.NIELSEN_RADIO_DAYPART_ID,
			dpt.ID
		FROM dbo.NIELSEN_RADIO_AUDIENCE a
			INNER JOIN dbo.NIELSEN_RADIO_SEGMENT_CHILD c ON a.NIELSEN_RADIO_SEGMENT_CHILD_ID = c.NIELSEN_RADIO_SEGMENT_CHILD_ID
														AND c.NIELSEN_RADIO_DAYPART_ID IN (SELECT items FROM dbo.udf_split_list(@NIELSEN_RADIO_DAYPART_IDs, ','))
														AND c.LISTENING_LOCATION = @LISTENING_LOCATION
														AND c.NIELSEN_RADIO_STATION_COMBO_ID IN (SELECT items FROM dbo.udf_split_list(@NIELSEN_RADIO_STATION_COMBO_IDs, ','))
			INNER JOIN @MEDIA_SPOT_RADIO_RESEARCH_DAYPART_TYPE dpt ON c.NIELSEN_RADIO_DAYPART_ID = dpt.NielsenRadioDaypartID
		WHERE a.NIELSEN_RADIO_SEGMENT_PARENT_ID IN (SELECT items FROM dbo.udf_split_list(@NIELSEN_RADIO_SEGMENT_PARENT_IDs, ','))
	) AUDDATA
	UNPIVOT
	(
		aqh
		for nielsen_demo_code in ([251], [42], [1], [264], [8], [254], [255], [3], [4], [5], [6], [82], [7], [267], [268], [10], [11], [12], [13], [14], [15]) 
	) u
	RETURN
END
GO

GRANT SELECT ON [advtf_nielsen_radio_audience_spot_radio_research_aqh] TO PUBLIC
GO