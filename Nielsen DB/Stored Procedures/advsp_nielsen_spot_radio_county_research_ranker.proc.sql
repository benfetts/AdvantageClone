CREATE PROCEDURE [dbo].[advsp_nielsen_spot_radio_county_research_ranker]
	@COUNTY_CODE int,
	@MEDIA_DEMO_CODE varchar(10),
	@MEDIA_DEMO_DESC varchar(50),
	@DAYPART_68 bit,
	@DAYPART_84 bit,
	@CALL_LETTERS_BAND varchar(MAX),
	@MEDIA_SPOT_RADIO_COUNTY_RESEARCH_YEAR_TYPE [dbo].[MEDIA_SPOT_RADIO_COUNTY_RESEARCH_YEAR_TYPE] READONLY,
	@DEBUG bit = 0
AS
BEGIN
	DECLARE @SHOW_FOOTNOTE bit,
			@Years varchar(200),
			@Population varchar(200),
			@Intab varchar(200),
			@BOOK_NAME varchar(max),
			@ROW_COUNT int,
			@ROW_ID int

	DECLARE @BOOKS TABLE (
		[ID] int NOT NULL,
		[NIELSEN_RADIO_COUNTY_PERIOD_ID] int NOT NULL
	)
	
	DECLARE @BOOK_NAMES TABLE (
		[ID] int IDENTITY(1,1) NOT NULL,
		[BOOK_ID] int NOT NULL,
		[NAME] varchar(max) NULL
	)
	
	INSERT INTO @BOOKS (ID, NIELSEN_RADIO_COUNTY_PERIOD_ID)
	SELECT u.ID, u.NIELSEN_RADIO_COUNTY_PERIOD_ID
	FROM (
		SELECT ID,
				Year1 = (SELECT NIELSEN_RADIO_COUNTY_PERIOD_ID FROM dbo.NIELSEN_RADIO_COUNTY_PERIOD WHERE COUNTY_CODE = @COUNTY_CODE AND [YEAR] = Year1), 
				Year2 = (SELECT NIELSEN_RADIO_COUNTY_PERIOD_ID FROM dbo.NIELSEN_RADIO_COUNTY_PERIOD WHERE COUNTY_CODE = @COUNTY_CODE AND [YEAR] = Year2),
				Year3 = (SELECT NIELSEN_RADIO_COUNTY_PERIOD_ID FROM dbo.NIELSEN_RADIO_COUNTY_PERIOD WHERE COUNTY_CODE = @COUNTY_CODE AND [YEAR] = Year3),
				Year4 = (SELECT NIELSEN_RADIO_COUNTY_PERIOD_ID FROM dbo.NIELSEN_RADIO_COUNTY_PERIOD WHERE COUNTY_CODE = @COUNTY_CODE AND [YEAR] = Year4),
				Year5 = (SELECT NIELSEN_RADIO_COUNTY_PERIOD_ID FROM dbo.NIELSEN_RADIO_COUNTY_PERIOD WHERE COUNTY_CODE = @COUNTY_CODE AND [YEAR] = Year5)
		FROM @MEDIA_SPOT_RADIO_COUNTY_RESEARCH_YEAR_TYPE
		) t
		UNPIVOT
		(
			NIELSEN_RADIO_COUNTY_PERIOD_ID
			FOR [YEAR] IN (Year1, Year2, Year3, Year4, Year5)
		) u
	
	IF EXISTS(
				SELECT 1
				FROM @BOOKS b
					INNER JOIN dbo.NIELSEN_RADIO_COUNTY_PERIOD p ON b.NIELSEN_RADIO_COUNTY_PERIOD_ID = p.NIELSEN_RADIO_COUNTY_PERIOD_ID
				WHERE p.MEASUREMENT_TYPE = 'P'
			 )
		SET @SHOW_FOOTNOTE = 1
	ELSE
		SET @SHOW_FOOTNOTE = 0
				
	INSERT INTO @BOOK_NAMES (BOOK_ID)
	SELECT DISTINCT ID FROM @BOOKS
	
	SELECT @ROW_COUNT = COUNT(1) FROM @BOOK_NAMES
	SET @ROW_ID = 1

	WHILE @ROW_ID <= @ROW_COUNT BEGIN

		SET @BOOK_NAME = NULL

		SELECT @BOOK_NAME = COALESCE(@BOOK_NAME + ', ', '') + CAST(nrcp.[YEAR] as varchar)
		FROM @BOOKS b
			INNER JOIN dbo.NIELSEN_RADIO_COUNTY_PERIOD nrcp ON b.NIELSEN_RADIO_COUNTY_PERIOD_ID = nrcp.NIELSEN_RADIO_COUNTY_PERIOD_ID
		WHERE b.ID = (SELECT BOOK_ID FROM @BOOK_NAMES WHERE ID = @ROW_ID)
		ORDER BY nrcp.[YEAR]

		UPDATE @BOOK_NAMES SET [NAME] = @BOOK_NAME
		WHERE ID = @ROW_ID

		SET @ROW_ID = @ROW_ID + 1
	END

	SELECT @Years = COALESCE(@Years + ', ', '') + CAST([YEAR] as varchar)
	FROM dbo.NIELSEN_RADIO_COUNTY_PERIOD
	WHERE NIELSEN_RADIO_COUNTY_PERIOD_ID IN (SELECT NIELSEN_RADIO_COUNTY_PERIOD_ID FROM @BOOKS)
	ORDER BY [YEAR]
	
	SELECT @Population = COALESCE(@Population + '; ', '') + CAST(
			CASE @MEDIA_DEMO_CODE 
				WHEN 'RBGMF12' THEN REPLACE(CONVERT(varchar, CAST(NRCU.PERSONS_12PLUS_UE AS money), 1), '.00','')
				WHEN 'RBGMF1234' THEN REPLACE(CONVERT(varchar, CAST(NRCU.PERSONS_12TO34_UE AS money), 1), '.00','')
				WHEN 'RMF18' THEN REPLACE(CONVERT(varchar, CAST(NRCU.PERSONS_18PLUS_UE AS money), 1), '.00','')
				WHEN 'RMF1834' THEN REPLACE(CONVERT(varchar, CAST(NRCU.PERSONS_18TO34_UE AS money), 1), '.00','')
				WHEN 'RMF2554' THEN REPLACE(CONVERT(varchar, CAST(NRCU.PERSONS_25TO54_UE AS money), 1), '.00','')
				WHEN 'RMF35' THEN REPLACE(CONVERT(varchar, CAST(NRCU.PERSONS_35PLUS_UE AS money), 1), '.00','')
				WHEN 'RMF3564' THEN REPLACE(CONVERT(varchar, CAST(NRCU.PERSONS_35TO64_UE AS money), 1), '.00','')
				WHEN 'RM18' THEN REPLACE(CONVERT(varchar, CAST(NRCU.MALES_18PLUS_UE AS money), 1), '.00','')
				WHEN 'RF18' THEN REPLACE(CONVERT(varchar, CAST(NRCU.FEMALES_18PLUS_UE  AS money), 1), '.00','')
			END as varchar)
	FROM dbo.NIELSEN_RADIO_COUNTY_UNIVERSE NRCU
		INNER JOIN dbo.NIELSEN_RADIO_COUNTY_PERIOD NRCP ON NRCP.NIELSEN_RADIO_COUNTY_PERIOD_ID = NRCU.NIELSEN_RADIO_COUNTY_PERIOD_ID
	WHERE NRCP.NIELSEN_RADIO_COUNTY_PERIOD_ID IN (SELECT NIELSEN_RADIO_COUNTY_PERIOD_ID FROM @BOOKS)
	ORDER BY NRCP.[YEAR]
	
	SELECT @Intab = COALESCE(@Intab + '; ', '') + CAST(
			CASE @MEDIA_DEMO_CODE 
				WHEN 'RBGMF12' THEN REPLACE(CONVERT(varchar, CAST(NRCI.PERSONS_12PLUS_INTAB AS money), 1), '.00','')
				WHEN 'RBGMF1234' THEN REPLACE(CONVERT(varchar, CAST(NRCI.PERSONS_12TO34_INTAB AS money), 1), '.00','')
				WHEN 'RMF18' THEN REPLACE(CONVERT(varchar, CAST(NRCI.PERSONS_18PLUS_INTAB AS money), 1), '.00','')
				WHEN 'RMF1834' THEN REPLACE(CONVERT(varchar, CAST(NRCI.PERSONS_18TO34_INTAB AS money), 1), '.00','')
				WHEN 'RMF2554' THEN REPLACE(CONVERT(varchar, CAST(NRCI.PERSONS_25TO54_INTAB AS money), 1), '.00','')
				WHEN 'RMF35' THEN REPLACE(CONVERT(varchar, CAST(NRCI.PERSONS_35PLUS_INTAB AS money), 1), '.00','')
				WHEN 'RMF3564' THEN REPLACE(CONVERT(varchar, CAST(NRCI.PERSONS_35TO64_INTAB AS money), 1), '.00','')
				WHEN 'RM18' THEN REPLACE(CONVERT(varchar, CAST(NRCI.MALES_18PLUS_INTAB AS money), 1), '.00','')
				WHEN 'RF18' THEN REPLACE(CONVERT(varchar, CAST(NRCI.FEMALES_18PLUS_INTAB AS money), 1), '.00','')
			END as varchar)
	FROM dbo.NIELSEN_RADIO_COUNTY_INTAB NRCI
		INNER JOIN dbo.NIELSEN_RADIO_COUNTY_PERIOD NRCP ON NRCP.NIELSEN_RADIO_COUNTY_PERIOD_ID = NRCI.NIELSEN_RADIO_COUNTY_PERIOD_ID
	WHERE NRCP.NIELSEN_RADIO_COUNTY_PERIOD_ID IN (SELECT NIELSEN_RADIO_COUNTY_PERIOD_ID FROM @BOOKS)
	ORDER BY NRCP.[YEAR]
	
	SELECT
		AQH = CAST(ROUND(CASE WHEN SUM(dtl.[Population]) = 0 THEN 0 
			ELSE SUM(dtl.AQH * dtl.[Population]) / SUM(dtl.[Population]) / 100.00 END, 0) as int),
		AQHRating = CASE WHEN SUM(dtl.[Population]) = 0 THEN 0 
			ELSE SUM(dtl.AQHRating * dtl.[Population]) / SUM(dtl.[Population]) END,
		StationShareofCountyPercent = CASE WHEN SUM(dtl.[Population]) = 0 THEN 0 
			ELSE SUM(dtl.StationShareofCountyPercent * dtl.[Population]) / SUM(dtl.[Population]) END,
		CountyShareofStationPercent = CASE WHEN SUM(dtl.[Population]) = 0 THEN 0 
			ELSE SUM(dtl.CountyShareofStationPercent * dtl.[Population]) / SUM(dtl.[Population]) END,
		Cume = CAST(CASE WHEN SUM(dtl.[Population]) = 0 THEN 0 
			ELSE SUM(dtl.Cume * dtl.[Population]) / SUM(dtl.[Population]) / 100 END as int),
		CumeRating = CASE WHEN SUM(dtl.[Population]) = 0 THEN 0 
			ELSE SUM(dtl.CumeRating * dtl.[Population]) / SUM(dtl.[Population]) END,
		dtl.StationName,
		dtl.StationFrequency,
		dtl.Daypart, 
		dtl.NielsenRadioDaypartID,
		dtl.[CountyName],
		dtl.[Demographic],
		dtl.[ShowFootNote],
		[Years] = @Years,
		[Population] = @Population,
		[Intab] = @Intab,
		[BookCount] = CAST( (SELECT COALESCE(COUNT(1), 0) FROM @BOOKS) as int),
		[IntabFlag] = CAST(CASE WHEN MIN(Intab) < 30 THEN 1 ELSE 0 END as bit),
		dtl.[State],
		--trend
		TrendBookOrder = CAST(
						   (SELECT TOP 1 nrcp.[YEAR]
							FROM dbo.NIELSEN_RADIO_COUNTY_PERIOD nrcp
								INNER JOIN @BOOKS b ON nrcp.NIELSEN_RADIO_COUNTY_PERIOD_ID = b.NIELSEN_RADIO_COUNTY_PERIOD_ID
							WHERE b.ID = dtl.[BookID]
							ORDER BY 1) as int),
		dtl.[BookID],
		[Books] = (SELECT [NAME] FROM @BOOK_NAMES WHERE BOOK_ID = dtl.[BookID])
	FROM (
		SELECT
			AQH = CASE @MEDIA_DEMO_CODE 
				WHEN 'RBGMF12' THEN NRCA.PERSONS_12PLUS_AQH
				WHEN 'RBGMF1234' THEN NRCA.PERSONS_12TO34_AQH
				WHEN 'RMF18' THEN NRCA.PERSONS_18PLUS_AQH
				WHEN 'RMF1834' THEN NRCA.PERSONS_18TO34_AQH
				WHEN 'RMF2554' THEN NRCA.PERSONS_25TO54_AQH
				WHEN 'RMF35' THEN NRCA.PERSONS_35PLUS_AQH
				WHEN 'RMF3564' THEN NRCA.PERSONS_35TO64_AQH
				WHEN 'RM18' THEN NRCA.MALES_18PLUS_AQH
				WHEN 'RF18' THEN NRCA.FEMALES_18PLUS_AQH
			END,

			AQHRating = CASE @MEDIA_DEMO_CODE 
				WHEN 'RBGMF12' THEN NRCA.PERSONS_12PLUS_RATING
				WHEN 'RBGMF1234' THEN NRCA.PERSONS_12TO34_RATING
				WHEN 'RMF18' THEN NRCA.PERSONS_18PLUS_RATING
				WHEN 'RMF1834' THEN NRCA.PERSONS_18TO34_RATING
				WHEN 'RMF2554' THEN NRCA.PERSONS_25TO54_RATING
				WHEN 'RMF35' THEN NRCA.PERSONS_35PLUS_RATING
				WHEN 'RMF3564' THEN NRCA.PERSONS_35TO64_RATING
				WHEN 'RM18' THEN NRCA.MALES_18PLUS_RATING
				WHEN 'RF18' THEN NRCA.FEMALES_18PLUS_RATING
			END,
		
			StationShareofCountyPercent = CASE @MEDIA_DEMO_CODE 
				WHEN 'RBGMF12' THEN NRCA.PERSONS_12PLUS_SSOC
				WHEN 'RBGMF1234' THEN NRCA.PERSONS_12TO34_SSOC
				WHEN 'RMF18' THEN NRCA.PERSONS_18PLUS_SSOC
				WHEN 'RMF1834' THEN NRCA.PERSONS_18TO34_SSOC
				WHEN 'RMF2554' THEN NRCA.PERSONS_25TO54_SSOC
				WHEN 'RMF35' THEN NRCA.PERSONS_35PLUS_SSOC
				WHEN 'RMF3564' THEN NRCA.PERSONS_35TO64_SSOC
				WHEN 'RM18' THEN NRCA.MALES_18PLUS_SSOC
				WHEN 'RF18' THEN NRCA.FEMALES_18PLUS_SSOC
			END,
		
			CountyShareofStationPercent = CASE @MEDIA_DEMO_CODE 
				WHEN 'RBGMF12' THEN NRCA.PERSONS_12PLUS_CSOS
				WHEN 'RBGMF1234' THEN NRCA.PERSONS_12TO34_CSOS
				WHEN 'RMF18' THEN NRCA.PERSONS_18PLUS_CSOS
				WHEN 'RMF1834' THEN NRCA.PERSONS_18TO34_CSOS
				WHEN 'RMF2554' THEN NRCA.PERSONS_25TO54_CSOS
				WHEN 'RMF35' THEN NRCA.PERSONS_35PLUS_CSOS
				WHEN 'RMF3564' THEN NRCA.PERSONS_35TO64_CSOS
				WHEN 'RM18' THEN NRCA.MALES_18PLUS_CSOS
				WHEN 'RF18' THEN NRCA.FEMALES_18PLUS_CSOS
			END,
		
			Cume = CASE @MEDIA_DEMO_CODE 
				WHEN 'RBGMF12' THEN NRCA.PERSONS_12PLUS_CUME
				WHEN 'RBGMF1234' THEN NRCA.PERSONS_12TO34_CUME
				WHEN 'RMF18' THEN NRCA.PERSONS_18PLUS_CUME
				WHEN 'RMF1834' THEN NRCA.PERSONS_18TO34_CUME
				WHEN 'RMF2554' THEN NRCA.PERSONS_25TO54_CUME
				WHEN 'RMF35' THEN NRCA.PERSONS_35PLUS_CUME
				WHEN 'RMF3564' THEN NRCA.PERSONS_35TO64_CUME
				WHEN 'RM18' THEN NRCA.MALES_18PLUS_CUME
				WHEN 'RF18' THEN NRCA.FEMALES_18PLUS_CUME
			END,
		
			CumeRating = CASE @MEDIA_DEMO_CODE 
				WHEN 'RBGMF12' THEN NRCA.PERSONS_12PLUS_CUME_RATING
				WHEN 'RBGMF1234' THEN NRCA.PERSONS_12TO34_CUME_RATING
				WHEN 'RMF18' THEN NRCA.PERSONS_18PLUS_CUME_RATING
				WHEN 'RMF1834' THEN NRCA.PERSONS_18TO34_CUME_RATING
				WHEN 'RMF2554' THEN NRCA.PERSONS_25TO54_CUME_RATING
				WHEN 'RMF35' THEN NRCA.PERSONS_35PLUS_CUME_RATING
				WHEN 'RMF3564' THEN NRCA.PERSONS_35TO64_CUME_RATING
				WHEN 'RM18' THEN NRCA.MALES_18PLUS_CUME_RATING
				WHEN 'RF18' THEN NRCA.FEMALES_18PLUS_CUME_RATING
			END,
		
			[Population] = CAST(CASE @MEDIA_DEMO_CODE 
				WHEN 'RBGMF12' THEN NRCU.PERSONS_12PLUS_UE
				WHEN 'RBGMF1234' THEN NRCU.PERSONS_12TO34_UE
				WHEN 'RMF18' THEN NRCU.PERSONS_18PLUS_UE
				WHEN 'RMF1834' THEN NRCU.PERSONS_18TO34_UE
				WHEN 'RMF2554' THEN NRCU.PERSONS_25TO54_UE
				WHEN 'RMF35' THEN NRCU.PERSONS_35PLUS_UE
				WHEN 'RMF3564' THEN NRCU.PERSONS_35TO64_UE
				WHEN 'RM18' THEN NRCU.MALES_18PLUS_UE
				WHEN 'RF18' THEN NRCU.FEMALES_18PLUS_UE 
			END as decimal),
		
			[Intab] = CASE @MEDIA_DEMO_CODE 
				WHEN 'RBGMF12' THEN NRCI.PERSONS_12PLUS_INTAB
				WHEN 'RBGMF1234' THEN NRCI.PERSONS_12TO34_INTAB
				WHEN 'RMF18' THEN NRCI.PERSONS_18PLUS_INTAB
				WHEN 'RMF1834' THEN NRCI.PERSONS_18TO34_INTAB
				WHEN 'RMF2554' THEN NRCI.PERSONS_25TO54_INTAB
				WHEN 'RMF35' THEN NRCI.PERSONS_35PLUS_INTAB
				WHEN 'RMF3564' THEN NRCI.PERSONS_35TO64_INTAB
				WHEN 'RM18' THEN NRCI.MALES_18PLUS_INTAB
				WHEN 'RF18' THEN NRCI.FEMALES_18PLUS_INTAB
			END,

			[StationName] = NRCS.CALL_LETTERS + '-' + NRCS.BAND,
			[StationFrequency] = NRCS.FREQUENCY,
			[Daypart] = CASE WHEN NRCA.NIELSEN_RADIO_DAYPART_ID = 68 THEN 'Mon-Fri / 6AM - 7PM'
							 WHEN NRCA.NIELSEN_RADIO_DAYPART_ID = 84 THEN 'Mon-Sun / 6AM - 12AM' END,
			[NielsenRadioDaypartID] = NRCA.NIELSEN_RADIO_DAYPART_ID,
			[CountyName] = NRCP.[NAME],
			[Year] = NRCP.[YEAR],
			[Demographic] = @MEDIA_DEMO_DESC,
			[ShowFootNote] = @SHOW_FOOTNOTE,
			[State] = NRCP.[STATE],
			[BookID] = B.ID
		FROM dbo.NIELSEN_RADIO_COUNTY_AUDIENCE NRCA 
			INNER JOIN dbo.NIELSEN_RADIO_COUNTY_UNIVERSE NRCU ON NRCA.NIELSEN_RADIO_COUNTY_PERIOD_ID = NRCU.NIELSEN_RADIO_COUNTY_PERIOD_ID 
			INNER JOIN dbo.NIELSEN_RADIO_COUNTY_PERIOD NRCP ON NRCA.NIELSEN_RADIO_COUNTY_PERIOD_ID = NRCP.NIELSEN_RADIO_COUNTY_PERIOD_ID 
			INNER JOIN dbo.NIELSEN_RADIO_COUNTY_STATION NRCS ON NRCA.NIELSEN_RADIO_COUNTY_STATION_ID = NRCS.NIELSEN_RADIO_COUNTY_STATION_ID 
			INNER JOIN dbo.NIELSEN_RADIO_COUNTY_INTAB NRCI ON NRCA.NIELSEN_RADIO_COUNTY_PERIOD_ID = NRCI.NIELSEN_RADIO_COUNTY_PERIOD_ID 
			INNER JOIN @BOOKS B ON NRCP.NIELSEN_RADIO_COUNTY_PERIOD_ID = B.NIELSEN_RADIO_COUNTY_PERIOD_ID
		WHERE
			NRCP.COUNTY_CODE = @COUNTY_CODE
		AND 
			( (NRCA.NIELSEN_RADIO_DAYPART_ID = 68 AND @DAYPART_68 = 1) OR (NRCA.NIELSEN_RADIO_DAYPART_ID = 84 AND @DAYPART_84 = 1))
		AND NRCS.CALL_LETTERS + '|' + NRCS.BAND IN (SELECT * FROM [dbo].[udf_split_list](@CALL_LETTERS_BAND, ','))
	) dtl
	GROUP BY
		dtl.StationName,
		dtl.StationFrequency,
		dtl.Daypart, 
		dtl.NielsenRadioDaypartID,
		dtl.[CountyName],
		dtl.[Demographic],
		dtl.[ShowFootNote],
		dtl.[State],
		dtl.[BookID]
END

GO
