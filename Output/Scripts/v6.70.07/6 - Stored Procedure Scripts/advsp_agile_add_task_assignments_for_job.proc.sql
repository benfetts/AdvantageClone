IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'[dbo].[advsp_agile_add_task_assignments_for_job]') AND OBJECTPROPERTY(id, N'IsProcedure') = 1)
    DROP PROCEDURE [dbo].[advsp_agile_add_task_assignments_for_job]
GO
CREATE PROCEDURE [dbo].[advsp_agile_add_task_assignments_for_job] /*WITH ENCRYPTION*/
@JOB_NUMBER INT,
@JOB_COMPONENT_NBR SMALLINT,
@USER_CODE VARCHAR(100)
AS
/*=========== QUERY ===========*/
IF @JOB_NUMBER IS NOT NULL AND @JOB_COMPONENT_NBR IS NOT NULL
BEGIN
	--  VARIABLES
	BEGIN
		DECLARE @RECS TABLE (ID INT IDENTITY, SEQ_NBR SMALLINT);
		DECLARE @ALERTS TABLE (ID INT IDENTITY, ALERT_ID INT);
		DECLARE 
			@REC_COUNT SMALLINT,
			@CTR SMALLINT,
			@SEQ_NBR SMALLINT;
	END
	--  INIT
	BEGIN
		SET @REC_COUNT = 0;
		SET @CTR = 0
	END
	--  GET ONLY TASKS MISSING BRD/CAT=71 ALERT RECORDS
	BEGIN
		INSERT INTO @RECS(SEQ_NBR)
		SELECT
			J.SEQ_NBR
		FROM
			ALERT A WITH(NOLOCK)
			RIGHT OUTER JOIN JOB_TRAFFIC_DET J WITH(NOLOCK) ON A.JOB_NUMBER = J.JOB_NUMBER 
				AND A.JOB_COMPONENT_NBR = J.JOB_COMPONENT_NBR
				AND A.TASK_SEQ_NBR = J.SEQ_NBR
		WHERE
			J.JOB_NUMBER = @JOB_NUMBER 
			AND J.JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR
			AND
				1 = CASE
						WHEN A.ALERT_ID IS NULL THEN 1
						WHEN A.ALERT_ID IS NOT NULL AND A.ALERT_LEVEL = 'BRD' THEN 0
						WHEN A.ALERT_ID IS NOT NULL AND A.ALERT_LEVEL <> 'BRD' THEN 1
						ELSE 0
					END;
	END
	--	MAKE SURE BRD ASSIGNMENT EXISTS, IF NO, CREATE
	BEGIN
		SELECT @REC_COUNT = COUNT(1) FROM @RECS;
		IF @REC_COUNT IS NOT NULL AND @REC_COUNT > 0
		BEGIN

			WHILE @CTR < @REC_COUNT
			BEGIN
				SELECT @CTR = @CTR + 1, @SEQ_NBR = NULL;
				SELECT @SEQ_NBR = SEQ_NBR FROM @RECS R WHERE R.ID = @CTR;
				IF @SEQ_NBR IS NOT NULL
				BEGIN
					INSERT INTO @ALERTS (ALERT_ID)
					EXEC [dbo].[advsp_agile_add_assignment_from_task] @JOB_NUMBER, @JOB_COMPONENT_NBR, @SEQ_NBR, @USER_CODE;
				END
			END
		END
	END
	--  RETURN ALERTS CREATED
	SELECT 
		A.*
	FROM
		ALERT A WITH(NOLOCK)
		INNER JOIN @ALERTS AA ON A.ALERT_ID = AA.ALERT_ID;
END
/*=========== QUERY ===========*/
