IF EXISTS ( SELECT * FROM dbo.sysobjects WHERE id = OBJECT_ID(N'[dbo].[advsp_alert_get_current_assignees]') AND OBJECTPROPERTY(id, N'IsProcedure') = 1)
BEGIN
    DROP PROCEDURE [dbo].[advsp_alert_get_current_assignees];
END
GO
CREATE PROCEDURE [dbo].[advsp_alert_get_current_assignees] 
@ALERT_ID INT
AS
/*=========== QUERY ===========*/
BEGIN

	DECLARE 
		@IS_WORK_ITEM BIT,
		@IS_ROUTED BIT,
		@IS_TASK BIT

		SELECT
			@IS_WORK_ITEM = ISNULL(A.IS_WORK_ITEM, 0),			
			@IS_ROUTED =	CASE
								WHEN ALRT_NOTIFY_HDR_ID IS NULL AND ALERT_STATE_ID IS NULL THEN CAST(0 AS BIT)
								ELSE CAST(1 AS BIT)
							END,
			@IS_TASK =	CASE
							WHEN ALERT_LEVEL = 'BRD' OR ALERT_CAT_ID = 71 THEN CAST(1 AS BIT)
							ELSE CAST(0 AS BIT)
						END
		FROM
			ALERT A WITH(NOLOCK)
		WHERE
			ALERT_ID = @ALERT_ID;

		IF @IS_TASK = 0
		BEGIN
			IF @IS_WORK_ITEM = 1
			BEGIN
				IF @IS_ROUTED = 1
				BEGIN
					SELECT DISTINCT 
						AR.EMP_CODE 
					FROM 
						ALERT_RCPT AR WITH(NOLOCK) 
						INNER JOIN ALERT A WITH(NOLOCK) ON A.ALERT_ID = AR.ALERT_ID 
							AND A.ALRT_NOTIFY_HDR_ID = AR.ALRT_NOTIFY_HDR_ID 
							AND A.ALERT_STATE_ID = AR.ALERT_STATE_ID 
					WHERE 
						A.ALERT_ID = @ALERT_ID
						AND AR.CURRENT_NOTIFY = 1;
				END
				ELSE
				BEGIN
					SELECT DISTINCT 
						AR.EMP_CODE 
					FROM 
						ALERT_RCPT AR WITH(NOLOCK) 
					WHERE 
						AR.ALERT_ID = @ALERT_ID
						AND AR.CURRENT_NOTIFY = 1
						AND AR.ALRT_NOTIFY_HDR_ID IS NULL
						AND AR.ALERT_STATE_ID IS NULL;
				END
			END
		END
		ELSE
		BEGIN
			SELECT DISTINCT
				J.EMP_CODE
			FROM
				JOB_TRAFFIC_DET_EMPS J WITH(NOLOCK)
				INNER JOIN ALERT A WITH(NOLOCK) ON J.JOB_NUMBER = A.JOB_NUMBER AND J.JOB_COMPONENT_NBR = A.JOB_COMPONENT_NBR AND J.SEQ_NBR = A.TASK_SEQ_NBR
			WHERE
				A.ALERT_ID = @ALERT_ID;
		END

END
/*=========== QUERY ===========*/
