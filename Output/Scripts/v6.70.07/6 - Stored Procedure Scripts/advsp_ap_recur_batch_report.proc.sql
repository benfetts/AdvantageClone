IF EXISTS ( SELECT * FROM dbo.sysobjects WHERE id = OBJECT_ID( N'[dbo].[advsp_ap_recur_batch_report]') AND OBJECTPROPERTY( id, N'IsProcedure' ) = 1 )
	DROP PROCEDURE [dbo].[advsp_ap_recur_batch_report]
GO

CREATE PROCEDURE [dbo].[advsp_ap_recur_batch_report] @user_code varchar(100), @batch_date smalldatetime
AS
BEGIN
	SET NOCOUNT ON;
	
	DECLARE @AP_BATCH_DETAIL TABLE
	(
		GLTransaction INT NOT NULL,
		AccountPayableID INT NOT NULL,
		SumAmount DECIMAL(14,2) NULL,
		SumSalesTax DECIMAL(11,2) NULL,
		MAX_AP_SEQ SMALLINT NULL,
		[Status] VARCHAR(30) NULL
	)
	
	INSERT INTO @AP_BATCH_DETAIL
	SELECT
		A.GLEXACT,
		A.AP_ID,
		SUM(H.AP_INV_AMT) AS SUM_AMOUNT,
		SUM(AP_SALES_TAX) AS SUM_SALES_TAX,
		MAX(AP_SEQ) AS MAX_AP_SEQ,
		'' AS [Status]
	FROM (
		SELECT GLEXACT, AP_ID FROM [dbo].AP_GL_DIST 
		UNION 
		SELECT GLEXACT, AP_ID FROM [dbo].AP_HEADER) A
			LEFT OUTER JOIN [dbo].AP_HEADER H ON A.AP_ID = H.AP_ID AND (A.GLEXACT = H.GLEXACT OR H.GLEXACT IS NULL)
	WHERE
		A.GLEXACT IN
			(SELECT
					GLEHXACT
			 FROM
					[dbo].GLENTHDR
			 WHERE
					UPPER(GLEHUSER) = UPPER(@user_code)
			 AND	GLEHSOURCE = 'RA'
			 AND	BATCH_DATE = @batch_date)
	GROUP BY
		A.GLEXACT, A.AP_ID
			
	UPDATE @AP_BATCH_DETAIL SET [Status] = CASE WHEN MAX_AP_SEQ IS NULL THEN 'Modified - Detail Only'
												WHEN MAX_AP_SEQ = 0 THEN 'New'
												WHEN H.DELETE_FLAG = 1 THEN 'Delete'
												ELSE 'Modified'
												END
	FROM @AP_BATCH_DETAIL A
		LEFT OUTER JOIN [dbo].AP_HEADER H ON A.AccountPayableID = H.AP_ID AND (A.GLTransaction = H.GLEXACT OR H.GLEXACT IS NULL)

	UPDATE @AP_BATCH_DETAIL SET SumAmount = 0, SumSalesTax = 0 WHERE MAX_AP_SEQ IS NULL
	
	SELECT A.AccountPayableID, MAX(H.AP_SEQ) AS MAX_AP_SEQ
	INTO #UPDATE_AP_BATCH
	FROM @AP_BATCH_DETAIL A
		INNER JOIN [dbo].AP_HEADER H ON A.AccountPayableID = H.AP_ID
	WHERE
		A.MAX_AP_SEQ IS NULL
	GROUP BY A.AccountPayableID 

	UPDATE @AP_BATCH_DETAIL SET MAX_AP_SEQ = B.MAX_AP_SEQ 
	FROM @AP_BATCH_DETAIL A
		INNER JOIN #UPDATE_AP_BATCH B ON A.AccountPayableID = B.AccountPayableID 

	SELECT
		A.[Status],
		A.AccountPayableID,
		A.GLTransaction, 
		A.SumAmount, 
		A.SumSalesTax,
		H.VN_FRL_EMP_CODE + ' - ' + V.VN_NAME AS Vendor,
		H.AP_INV_VCHR + ' - ' + COALESCE(H.AP_DESC,'') AS Invoice,
		H.AP_INV_DATE AS InvoiceDate,
		H.AP_DATE_PAY AS DateToPay,
		H.OFFICE_CODE + ' - ' + O.OFFICE_NAME AS Office,
		COALESCE(H.FLAG_1099,0) AS Is1099Flag,
		COALESCE(H.PAYMENT_HOLD,0) AS PaymentHold,
		H.VT_CODE + ' - ' + VT.VT_DESCRIPTION AS Terms,
		COALESCE(H.AP_DISC_PCT,0) AS DiscountPercent,
		H.GLACODE AS GLACode,
		H.GLACODE + ' - ' + G.GLADESC AS GLAccount,
		H.POST_PERIOD AS PostPeriodCode,
		H.CURRENCY_CODE AS CurrencyCode,
		H.FOREIGN_INV_AMOUNT AS ForeignCurrencyTotal
	FROM
		@AP_BATCH_DETAIL A
			INNER JOIN [dbo].AP_HEADER H ON A.AccountPayableID = H.AP_ID AND A.MAX_AP_SEQ = H.AP_SEQ
			INNER JOIN [dbo].VENDOR V ON H.VN_FRL_EMP_CODE = V.VN_CODE 
			LEFT OUTER JOIN [dbo].OFFICE O ON H.OFFICE_CODE = O.OFFICE_CODE 
			LEFT OUTER JOIN [dbo].VTERMS VT ON H.VT_CODE = VT.VT_CODE 
			INNER JOIN [dbo].GLACCOUNT G ON H.GLACODE = G.GLACODE 
	ORDER BY A.GLTransaction  
	
END

GO
