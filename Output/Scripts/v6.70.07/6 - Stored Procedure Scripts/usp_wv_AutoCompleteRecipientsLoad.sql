IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'[dbo].[usp_wv_AutoCompleteRecipientsLoad]') AND OBJECTPROPERTY(id, N'IsProcedure') = 1)
DROP PROCEDURE [dbo].[usp_wv_AutoCompleteRecipientsLoad]
GO
CREATE PROCEDURE [dbo].[usp_wv_AutoCompleteRecipientsLoad]
@CL_CODE VARCHAR(6),
@DIV_CODE VARCHAR(6),
@PRD_CODE VARCHAR(6),
@JOB_NUMBER INT,
@JOB_COMPONENT_NBR SMALLINT,
@CMP_IDENTIFIER INT,
@CLIENT_PORTAL_USER_ID INT,
@ALERT_ID INT,
@USER_CODE AS VARCHAR(100),
@IS_REVIEWERS BIT,
@EMAIL_GR_CODE VARCHAR(50)
AS
BEGIN
/*=========== QUERY ===========*/
	DECLARE 
		@IS_CLIENT_PORTAL BIT,
		@CP_USER_CL_CODE VARCHAR(6),
		@CP_USER_AGENCY_CONTACT_CODE VARCHAR(6);
	DECLARE 
		@OfficeRestrictions AS INT,
		@EMP_CODE AS VARCHAR(6)

	SELECT @EMP_CODE = EMP_CODE FROM [dbo].[SEC_USER] WHERE UPPER([USER_CODE]) = UPPER(@USER_CODE);
	SELECT @OfficeRestrictions = COUNT(1) FROM EMP_OFFICE WHERE EMP_CODE = @EMP_CODE;

	SET @JOB_NUMBER = ISNULL(@JOB_NUMBER, 0);
	SET @JOB_COMPONENT_NBR = ISNULL(@JOB_COMPONENT_NBR, 0);
	SET @CMP_IDENTIFIER = ISNULL(@CMP_IDENTIFIER, 0);
	SET @CLIENT_PORTAL_USER_ID = ISNULL(@CLIENT_PORTAL_USER_ID, 0);
	SET @ALERT_ID = ISNULL(@ALERT_ID, 0);

	IF @ALERT_ID > 0 AND @JOB_NUMBER = 0
	BEGIN
		SELECT @JOB_NUMBER = ISNULL(JOB_NUMBER, 0) FROM ALERT WHERE ALERT_ID = @ALERT_ID;
	END

	IF @ALERT_ID > 0 AND @JOB_COMPONENT_NBR = 0
	BEGIN
		SELECT @JOB_COMPONENT_NBR = ISNULL(JOB_COMPONENT_NBR, 0) FROM ALERT WHERE ALERT_ID = @ALERT_ID;
	END

	IF @JOB_NUMBER > 0 
	BEGIN
		SELECT @CL_CODE = CL_CODE, @DIV_CODE = DIV_CODE, @PRD_CODE = PRD_CODE FROM JOB_LOG WITH(NOLOCK) WHERE JOB_NUMBER = @JOB_NUMBER;
	END

	IF RTRIM(LTRIM(@CL_CODE)) = ''
	BEGIN
		SET @CL_CODE = NULL;
	END
	IF RTRIM(LTRIM(@DIV_CODE)) = ''
	BEGIN
		SET @DIV_CODE = NULL;
	END
	IF RTRIM(LTRIM(@PRD_CODE)) = ''
	BEGIN
		SET @PRD_CODE = NULL;
	END

	IF @JOB_NUMBER > 0 AND @JOB_COMPONENT_NBR > 0 AND (@EMAIL_GR_CODE IS NULL OR @EMAIL_GR_CODE = '') AND (@IS_REVIEWERS = 0 OR @IS_REVIEWERS IS NULL)
	BEGIN
		SELECT @EMAIL_GR_CODE = EMAIL_GR_CODE FROM JOB_COMPONENT WITH(NOLOCK) WHERE JOB_NUMBER = @JOB_NUMBER AND JOB_COMPONENT_NBR = @JOB_COMPONENT_NBR;
	END
	IF @CMP_IDENTIFIER > 0
	BEGIN
		SELECT @EMAIL_GR_CODE = ALERT_GROUP FROM CAMPAIGN WITH(NOLOCK) WHERE CMP_IDENTIFIER = @CMP_IDENTIFIER;
	END

	IF @CLIENT_PORTAL_USER_ID > 0
	BEGIN
		SELECT @CP_USER_CL_CODE = CL_CODE, @EMAIL_GR_CODE = ALERT_GROUP_CODE, @CP_USER_AGENCY_CONTACT_CODE = AGENCY_CONTACT_CODE FROM CP_USER WITH(NOLOCK) WHERE CDP_CONTACT_ID = @CLIENT_PORTAL_USER_ID;
		SET @IS_CLIENT_PORTAL = 1;
	END

	DECLARE @FINAL_RETURN TABLE (
						CODE VARCHAR(MAX) COLLATE SQL_Latin1_General_CP1_CS_AS, 
						FULL_NAME VARCHAR(1000), 
						IS_EMPLOYEE BIT, 
						IS_CLIENT_CONTACT BIT,
						IN_ALERT_GROUP BIT,
						CS_USERID INT,
						EMP_PICTURE IMAGE
						);
	DECLARE @RECIPIENTS TABLE (
						CODE VARCHAR(MAX) COLLATE SQL_Latin1_General_CP1_CS_AS, 
						FULL_NAME VARCHAR(1000), 
						IS_EMPLOYEE BIT, 
						IS_CLIENT_CONTACT BIT,
						IS_IN_EMAIL_GROUP BIT,
						CS_USER_ID INT
						);
	DECLARE @EMPLOYEE_RECIPIENTS TABLE (
						 EMPGROUP VARCHAR(255) COLLATE SQL_Latin1_General_CP1_CS_AS, 
						 EMP_CODE VARCHAR(150) COLLATE SQL_Latin1_General_CP1_CS_AS, 
						 DESCRIPTION VARCHAR(2000),
						 Checked BIT, 
						 REC_ORDER INT,
						 EMP_FML VARCHAR(2000)
						 );
	DECLARE @CLIENT_PORTAL_USER_RECIPIENTS TABLE (
						 EMPGROUP VARCHAR(255) COLLATE SQL_Latin1_General_CP1_CS_AS, 
						 EMP_CODE VARCHAR(150) COLLATE SQL_Latin1_General_CP1_CS_AS, 
						 DESCRIPTION VARCHAR(2000),
						 Checked BIT
						 );
	DECLARE @EMAIL_GROUP_RECIPIENTS TABLE (
						 EMPGROUP VARCHAR(255) COLLATE SQL_Latin1_General_CP1_CS_AS, 
						 EMP_CODE VARCHAR(150) COLLATE SQL_Latin1_General_CP1_CS_AS, 
						 DESCRIPTION VARCHAR(2000),
						 Checked BIT, 
						 REC_ORDER INT,
						 EMP_FML VARCHAR(2000)
						 );
	DECLARE @CLIENT_CONTACT_RECIPIENTS TABLE (
						CDP_CONTACT_ID INT,
						CONT_FML VARCHAR(2000),
						CP_ALERTS TINYINT,
						EMAIL_RCPT INT,
						CONT_CODE VARCHAR(6) COLLATE SQL_Latin1_General_CP1_CS_AS
						);

	IF @IS_CLIENT_PORTAL = 1
	BEGIN
		
		INSERT INTO @CLIENT_PORTAL_USER_RECIPIENTS
		EXEC usp_cp_GetAlertGroup @EMAIL_GR_CODE;

		INSERT INTO @EMAIL_GROUP_RECIPIENTS (EMPGROUP, EMP_CODE, [DESCRIPTION], Checked)
		SELECT EMPGROUP, EMP_CODE, [DESCRIPTION], Checked FROM @CLIENT_PORTAL_USER_RECIPIENTS WHERE Checked = 1;

		INSERT INTO @RECIPIENTS
		SELECT DISTINCT ER.EMP_CODE, ISNULL(EMPLOYEE.EMP_FNAME + ' ', '') + ISNULL(EMPLOYEE.EMP_MI + '. ', '') + ISNULL(EMPLOYEE.EMP_LNAME, ''), 1, 0, [Checked], 0
		FROM @CLIENT_PORTAL_USER_RECIPIENTS AS ER INNER JOIN EMPLOYEE WITH(NOLOCK) ON ER.EMP_CODE = EMPLOYEE.EMP_CODE;

	END
	ELSE
	BEGIN

		INSERT INTO @EMPLOYEE_RECIPIENTS
		SELECT EMPGROUP, EMPCODE, [DESCRIPTION], Checked, REC_ORDER, EMP_FML FROM [dbo].[advtf_get_email_groups] (@EMAIL_GR_CODE, @JOB_NUMBER, @JOB_COMPONENT_NBR, 1);

		INSERT INTO @EMAIL_GROUP_RECIPIENTS (EMPGROUP, EMP_CODE, [DESCRIPTION], Checked, REC_ORDER, EMP_FML)
		SELECT EMPGROUP, EMP_CODE, [DESCRIPTION], Checked, REC_ORDER, EMP_FML FROM @EMPLOYEE_RECIPIENTS WHERE Checked = 1;

		INSERT INTO @RECIPIENTS
		SELECT DISTINCT ER.EMP_CODE, EMP_FML, 1, 0, [Checked], EMPLOYEE.CS_USERID FROM @EMPLOYEE_RECIPIENTS AS ER INNER JOIN EMPLOYEE WITH(NOLOCK) ON ER.EMP_CODE = EMPLOYEE.EMP_CODE;

	END

	IF @IS_CLIENT_PORTAL = 1 AND (NOT EXISTS (SELECT 1 FROM @RECIPIENTS R WHERE R.CODE = @CP_USER_AGENCY_CONTACT_CODE))
	BEGIN

		INSERT INTO @RECIPIENTS
		SELECT EMPLOYEE.EMP_CODE, ISNULL(EMPLOYEE.EMP_FNAME + ' ', '') + ISNULL(EMPLOYEE.EMP_MI + '. ', '') + ISNULL(EMPLOYEE.EMP_LNAME, ''), 1, 0, 0, EMPLOYEE.CS_USERID
		FROM EMPLOYEE WITH(NOLOCK) 
		WHERE EMPLOYEE.EMP_CODE = @CP_USER_AGENCY_CONTACT_CODE
		AND (EMPLOYEE.EMP_TERM_DATE IS NULL OR EMPLOYEE.EMP_TERM_DATE >= GETDATE());

	END

	IF @ALERT_ID > 0
	BEGIN

		DECLARE @CURRENT_ALERT_RECIPIENTS TABLE (
							EMP_CODE VARCHAR(6)
							);
		INSERT INTO @CURRENT_ALERT_RECIPIENTS
		SELECT A.EMP_CODE
		FROM (
			SELECT EMP_CODE FROM ALERT_RCPT WHERE (CURRENT_NOTIFY IS NULL OR CURRENT_NOTIFY = 0) AND ALERT_ID = @ALERT_ID
			UNION
			SELECT EMP_CODE FROM ALERT_RCPT_DISMISSED WHERE (CURRENT_NOTIFY IS NULL OR CURRENT_NOTIFY = 0) AND ALERT_ID = @ALERT_ID
		) AS A;
		
		IF @OfficeRestrictions > 0
		BEGIN
			INSERT INTO @FINAL_RETURN(CODE, FULL_NAME, IS_EMPLOYEE, IS_CLIENT_CONTACT, IN_ALERT_GROUP)
			SELECT 
				DISTINCT 
				R.CODE, R.FULL_NAME, R.IS_EMPLOYEE, R.IS_CLIENT_CONTACT,
				CASE WHEN EG.Checked = 1 THEN 1 ELSE 0 END AS IN_ALERT_GROUP
			FROM 
				@RECIPIENTS AS R 
				LEFT OUTER JOIN @EMAIL_GROUP_RECIPIENTS AS EG ON R.CODE = EG.EMP_CODE
				INNER JOIN EMPLOYEE EMP ON R.CODE = EMP.EMP_CODE
				INNER JOIN EMP_OFFICE EO ON EMP.OFFICE_CODE = EO.OFFICE_CODE
			WHERE
				R.CODE NOT IN (SELECT DISTINCT E.EMP_CODE FROM @CURRENT_ALERT_RECIPIENTS AS E)
				AND (EMP.EMP_TERM_DATE IS NULL OR EMP.EMP_TERM_DATE >= GETDATE()) AND EO.EMP_CODE = @EMP_CODE
			ORDER BY
				IN_ALERT_GROUP DESC, FULL_NAME;
		END
		ELSE
		BEGIN
			INSERT INTO @FINAL_RETURN(CODE, FULL_NAME, IS_EMPLOYEE, IS_CLIENT_CONTACT, IN_ALERT_GROUP)
			SELECT 
				DISTINCT 
				R.CODE, R.FULL_NAME, R.IS_EMPLOYEE, R.IS_CLIENT_CONTACT,
				CASE WHEN EG.Checked = 1 THEN 1 ELSE 0 END AS IN_ALERT_GROUP
			FROM 
				@RECIPIENTS AS R 
				LEFT OUTER JOIN @EMAIL_GROUP_RECIPIENTS AS EG ON R.CODE = EG.EMP_CODE
				INNER JOIN EMPLOYEE EMP ON R.CODE = EMP.EMP_CODE
			WHERE
				R.CODE NOT IN (SELECT DISTINCT E.EMP_CODE FROM @CURRENT_ALERT_RECIPIENTS AS E)
				AND (EMP.EMP_TERM_DATE IS NULL OR EMP.EMP_TERM_DATE >= GETDATE())
			ORDER BY
				IN_ALERT_GROUP DESC, FULL_NAME;
		END
		

	END
	ELSE
	BEGIN
		IF @OfficeRestrictions > 0
		BEGIN
			INSERT INTO @FINAL_RETURN(CODE, FULL_NAME, IS_EMPLOYEE, IS_CLIENT_CONTACT, IN_ALERT_GROUP)
			SELECT 
				DISTINCT 
				R.CODE, R.FULL_NAME, R.IS_EMPLOYEE, R.IS_CLIENT_CONTACT,
				CASE WHEN EG.Checked = 1 THEN 1 ELSE 0 END AS IN_ALERT_GROUP
			FROM 
				@RECIPIENTS AS R 
				LEFT OUTER JOIN @EMAIL_GROUP_RECIPIENTS AS EG ON R.CODE = EG.EMP_CODE
				INNER JOIN EMPLOYEE EMP ON R.CODE = EMP.EMP_CODE
				INNER JOIN EMP_OFFICE EO ON EMP.OFFICE_CODE = EO.OFFICE_CODE
			WHERE
				(EMP.EMP_TERM_DATE IS NULL OR EMP.EMP_TERM_DATE >= GETDATE()) AND EO.EMP_CODE = @EMP_CODE
			ORDER BY
				IN_ALERT_GROUP DESC, FULL_NAME;
		END
		ELSE
		BEGIN

			INSERT INTO @FINAL_RETURN(CODE, FULL_NAME, IS_EMPLOYEE, IS_CLIENT_CONTACT, IN_ALERT_GROUP)
			SELECT 
				DISTINCT 
				R.CODE, R.FULL_NAME, R.IS_EMPLOYEE, R.IS_CLIENT_CONTACT,
				CASE WHEN EG.Checked = 1 THEN 1 ELSE 0 END AS IN_ALERT_GROUP
			FROM 
				@RECIPIENTS AS R 
				LEFT OUTER JOIN @EMAIL_GROUP_RECIPIENTS AS EG ON R.CODE = EG.EMP_CODE
				INNER JOIN EMPLOYEE EMP ON R.CODE = EMP.EMP_CODE
			WHERE
				(EMP.EMP_TERM_DATE IS NULL OR EMP.EMP_TERM_DATE >= GETDATE())
			ORDER BY
				IN_ALERT_GROUP DESC, FULL_NAME;
		END
	END

	IF NOT @CL_CODE IS NULL
	BEGIN

		INSERT INTO @CLIENT_CONTACT_RECIPIENTS
		EXEC [dbo].[usp_wv_getCPUsersAlerts] @Client = @CL_CODE, @Product = @PRD_CODE, @Division = @DIV_CODE;

		INSERT INTO @FINAL_RETURN(CODE, FULL_NAME, IS_EMPLOYEE, IS_CLIENT_CONTACT)
		SELECT DISTINCT CAST(CDP_CONTACT_ID AS VARCHAR), CONT_FML + ' (CC)', 0, 1 FROM @CLIENT_CONTACT_RECIPIENTS WHERE CP_ALERTS = 1;

	END

	UPDATE @FINAL_RETURN SET CS_USERID = E.CS_USERID
	FROM
		@FINAL_RETURN FR INNER JOIN EMPLOYEE E ON FR.CODE = E.EMP_CODE;
	UPDATE @FINAL_RETURN SET CS_USERID = CP.CS_USERID
	FROM
		@FINAL_RETURN FR INNER JOIN CP_USER CP ON FR.CODE = CAST(CP.CDP_CONTACT_ID AS VARCHAR);

	UPDATE @FINAL_RETURN SET EMP_PICTURE = EP.EMP_IMAGE
	FROM
		@FINAL_RETURN FR INNER JOIN EMPLOYEE_PICTURE EP ON FR.CODE = EP.EMP_CODE
	WHERE FR.IS_EMPLOYEE = 1;

	----  CAN CP USERS UPLOAD IMAGE????
	--UPDATE @FINAL_RETURN SET EMP_PICTURE = EP.EMP_IMAGE
	--FROM
	--	@FINAL_RETURN FR INNER JOIN EMPLOYEE_PICTURE EP ON FR.CODE = EP.EMP_CODE
	--WHERE FR.IS_EMPLOYEE = 0;

	
	IF @IS_REVIEWERS = 1
	BEGIN
		
		IF DATALENGTH(@EMAIL_GR_CODE) > 0
		BEGIN
		   DELETE FROM @FINAL_RETURN WHERE CODE NOT IN (
			   SELECT DISTINCT EMP_CODE 
			   FROM EMAIL_GROUP 
			   WHERE (EMAIL_GR_CODE = @EMAIL_GR_CODE) AND (INACTIVE_FLAG IS NULL OR INACTIVE_FLAG = 0)
			   UNION
			   SELECT DISTINCT CAST(CDP_CONTACT_ID AS VARCHAR) 
			   FROM CDP_CONTACT_HDR CCH INNER JOIN EMAIL_GROUP EG ON CCH.CONT_CODE = EG.EMP_CODE 
			   WHERE EG.EMAIL_GR_CODE = @EMAIL_GR_CODE AND CCH.CL_CODE = @CL_CODE
		   )  
		END

		SELECT 
			FR.CODE + '|' + CAST(FR.CS_USERID AS VARCHAR) AS Code,
			FR.FULL_NAME AS FullName,
			FR.IS_EMPLOYEE AS IsEmployee,
			FR.IS_CLIENT_CONTACT AS IsClientContact,
			CAST(FR.IN_ALERT_GROUP AS BIT) AS InAlertGroup,
			FR.CS_USERID AS ConceptShareUserID,
			CASE 
				WHEN FR.CS_USERID IS NULL THEN CAST(0 AS BIT)
				ELSE CAST(1 AS BIT)
			END AS IsConceptShareUser,
			FR.EMP_PICTURE AS Picture
		FROM 
			@FINAL_RETURN FR
		WHERE
			NOT FR.CS_USERID IS NULL;
	END
	ELSE
	BEGIN
		SELECT 
			FR.CODE AS Code,
			FR.FULL_NAME AS FullName,
			FR.IS_EMPLOYEE AS IsEmployee,
			FR.IS_CLIENT_CONTACT AS IsClientContact,
			CAST(FR.IN_ALERT_GROUP AS BIT) AS InAlertGroup,
			FR.CS_USERID AS ConceptShareUserID,
			CASE 
				WHEN FR.CS_USERID IS NULL THEN CAST(0 AS BIT)
				ELSE CAST(1 AS BIT)
			END AS IsConceptShareUser,
			FR.EMP_PICTURE AS Picture
		FROM 
			@FINAL_RETURN AS FR;
	END
/*=========== QUERY ===========*/
END
GO