CREATE PROCEDURE [dbo].[advsp_media_broadcast_worksheet_get_by_order_number]
	@ORDER_NBR int
AS

DECLARE @MaxRevisionNumber int,
		@MediaBroadcastWorksheetMarketID int,
		@MediaBroadcastWorksheetID int,
		@IsEditable bit = 0,
		@OrderIsOnWorksheet bit,
		@PrimaryMediaDemoID int,
		@MarketNumber int,
		@RatingsService varchar(20)

IF EXISTS(SELECT 1
			FROM dbo.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_DATE MBWMDD
				INNER JOIN dbo.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL MBWMD ON MBWMDD.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_ID = MBWMD.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_ID 
			WHERE ORDER_NBR = @ORDER_NBR)
	SET @OrderIsOnWorksheet = 1
ELSE
	SET @OrderIsOnWorksheet = 0

SELECT @MaxRevisionNumber = MBWMD.REVISION_NUMBER 
FROM dbo.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_DATE MBWMDD
	INNER JOIN dbo.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL MBWMD ON MBWMDD.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_ID = MBWMD.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_ID 
WHERE ORDER_NBR = @ORDER_NBR

IF NOT EXISTS(SELECT 1
				FROM dbo.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_DATE MBWMDD
					INNER JOIN dbo.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL MBWMD ON MBWMDD.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_ID = MBWMD.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_ID 
				WHERE MBWMDD.ORDER_NBR = @ORDER_NBR
				AND MBWMD.REVISION_NUMBER = @MaxRevisionNumber)
	SET @IsEditable = 0
ELSE IF EXISTS(SELECT 1
				FROM dbo.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_DATE MBWMDD
					INNER JOIN dbo.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL MBWMD ON MBWMDD.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_ID = MBWMD.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_ID 
				WHERE MBWMDD.ORDER_NBR = @ORDER_NBR
				AND MBWMD.REVISION_NUMBER = @MaxRevisionNumber
				AND MBWMDD.MEDIA_BROADCAST_WORKSHEET_ORDER_STATUS_ID = 4)
	SET @IsEditable = 0
ELSE
	SET @IsEditable = 1
	
SELECT
	@MediaBroadcastWorksheetMarketID = MBWMD.MEDIA_BROADCAST_WORKSHEET_MARKET_ID,
	@MediaBroadcastWorksheetID = MBWM.MEDIA_BROADCAST_WORKSHEET_ID,
	@PrimaryMediaDemoID = MBW.PRIMARY_MEDIA_DEMO_ID,
	@MarketNumber = CASE 
						WHEN MBW.RATINGS_SERVICE_ID = 1 THEN (SELECT NIELSEN_TV_CODE FROM dbo.MARKET WHERE MARKET_CODE = MBWM.MARKET_CODE)
						WHEN MBW.RATINGS_SERVICE_ID = 2 THEN (SELECT COMSCORE_NEW_MARKET_NUMBER FROM dbo.MARKET WHERE MARKET_CODE = MBWM.MARKET_CODE)
						ELSE NULL
					END,
	@RatingsService = CASE
						WHEN MBW.MEDIA_TYPE_CODE = 'T' THEN (SELECT [NAME] FROM dbo.RATINGS_SERVICE WHERE RATINGS_SERVICE_ID = MBW.RATINGS_SERVICE_ID)
						WHEN MBW.MEDIA_TYPE_CODE = 'R' THEN
							CASE 
								WHEN MBWM.EXTERNAL_RADIO_SOURCE = 0 THEN 'Nielsen'
								WHEN MBWM.EXTERNAL_RADIO_SOURCE = 1 THEN 'Eastlan'
							END
					  END
FROM dbo.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_DATE MBWMDD
	INNER JOIN dbo.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL MBWMD ON MBWMDD.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_ID = MBWMD.MEDIA_BROADCAST_WORKSHEET_MARKET_DETAIL_ID 
	INNER JOIN dbo.MEDIA_BROADCAST_WORKSHEET_MARKET MBWM ON MBWMD.MEDIA_BROADCAST_WORKSHEET_MARKET_ID = MBWM.MEDIA_BROADCAST_WORKSHEET_MARKET_ID 
	INNER JOIN dbo.MEDIA_BROADCAST_WORKSHEET MBW ON MBWM.MEDIA_BROADCAST_WORKSHEET_ID = MBW.MEDIA_BROADCAST_WORKSHEET_ID
WHERE MBWMDD.ORDER_NBR = @ORDER_NBR
AND MBWMD.REVISION_NUMBER = @MaxRevisionNumber

SELECT
	[MediaBroadcastWorksheetMarketID] = @MediaBroadcastWorksheetMarketID,
	[MediaBroadcastWorksheetID] =	@MediaBroadcastWorksheetID,
	[IsEditable] = @IsEditable,
	[OrderIsOnWorksheet] = @OrderIsOnWorksheet,
	[OrderNumber] = @ORDER_NBR,
	[PrimaryMediaDemoID] = @PrimaryMediaDemoID,
	[MarketNumber] = @MarketNumber,
	[PrimaryMediaDemographicDescription] = (SELECT [DESCRIPTION] FROM dbo.MEDIA_DEMO WHERE MEDIA_DEMO_ID = @PrimaryMediaDemoID),
	[RatingsService] = @RatingsService
GO
